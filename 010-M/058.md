Suave Tin Poodle

medium

# Denial of Service: `Auction.sol#_computeTotalRewards` reverts with `INVALID_REWARD_TOTAL` for a `totalBPS` of `10_000`.

## Summary

When attempting to [`_settleAuction()`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L244), calls to [`_computeTotalRewards`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L465) will revert with `INVALID_REWARD_TOTAL` if the reward split is _exactly_ `10_000` BPS.

## Vulnerability Detail

Calls to `_computeTotalRewards` will fail if the split amount is greater than **or equal to** the maximum percentage:

```solidity
// Calculate the total rewards percentage
uint256 totalBPS = _founderRewardBps + referralRewardsBPS + builderRewardsBPS;

// Verify percentage is not more than 100
if (totalBPS >= BPS_PER_100_PERCENT) /// @audit
    revert INVALID_REWARD_TOTAL();
```

## Impact

Auctions designed to maximize rewards for all stakeholders aside from the treasury address will not be possible to settle once they have accrued a nonzero `highestBid`.

## Code Snippet

```solidity
/// @notice Computes the total rewards for a bid
/// @param _currentBidRefferal The referral for the current bid
/// @param _finalBidAmount The final bid amount
/// @param _founderRewardBps The reward to be paid to the founder in BPS
function _computeTotalRewards(
    address _currentBidRefferal,
    uint256 _finalBidAmount,
    uint256 _founderRewardBps
) internal view returns (RewardSplits memory split) {
    // Get global builder recipient from manager
    address builderRecipient = manager.builderRewardsRecipient();

    // Calculate the total rewards percentage
    uint256 totalBPS = _founderRewardBps + referralRewardsBPS + builderRewardsBPS;

    // Verify percentage is not more than 100
    if (totalBPS >= BPS_PER_100_PERCENT) {
        revert INVALID_REWARD_TOTAL();
    }

    // Calulate total rewards
    split.totalRewards = (_finalBidAmount * totalBPS) / BPS_PER_100_PERCENT;

    // Check if founder reward is enabled
    bool hasFounderReward = _founderRewardBps > 0 && founderReward.recipient != address(0);

    // Set array size based on if founder reward is enabled
    uint256 arraySize = hasFounderReward ? 3 : 2;

    // Initialize arrays
    split.recipients = new address[](arraySize);
    split.amounts = new uint256[](arraySize);
    split.reasons = new bytes4[](arraySize);

    // Set builder reward
    split.recipients[0] = builderRecipient;
    split.amounts[0] = (_finalBidAmount * builderRewardsBPS) / BPS_PER_100_PERCENT;

    // Set referral reward
    split.recipients[1] = _currentBidRefferal != address(0) ? _currentBidRefferal : builderRecipient;
    split.amounts[1] = (_finalBidAmount * referralRewardsBPS) / BPS_PER_100_PERCENT;

    // Set founder reward if enabled
    if (hasFounderReward) {
        split.recipients[2] = founderReward.recipient;
        split.amounts[2] = (_finalBidAmount * _founderRewardBps) / BPS_PER_100_PERCENT;
    }
}
```

## Tool used

Manual Review, Visual Studio Code

## Recommendation

If the intention was that callers should never exceed the `BPS_PER_100_PERCENT`, then modify the conditional to support this:

```diff
// Verify percentage is not more than 100
- if (totalBPS >= BPS_PER_100_PERCENT)
+ if (totalBPS > BPS_PER_100_PERCENT)
    revert INVALID_REWARD_TOTAL();
```

Alternatively, if the intention of this function was to at least guarantee there was _some_ value to transfer back to the treasury [via the following call to `_handleOutgoingTransfer`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L276), enforce a minimum `treasuryRewardBPS`, or alternatively by setting a cap on `referralRewardsBPS`, which is uncapped.
