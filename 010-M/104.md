Melodic Menthol Starfish

high

# Users' funds can be locked in the ```Auction``` contract if ```builderRewardsBPS``` and ```referralRewardsBPS``` add up to 100 percent.

## Summary

When settling an auction, if ```builderRewardsBPS``` and ```referralRewardsBPS``` (set up at build time) add up to ```BPS_PER_100_PERCENT``` (or above), the action will revert and the auction will never be settled (as ```builderRewardsBPS``` and ```referralRewardsBPS``` are not modifiable with a setter function afterwards).

## Vulnerability Detail

Let's assume ```owner``` sets  ```builderRewardsBPS``` to ```5_000``` (i.e. 50 percent) and ```referralRewardsBPS``` to ```5_000``` (i.e. 50 percent) when deploying the [```Auction```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol) contract (which could seem sensible as we could imagine a 50/50 split between builder and referral).

Let's now say Alice places a bid for 10 ETH in this auction.
If Bob places a bid for 15 ETH, the 10 ETH will be refunded to Alice via the [```_handleOutgoingTransfer```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L517-L543) function as part of the [```_createBid```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L158-L231) method.

Let's now assume we try to settle the ```Auction``` with the [```settleCurrentAndCreateNewAuction```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L238-L241) function. The [```_settleAuction```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L244-L289) function is then called.

The condition ```if (_auction.highestBidder != address(0))``` is met as ```_auction.highestBidder``` is Bob.
The condition ```if (highestBid != 0)``` is also met as ```highestBid``` is 15 ETH.

We then try to execute the line 

```solidity
RewardSplits memory split = _computeTotalRewards(currentBidReferral, highestBid, founderReward.percentBps);
```

Let's assume ```founderReward.percentBps``` to be zero for the sake of simplicity here.

In the [```_computeTotalRewards```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L465-L508) function, we then have 

```solidity
uint256 totalBPS = _founderRewardBps + referralRewardsBPS + builderRewardsBPS;
```

i.e. ```totalBPS = 10_000```

meaning the function will revert because of the next condition

```solidity
 if (totalBPS >= BPS_PER_100_PERCENT) {
            revert INVALID_REWARD_TOTAL();
 }
```

We will then not be able to settle the auction and Bob will not get the ```NFT``` as the ```token.transferFrom(address(this), _auction.highestBidder, _auction.tokenId);``` line of the [```_settleAuction```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L244-L289) function will never get executed. He will also not get his bid back and will lose his 15 ETH.

## Impact

Because of this vulnerability, the owner could mistakenly set values for ```builderRewardsBPS``` and ```referralRewardsBPS``` with a total greater than ```BPS_PER_100_PERCENT```.
Users will then be able to place bids (hence transferring funds to the contract).
The last ```highestBidder``` will then never get refunded nor get the ```NFT``` as the [```_computeTotalRewards```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L465-L508) function will always revert. This vulnerability should be marked as HIGH as potentially leading to a loss of funds for the user.

## Code Snippet

Below is the [```_computeTotalRewards```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L465-L508) function of the [```Auction```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol) contract

```solidity
function _computeTotalRewards(
        address _currentBidRefferal,
        uint256 _finalBidAmount,
        uint256 _founderRewardBps
    ) internal view returns (RewardSplits memory split) {
        // Get global builder recipient from manager
        address builderRecipient = manager.builderRewardsRecipient();

        // Calculate the total rewards percentage
        uint256 totalBPS = _founderRewardBps + referralRewardsBPS + builderRewardsBPS;

        // Verify percentage is not more than 100
        if (totalBPS >= BPS_PER_100_PERCENT) {
            revert INVALID_REWARD_TOTAL();
        }

        // Calulate total rewards
        split.totalRewards = (_finalBidAmount * totalBPS) / BPS_PER_100_PERCENT;

        // Check if founder reward is enabled
        bool hasFounderReward = _founderRewardBps > 0 && founderReward.recipient != address(0);

        // Set array size based on if founder reward is enabled
        uint256 arraySize = hasFounderReward ? 3 : 2;

        // Initialize arrays
        split.recipients = new address[](arraySize);
        split.amounts = new uint256[](arraySize);
        split.reasons = new bytes4[](arraySize);

        // Set builder reward
        split.recipients[0] = builderRecipient;
        split.amounts[0] = (_finalBidAmount * builderRewardsBPS) / BPS_PER_100_PERCENT;

        // Set referral reward
        split.recipients[1] = _currentBidRefferal != address(0) ? _currentBidRefferal : builderRecipient;
        split.amounts[1] = (_finalBidAmount * referralRewardsBPS) / BPS_PER_100_PERCENT;

        // Set founder reward if enabled
        if (hasFounderReward) {
            split.recipients[2] = founderReward.recipient;
            split.amounts[2] = (_finalBidAmount * _founderRewardBps) / BPS_PER_100_PERCENT;
        }
 }
```

## Tool used

Manual Review

## Recommendation

To fix this issue, we can either modify the ```constructor``` to make sure the sum of ```builderRewardsBPS``` and ```referralRewardsBPS``` is strictly lesser than 100 percent as per below

```solidity
constructor(
        address _manager,
        address _rewardsManager,
        address _weth,
        uint16 _builderRewardsBPS,
        uint16 _referralRewardsBPS
    ) payable initializer {
        manager = Manager(_manager);
        rewardsManager = IProtocolRewards(_rewardsManager);
        WETH = _weth;
        builderRewardsBPS = _builderRewardsBPS;
        referralRewardsBPS = _referralRewardsBPS;
        require(_builderRewardsBPS+_referralRewardsBPS < BPS_PER_100_PERCENT, "invalid reward split");
}
```

Alternatively, we can change the condition in the [```_computeTotalRewards```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L465-L508) function to 

```solidity
// Verify percentage is not more than 100
if (totalBPS > BPS_PER_100_PERCENT) {
       revert INVALID_REWARD_TOTAL();
}
``` 

to ensure a ```totalBPS``` value of 100 percent is considered valid.

We should also define setter functions for ```owner``` to be able to modify ```builderRewardsBPS``` and ```referralRewardsBPS``` values after deployment.