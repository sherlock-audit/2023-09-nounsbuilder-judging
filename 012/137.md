Furry Peach Turkey

high

# [High] Changing of Metadata Renderer will cause losing of existing tokens URI

## Summary
With this update, new core functionality for [Custom Metadata Renderers](https://hackmd.io/peXISQ2CSQOwRGmvpUpK9A?view#Custom-Metadata-Renderers) has been provided. 
Previously, this change could only be made through UUPS upgrade.

MetadataRenderer needed to retrieve metadata by tokenId like [tokenUri()](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L318-L320).

Render update can be done in few ways:
With [Manager.sol](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/manager/Manager.sol#L172-L191) 
With [Token.sol](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L454-L456)

## Vulnerability Detail
Itself both places are protected and can be called by owner or manager, but there is no restriction to change renderer when tokens already minted which can cause an issue with fetching tokenUri.

Even more, current abstraction of [IBaseMetadata](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/metadata/interfaces/IBaseMetadata.sol) designed in such way that it's mandatory to call [onMinted()](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/metadata/interfaces/IBaseMetadata.sol#L32) function when token created to generate token related attributes.

As example let's check current proposed implementation of MetadataRenderer.
With current provided implementation of MetadataRenderer we can see that during [tokenUri()](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/metadata/MetadataRenderer.sol#L366-L390) contract will try to find existing attributes with [getAttributes()](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/metadata/MetadataRenderer.sol#L281-L341) and will revert with TOKEN_NOT_MINTED if there is no **attributes[_tokenId]**.
```solidity
        // Get the token's generated attributes
        uint16[16] memory tokenAttributes = attributes[_tokenId];   // @audit storage level variable 

        // Cache the number of properties when the token was minted
        uint256 numProperties = tokenAttributes[0];

        // Ensure the given token was minted
        if (numProperties == 0) revert TOKEN_NOT_MINTED(_tokenId);
```

And the only way to add **attributes[_tokenId]** is calling [onMinted()](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/metadata/MetadataRenderer.sol#L242-L277) which can be called by Token only during minting.
```solidity
        // Ensure the caller is the token contract
        if (msg.sender != settings.token) revert ONLY_TOKEN();

        // Compute some randomness for the token id
        uint256 seed = _generateSeed(_tokenId);

        // Get the pointer to store generated attributes
        uint16[16] storage tokenAttributes = attributes[_tokenId];

        // Cache the total number of properties available
        uint256 numProperties = properties.length; // 

        if (numProperties == 0) {
            return false;
        }

        // Store the total as reference in the first slot of the token's array of attributes
        tokenAttributes[0] = uint16(numProperties);
        ...
```

## Impact
If we will try to change MetadataRenderer after tokens will be minted, it wouldn't be possible to get tokenUri and we will get TOKEN_NOT_MINTED error. 
And there is no easy option to restore affected tokens because of onMinted() protection `solidity if (msg.sender != settings.token) revert ONLY_TOKEN();` so this state will be irreparable. (Potentially you can try to rollback to the previous MetadataRenderer until new new token is not minted, but really, it's not a proper mitigation).

Severity justification: 
Marked it as High severity issue because [Custom Metadata Renderers](https://hackmd.io/peXISQ2CSQOwRGmvpUpK9A?view#Custom-Metadata-Renderers) is a new core functionality of this update and issue will lead to lose user's acquisitions.

Steps:
1) Initialize DAO components with metadata renderer.
2) Mint some tokens.
3) Set new metadata renderer.
4) Call [tokenUri()](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L318-L320) for existing tokens.
Actual result:
5) TOKEN_NOT_MINTED thrown.
Expected result:
5) Token uri retrieved.

## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L318-L320
```solidity
    function tokenURI(uint256 _tokenId) public view override(IToken, ERC721) returns (string memory) {
        return settings.metadataRenderer.tokenURI(_tokenId);
    }
```
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L248-L259
```solidity
    function _mint(address _to, uint256 _tokenId) internal override {
        // Mint the token
        super._mint(_to, _tokenId);

        // Increment the total supply
        unchecked {
            ++settings.totalSupply;
        }

        // Generate the token attributes
        if (!settings.metadataRenderer.onMinted(_tokenId)) revert NO_METADATA_GENERATED();
    }
```

https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/metadata/interfaces/IBaseMetadata.sol#L10-L46
```solidity
    /// @notice Generates attributes for a token upon mint
    /// @param tokenId The ERC-721 token id
    function onMinted(uint256 tokenId) external returns (bool);

    /// @notice The token URI
    /// @param tokenId The ERC-721 token id
    function tokenURI(uint256 tokenId) external view returns (string memory);

    /// @notice The contract URI
    function contractURI() external view returns (string memory);

    /// @notice The associated ERC-721 token
    function token() external view returns (address);

    /// @notice Get metadata owner address
    function owner() external view returns (address);
```

## Tool used

Manual Review

## Recommendation
Current way to handle created tokens with onMinted() worked with upgradable renderer because storage state for upgrades will be stale across old and new version. But with setter approach it's not the case. 

I have two recommendations here.
1) Restrict the changing MetadataRenderer after tokens have been minted. It's also potentially better for user who will get stable tokenUri after acquisitions of his tokens.
2) Add ability to move existing data from one renderer to another.