Exotic Aquamarine Giraffe

high

# Founders will not be able to mint their token share if their assigned token ID is in the hundreds (i.e., 100, 200).

## Summary

If a founder is assigned a token ID in the exact hundreds (e.g., 100, 200) through the `Token::_addFounders(...)` method, they will not be able to mint their token share for that ID, resulting in a loss of value for the founder.

## Vulnerability Detail

`Token::_addFounders(...)` is responsible for adding the founders for the `DAO` inside the token contract. This is also responsible for assigning the token IDs that founder will receive in every 100 tokens minted. The IDs will be corresponding to the share percent of the founder in the DAO (i.e, if `ownershipPct` is 10, he will get 10 reserved tokens in every 100 tokens minted). The calculation for these token IDs that will be assigned to the founders depends on three variable inside the function. `baseTokenId`, `schedule` and `reservedUntilTokenId`. Here is how each one of them is calculated:

In the first iteration, `baseTokenId` will be equal to this:

```solidity
                uint256 baseTokenId = reservedUntilTokenId;
```

Github: [161](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L161)

`schedule` is calculate like this:

```solidity
                uint256 schedule = 100 / founderPct;
```

Github: [158](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L158)

The value for `schedule` is calculated once for each user. So this will remain same as it is only used in the nested loop and not changed there as well.

And value inside the `reservedUntilTokenId` is decided at the time of DAO creation (i.e., during the `Token` contract Initialization). So this will be fixed:

```solidity

function deploy(
        FounderParams[] calldata _founderParams,
        TokenParams calldata _tokenParams,
        AuctionParams calldata _auctionParams,
        GovParams calldata _govParams
    ) external returns (address token, address metadata, address auction, address treasury, address governor) {


        ...


        IToken(token).initialize({
            founders: _founderParams,
            initStrings: _tokenParams.initStrings,
@>            reservedUntilTokenId: _tokenParams.reservedUntilTokenId,
            metadataRenderer: metadata,
            auction: auction,
            initialOwner: founder
        });


       ...

    }
```

GitHub: [36](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/manager/Manager.sol#L136)

All of these values will be calculated as mentioned above in the first iteration. In the next Iterations, the value for `baseTokenId` will change on the basis of this calculation:

```solidity
                for (uint256 j; j < founderPct; ++j) {
                    // Get the available token id
                    baseTokenId = _getNextTokenId(baseTokenId);

                    // Store the founder as the recipient
                    tokenRecipient[baseTokenId] = newFounder;

                    emit MintScheduled(baseTokenId, founderId, newFounder);

                    // Update the base token id
                    baseTokenId = (baseTokenId + schedule) % 100;
                }
```

GitHub: [165-175](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L164C2-L175C18)

### Now to see how the calculation works let's take an example:

#### Calculation for `Token::_addFounder(...)` through `Token::Initilaize(...)`:

- let's say `reservedUntilTokenId = 100`, and there is only 1 founder who has `2 percent` share (`foudnerPct`) .
- When `Token` is deployed with the above data, this data will be sent to the `Token::initialize(...)` for the setup.
- In the `Token::initialize(...)` function, call will be made to `Token::_addFounders(...)` function to add the founders. This `Token::_addFounder(...)` has a nested loop. The outer loop will run for each founders (i.e., 1 time in our case) and the inner loop will run for each `founderPct` (i.e., 2 time in our case) to reserve the token IDs for the founder.
- In the first iteration, the following will be calculated in the outer loop:

  - `schedule` = `100 / founderPct` = `50`
  - `baseTokenId` = `reservedUntilTokenId` = `100` // this is not the actual id that will be assigned to the founder. it will change

- Then the execution flow will move inside the inner loop that will run for each `founderPct` (i.e., 2 times in our case) and following calculation will be done:

  - `baseTokenId` = `_getNextTokenId(baseTokenId)` = `_getNextTokenId(100)`
  - This is [`Token::_getNextTokenId(baseTokenId)`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L186) function:

    ```solidity
    function _getNextTokenId(uint256 _tokenId) internal view returns (uint256) {
      unchecked {
        while (tokenRecipient[_tokenId].wallet != address(0)) {
          _tokenId = (++_tokenId) % 100;
        }

        return _tokenId;
      }
    }

    ```

  - So in the `_getNextTokenId()` function, a while loop will run that will first check the following statement:
  - `tokenRecipient[_tokenId].wallet != address(0)`

  - The above statement checks whether a founder has been assigned to `baseTokenId` (i.e., changed to `_tokenId` inside the current function ) already. If the statement becomes `true` that means there is already a founder and search for the next id will be done by using this calculation inside the loop:

  - `_tokenId` = `(++_tokenId) % 100`

  - And this loop will keep on running until the next available token Id is returned.

  - In our case, `tokenRecipient[100].wallet != address(0)` statement will be `false` as it is running first time and no other founder has been added yet. So `_tokenId = 100` will be returned from the function and will be assigned to `baseTokenId` inside the inner loop where `_getNextTokenId()` was called. This is the first token ID that will be assigned to the founder.
  - So the current founder that was calculated in the outer loop will be added for this token ID `100` in the `tokenRecipient` mapping:
  - ` tokenRecipient[100]` = `newFounder`

- Now the execution flow will continue inside the inner loop and new `baseTokenId` will be calculated like this:
  `baseTokenId` = `(baseTokenId + schedule) % 100` = `(100 + 50) % 100` = `50`

- Then this inner loop will run again for the next `founderPct` and same calculation will be done for the new `baseTokenId`. So the values will be:

  - `schedule` = `50`
  - `baseTokenId` = `_getNextTokenId(baseTokenId)` = `_getNextTokenId(50)` = `50`

  - `_getNextTokenId()` will return `50` as it is also not assigned to anybody.

  - so next Id that the current founder will receive is `50`.

- Now the nested loop inside `_addFounders(...)` will stop executing as inner loop has already run for `founderPct` times. And execution flow will move to outer loop and it will stop it's execution as well as there is no other founder (so outer loop will not perform another iteration).

- So the two token IDs that will be minted for the founder in every 100 tokens will be `100` and `50`.
- Next part will handle the calculation for minting logic.

#### Calculation for minting through `Token::mint(...)`, `Token::mintTo(...)` or `Token::mintBatchTo(...)`:

- To mint the tokens, `Token::mint(...)`, `Token::mintTo(...)` or `Token::mintBatchTo(...)` can be called by the `Auction` contract or a `Minter`. Each one of these handles the founders vesting.
- We will take an example of minting through `Token::mint(...)` when called by `Auction` contract.
- `Auction` contract will call the minting functions when it has to create a new auction. There are two ways to create an auction. first is by calling `Auction::settleCurrentAndCreateNewAuction(...)`. And the second is when `Auction::unpause(...)` is called. We can only create an auction with the first method if there is an active auction before. If we want to create an auction for the first time, then it can be done only by the owner of the `Auction` contract. To do that `Auction::unpause(...)` will be called by owner that will unpause the `Auction` contract and start the first auction by minting the first token ID for bidding. We will take an example where the auction is created for the first time.
- When the owner calls `Auction::unpause(...)`, the call will be made to `Token::mint()` function through `Auction::_createAuction(...)`.

  - Link to [`Auction::unpause(...)](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L336C5-L361C6)

- `Token::mint(...)` will call `Token::_mintWithVesting(msg.sender)` to calculate the token Id to mint and for the actual minting of the token. Here is the code for [`Token::_mintWithVesting(msg.sender)`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L230):

  ```solidity
  function _mintWithVesting(address recipient) internal returns (uint256 tokenId) {
    // Cannot realistically overflow
    unchecked {
      do {
        // Get the next token to mint
        tokenId = reservedUntilTokenId + settings.mintCount++;

        // Lookup whether the token is for a founder, and mint accordingly if so
      } while (_isForFounder(tokenId));
    }

    // Mint the next available token to the recipient for bidding
    _mint(recipient, tokenId);
  }

  ```

- Now to calculate the `tokenId` to mint, it will perform the following calculation in the do while loop as you can see above:

  - `tokenId` = `reservedUntilTokenId` + `settings.mintCount++`
  - we know, `reservedUntilTokenId = 100`. And `settingmintCount++` will be equal to `0` as no token in minted yet. so the above calculation will be equal to
  - `tokenId` = `100` + `0` = `100`
  - Now call to `Token::_isForFounder(tokenId)` will be made in the while condition like this:
  - `while (_isForFounder(100))`.
  - Code for [`_isForFounder(...)`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L263):

  ```solidity
  function _isForFounder(uint256 _tokenId) private returns (bool) {
    // Get the base token id
    uint256 baseTokenId = _tokenId % 100;

    // If there is no scheduled recipient:
    if (tokenRecipient[baseTokenId].wallet == address(0)) {
      return false;

      // Else if the founder is still vesting:
    } else if (block.timestamp < tokenRecipient[baseTokenId].vestExpiry) {
      // Mint the token to the founder
      _mint(tokenRecipient[baseTokenId].wallet, _tokenId);

      return true;

      // Else the founder has finished vesting:
    } else {
      // Remove them from future lookups
      delete tokenRecipient[baseTokenId];

      return false;
    }
  }

  ```

  - In `Token::_isForFounder(...)`, the actual check will be done for `baseTokenId` that will be calculated like this:
  - `baseTokenId` = `_tokenId % 100` = `0` as the `_tokenId` passed was `100`
  - Then the first if condition will be checked that checks whether there is a reserved recipient for `baseTokenId` like this:
  - `tokenRecipient[baseTokenId].wallet` == `address(0)`
  - But this where the real problem arises. The call to `_isForFounder()` was made to check for token Id `100`. But it is checking for Id `0`.
  - Now the first if condition will evaluate to true as it is not reserved for founder (we can check that in previous section). And `_isForFounder()` will return `false`. That mean the function evaluated that token ID `100` is not reserved.
  - That means execution will come out of `while` loop, and `tokenId = 100` will be minted to the `Auction` contract. But this token ID was reserved for the founder as we can see in the previous section and should be minted to the founder when next `else if` condition is evaluated to true inside `Token::_isForFounder(...)`.

- Because of the above issue, any token ID that is in hundreds (like 100, 200, 300 etc.) will be minted to `Auction` or any other user by either `Auction` contract or by `Minter` that should have been reserved for the founder.

Now, we might think that we can call `Token::updateFounders(...)` function and remove the previous founders and add new ones to cause handle the situation. But this might give rise to another problem. [`Token::updateFounders(...)`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L375) function first removes the old founders and deletes the old state of the different mappings. And to do that, it uses similar calculation that was done in `Token::_addFounders(...)` to find out which user has reserved slots in the `tokenRecipient` mapping. But there is one problem. It doesn't initialize the `baseTokenId` to `reservedUntilTokenId` to perform the calculation. The `baseTokenId` is initialized with default value that is `0`.

```solidity
function updateFounders(IManager.FounderParams[] calldata newFounders) external onlyOwner {

        unchecked {
            // for each existing founder:
            for (uint256 i; i < cachedFounders.length; ++i) {

@>                uint256 baseTokenId;

                for (uint256 j; j < cachedFounder.ownershipPct; ++j) {
                }
            }
        }

        _addFounders(newFounders, reservedUntilTokenId);
    }
```

Github: [412](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L412)

But in `Token::_addFounder()`, `baseTokenId` is initialized with the `reserveUnitilTokenId`

```solidity
function _addFounders(IManager.FounderParams[] calldata _founders, uint256 reservedUntilTokenId) internal {

                     ...

@>                uint256 baseTokenId = reservedUntilTokenId;

                   ...

    }

```

GitHub: [161](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L161)

The net effect of this will be that the data inside `tokenRecipient` mapping for token IDs that are in hundreds will not be deleted. The proof for this is shown in the test below. So this is not a fix for the above situation.

In the above example, we have taken a founder that has `2` percent share. But if we take only `1` percent share then only token ID `100` will be reserved for the founder and he will not receive anything.

## Impact

Founders will not be able to mint their share of token if share amount is in proper hundreds and they will be in loss.

## Code Snippet

### Test that shows the `updateFounder(...)` will not work with current implementation of `_addFounder(...)`:

```solidity
function test_updatingThefoundersMappingWillNotRemoveAnyFoundersInHundreds() public {
  // setting up everything
  deployMock();

  // getting the number of founders info
  uint256 foundersAdded = token.totalFounders();

  // checking the number of founders. should be 1
  assertEq(numOfFounders, foundersAdded);

  // updating the founders
  IManager.FounderParams[] memory newFoundersArr = new IManager.FounderParams[](2);
  newFoundersArr[0] = IManager.FounderParams({
    wallet: address(makeAddr("newFounder1")),
    ownershipPct: 0,
    vestExpiry: 2556057600
  });
  newFoundersArr[1] = IManager.FounderParams({
    wallet: address(makeAddr("newFounder2")),
    ownershipPct: 10,
    vestExpiry: 2556057600
  });

  vm.prank(token.owner());
  token.updateFounders(newFoundersArr);

  assertEq(token.getFounders().length, 1);
}

```

Link to original test: [Test](https://github.com/Aamirusmani1552/nouns-dao-audit/blob/95bbfd2e6d39862c6b51e88a16814e5d9ad98549/nouns-protocol/test/Integration/Integrations.sol#L60)

_*Outputs:*_

> NOTE
> Only relevant part of output is shown here. If you want to see the complete output please go to the link specified below the test code and run the test command: `forge test --mt test_updatingThefoundersMappingWillNotRemoveAnyFoundersInHundreds -vvvv`

This is the part of output that shows the assignment of the token IDs to the founder:

```bash
    │   │   │   │   ├─ emit MintScheduled(baseTokenId: 100, founderId: 0, founder: (0x0000000000000000000000000000000000000001, 2, 2419200 [2.419e6]))
    │   │   │   │   ├─ [0] console::log(tokenId %s, 50) [staticcall]
    │   │   │   │   │   └─ ← ()
    │   │   │   │   ├─ [0] console::log(50) [staticcall]
    │   │   │   │   │   └─ ← ()
    │   │   │   │   ├─ emit MintScheduled(baseTokenId: 50, founderId: 0, founder: (0x0000000000000000000000000000000000000001, 2, 2419200 [2.419e6]))
```

According to the above output token IDs `100` and `50` are assinged to the founder. But when we call `updateFounders` the token Id `0` and `50` is Unscheduled/removed as you can see below:

```bash
    │   │   ├─ emit MintUnscheduled(baseTokenId: 0, founderId: 0, founder: (0x0000000000000000000000000000000000000001, 2, 2419200 [2.419e6]))
    │   │   ├─ emit MintUnscheduled(baseTokenId: 50, founderId: 0, founder: (0x0000000000000000000000000000000000000001, 2, 2419200 [2.419e6]))
```

that means token ID `100` is still assigned to old founder.

### Test that show the token ID that is assigned to the founder can still be minted by `Auction` or `Minter`

```solidity
function test_startingTheAuctionWillMintTokenIdAssignedToFounder() public {
  // setting up everything
  deployMock();

  // getting the number of founders info
  uint256 foundersAdded = token.totalFounders();

  // checking the number of founders. should be 1
  assertEq(numOfFounders, foundersAdded);

  // starting the auction
  vm.prank(token.owner());
  auction.unpause();
}

```

Link to original test: [Test](https://github.com/Aamirusmani1552/nouns-dao-audit/blob/3ce28a46deb212ab7ca60e496a0868a596e317ea/nouns-protocol/test/Integration/Integrations.sol#L81)

_*Outputs:*_

> NOTE
> Only relevant part of output is shown here. If you want to see the complete output please go to the link specified below the test code and run the test command: `forge test --mt test_startingTheAuctionWillMintTokenIdAssignedToFounder -vvvv`

According to the output, tokens reserved for the founders are token ID `50` and `100`:

```bash
    │   │   │   │   ├─ emit MintScheduled(baseTokenId: 100, founderId: 0, founder: (0x0000000000000000000000000000000000000001, 2, 2419200 [2.419e6]))
    │   │   │   │   ├─ [0] console::log(tokenId %s, 50) [staticcall]
    │   │   │   │   │   └─ ← ()
    │   │   │   │   ├─ [0] console::log(50) [staticcall]
    │   │   │   │   │   └─ ← ()
    │   │   │   │   ├─ emit MintScheduled(baseTokenId: 50, founderId: 0, founder: (0x0000000000000000000000000000000000000001, 2, 2419200 [2.419e6]))
    │
```

But token ID `100` is minted to `Auction` contract when auction is started that was meant to be for founder. Here is the output for that:

```bash
    │   │   ├─ [126776] TOKEN::mint()
    │   │   │   ├─ [126393] Token::mint() [delegatecall]
    │   │   │   │   ├─ emit Transfer(from: 0x0000000000000000000000000000000000000000, to: AUCTION: [0xED0F1F3fB92e91084dA8D66A2FC6cFABc7b010cf], tokenId: 100)
    │   │   │   │   ├─ emit DelegateVotesChanged(delegate: AUCTION: [0xED0F1F3fB92e91084dA8D66A2FC6cFABc7b010cf], prevTotalVotes: 0, newTotalVotes: 1)
    │   │   │   │   ├─ [24292] METADATA_RENDERER::onMinted(100)
    │   │   │   │   │   ├─ [23906] MetadataRenderer::onMinted(100) [delegatecall]
    │   │   │   │   │   │   └─ ← true
    │   │   │   │   │   └─ ← true
    │   │   │   │   └─ ← 100
    │   │   │   └─ ← 100
```

## Tool used

- Manual Review

## Recommendation

It is recommended to add the following changes:

```diff
File: Token.sol

function _addFounders(IManager.FounderParams[] calldata _founders, uint256 reservedUntilTokenId) internal {
        // Used to store the total percent ownership among the founders
        uint256 totalOwnership;

        uint8 numFoundersAdded = 0;

        unchecked {
            // For each founder:
            for (uint256 i; i < _founders.length; ++i) {
                // Cache the percent ownership
                uint256 founderPct = _founders[i].ownershipPct;

                // Continue if no ownership is specified
                if (founderPct == 0) {
                    continue;
                }

                // Update the total ownership and ensure it's valid
                totalOwnership += founderPct;

                // Check that founders own less than 100% of tokens
                if (totalOwnership > 99) {
                    revert INVALID_FOUNDER_OWNERSHIP();
                }

                // Compute the founder's id
                uint256 founderId = numFoundersAdded++;

                // Get the pointer to store the founder
                Founder storage newFounder = founder[founderId];

                // Store the founder's vesting details
                newFounder.wallet = _founders[i].wallet;
                newFounder.vestExpiry = uint32(_founders[i].vestExpiry);
                // Total ownership cannot be above 100 so this fits safely in uint8
                newFounder.ownershipPct = uint8(founderPct);

                // Compute the vesting schedule
                uint256 schedule = 100 / founderPct;

                // Used to store the base token id the founder will recieve
-                uint256 baseTokenId = reservedUntilTokenId;
+               uint256 baseTokenId = 0;

                // For each token to vest:
                for (uint256 j; j < founderPct; ++j) {
                    // Get the available token id
                    baseTokenId = _getNextTokenId(baseTokenId);

                    // Store the founder as the recipient
                    tokenRecipient[baseTokenId] = newFounder;

                    emit MintScheduled(baseTokenId, founderId, newFounder);

                    // Update the base token id
                    baseTokenId = (baseTokenId + schedule) % 100;
                }
            }

            // Store the founders' details
            settings.totalOwnership = uint8(totalOwnership);
            settings.numFounders = numFoundersAdded;
        }
    }

```

This will make sure that a token ID in exact hundreds is never assigned to the founder.
