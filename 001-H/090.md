Bouncy Graphite Urchin

high

# Incorrect update of the founders list, some previous founders did not removed

## Summary
In the functions updateFounders() and _addFounders(), the calculation of the NFT token IDs, which are due to the founders, occurs differently. Therefore, part of the data about the previous founder is not removed when updating the list of founders
## Vulnerability Detail
After each completion of an auction for the sale of NFTs, minting of NFTs occurs for the next auction, as well as minting of NFTs for the founders if the next token ID matches one of the founder's addresses. The calculation occurs through mapping and the remainder of the token ID divided by 100.
```solidity
  function _isForFounder(uint256 _tokenId) private returns (bool) {
        uint256 baseTokenId = _tokenId % 100;
        if (tokenRecipient[baseTokenId].wallet == address(0)) {
            return false;
        } else if (block.timestamp < tokenRecipient[baseTokenId].vestExpiry) {
            // Mint the token to the founder
            _mint(tokenRecipient[baseTokenId].wallet, _tokenId);
```
The addition of founder addresses to the tokenRecipient array occurs during the call of the initialize() function or when updating the list of founders. During an update, old addresses are removed, and new ones are added. When deploying the contract on L2, it is possible to specify the parameter _reservedUntilTokenId, which will be taken into account by the _addFounders() function for calculating the token ID for the founders. For each founder, the calculation of the baseTokenId will start from the value of the reservedUntilTokenId variable.
```solidity
function _addFounders(IManager.FounderParams[] calldata _founders, uint256 reservedUntilTokenId) internal {
    ...
   uint256 baseTokenId = reservedUntilTokenId;   // <--- 
   for (uint256 j; j < founderPct; ++j) {
         baseTokenId = _getNextTokenId(baseTokenId);
         tokenRecipient[baseTokenId] = newFounder;
         emit MintScheduled(baseTokenId, founderId, newFounder);
         baseTokenId = (baseTokenId + schedule) % 100;
   }
  ...
}
```
When DAO on L2 decides to update the list of founders, the updateFounders() function is called, which first calculates the token ID for the current founders and resets the values in the tokenRecipient mapping. Then, it calls the _addFounders() function.

The issue is that the calculation is done without considering the _reservedUntilTokenId variable, which was used during the contract deployment. As a result, part of the data is not removed from the mapping, and the new founders do not overwrite the data of the old founders. Therefore, some of the old founders will also receive NFTs on par with the new founders.
```solidity
  function updateFounders(IManager.FounderParams[] calldata newFounders) external onlyOwner {
     ....
    // Used to reverse engineer the indices the founder has reserved tokens in.
    uint256 baseTokenId;     //   <---------    should be uint256 baseTokenId = reservedUntilTokenId; 
    for (uint256 j; j < cachedFounder.ownershipPct; ++j) {
           while (clearedTokenIds[baseTokenId] != false) {
                     baseTokenId = (++baseTokenId) % 100;
           }
    delete tokenRecipient[baseTokenId];

```

For example, when deploying the contract, let's specify 2 founders with stakes of 10 and 5, and set _reservedUntilTokenId to 30. Then, we call the updateFounders() function with a new list and notice that the second founder, specified during deployment, was not removed and will receive NFTs on par with the new founders.

```solidity
// Deploy
First founder, id = 0
  tokenId -  30
  tokenId -  40
  tokenId -  50
  tokenId -  60
  tokenId -  70
  tokenId -  80
  tokenId -  90
  tokenId -  0
  tokenId -  10
  tokenId -  20
 Second founder, id =  1
  tokenId -  31
  tokenId -  51
  tokenId -  71
  tokenId -  91
  tokenId -  11

// Call updateFounders()
 delete baseotoken -  0     // OK
 delete baseotoken -  10   // OK
 delete baseotoken -  20   // OK
 delete baseotoken -  30   // OK
 delete baseotoken -  40   // OK
 delete baseotoken -  50   // OK
 delete baseotoken -  60   // OK
 delete baseotoken -  70   // OK
 delete baseotoken -  80   // OK
 delete baseotoken -  90   // OK
 delete baseotoken -  1      // BAD
 delete baseotoken -  21    // BAD
 delete baseotoken -  41    // BAD
 delete baseotoken -  61    // BAD
 delete baseotoken -  81    // BAD
// _addFounders
 New first founder, id = 0
  tokenId -  30
  tokenId -  40
  tokenId -  50
  tokenId -  60
  tokenId -  70
  tokenId -  80
  tokenId -  90
  tokenId -  0
  tokenId -  10
  tokenId -  20
 New second founder, id = 1
  tokenId -  32 
  tokenId -  52
  tokenId -  72
  tokenId -  92
  tokenId -  12
```

POC
Create file UpdateFoundersTest.t.sol in test folder
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.16;

import { NounsBuilderTest } from "./utils/NounsBuilderTest.sol";
import { IManager, Manager } from "../src/manager/Manager.sol";
import { TokenTypesV1 } from "../src/token/types/TokenTypesV1.sol";

contract TokenTest is NounsBuilderTest, TokenTypesV1 {

    event MintScheduled(uint256 baseTokenId, uint256 founderId, Founder founder);
    event MintUnscheduled(uint256 baseTokenId, uint256 founderId, Founder founder);
    mapping(address => uint256) public mintedTokens;

    address founder3 = makeAddr("Founder3");
    address founder4 = makeAddr("Founder4");

    function setUp() public virtual override {
        super.setUp();
    }


     function test_UpdateFounders() public {
        address[] memory wallets = new address[](2);
        uint256[] memory percents = new uint256[](2);
        uint256[] memory vestingEnds = new uint256[](2);
        wallets[0] = founder;
        wallets[1] = founder2;
        percents[0] = 10;
        percents[1] = 5;
        vestingEnds[0] = 4 weeks;
        vestingEnds[1] = 4 weeks;
        setFounderParams(wallets, percents, vestingEnds);
        setTokenParams(
            "Mock Token",
            "MOCK",
            "This is a mock token",
            "ipfs://Qmew7TdyGnj6YRUjQR68sUJN3239MYXRD8uxowxF6rGK8j",
            "https://nouns.build",
            "http://localhost:5000/render",
            30,
            address(0)
        );
        setMockAuctionParams();
        setMockGovParams();
        vm.expectEmit(true, true, true, true); 
        emit MintScheduled(30, 0, Founder(wallets[0], uint8(percents[0]),  uint32(vestingEnds[0])));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(40, 0, Founder(wallets[0], uint8(percents[0]),  uint32(vestingEnds[0])));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(50, 0, Founder(wallets[0], uint8(percents[0]),  uint32(vestingEnds[0])));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(60, 0, Founder(wallets[0], uint8(percents[0]),  uint32(vestingEnds[0])));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(70, 0, Founder(wallets[0], uint8(percents[0]),  uint32(vestingEnds[0])));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(80, 0, Founder(wallets[0], uint8(percents[0]),  uint32(vestingEnds[0])));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(90, 0, Founder(wallets[0], uint8(percents[0]),  uint32(vestingEnds[0])));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(0, 0, Founder(wallets[0], uint8(percents[0]),  uint32(vestingEnds[0])));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(10, 0, Founder(wallets[0], uint8(percents[0]),  uint32(vestingEnds[0])));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(20, 0, Founder(wallets[0], uint8(percents[0]),  uint32(vestingEnds[0])));

        vm.expectEmit(true, true, true, true);
        emit MintScheduled(31, 1, Founder(wallets[1], uint8(percents[1]),  uint32(vestingEnds[1])));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(51, 1, Founder(wallets[1], uint8(percents[1]),  uint32(vestingEnds[1])));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(71, 1, Founder(wallets[1], uint8(percents[1]),  uint32(vestingEnds[1])));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(91, 1, Founder(wallets[1], uint8(percents[1]),  uint32(vestingEnds[1])));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(11, 1, Founder(wallets[1], uint8(percents[1]),  uint32(vestingEnds[1])));
  
        deploy(foundersArr, tokenParams, auctionParams, govParams);
        
    //  new founders
        IManager.FounderParams[] memory newFoundersArr = new IManager.FounderParams[](2);
         newFoundersArr[0] = IManager.FounderParams({
            wallet: founder3,
            ownershipPct: 10,
            vestExpiry: 2556057600
        });
         newFoundersArr[1] = IManager.FounderParams({
            wallet: founder3,
            ownershipPct: 5,
            vestExpiry: 2556057600
        });

        
        vm.prank(address(founder));

        // MintUnscheduled - Delete previous founders
        // We delete wrong ids: 1,21,41,61,81 instead of 31,51,71,91,11
         vm.expectEmit(true, true, true, true); 
        emit MintUnscheduled(1, 1, Founder(wallets[1], uint8(percents[1]),  uint32(vestingEnds[1])));
         vm.expectEmit(true, true, true, true); 
        emit MintUnscheduled(21, 1, Founder(wallets[1], uint8(percents[1]),  uint32(vestingEnds[1])));
         vm.expectEmit(true, true, true, true); 
        emit MintUnscheduled(41, 1, Founder(wallets[1], uint8(percents[1]),  uint32(vestingEnds[1])));
         vm.expectEmit(true, true, true, true); 
        emit MintUnscheduled(61, 1, Founder(wallets[1], uint8(percents[1]),  uint32(vestingEnds[1])));
         vm.expectEmit(true, true, true, true); 
        emit MintUnscheduled(81, 1, Founder(wallets[1], uint8(percents[1]),  uint32(vestingEnds[1])));

        vm.expectEmit(true, true, true, true); 
        emit MintScheduled(30, 0, Founder(newFoundersArr[0].wallet, uint8(10),  uint32(2556057600)));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(40, 0, Founder(newFoundersArr[0].wallet, uint8(10),  uint32(2556057600)));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(50, 0, Founder(newFoundersArr[0].wallet, uint8(10),  uint32(2556057600)));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(60, 0, Founder(newFoundersArr[0].wallet, uint8(10),  uint32(2556057600)));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(70, 0, Founder(newFoundersArr[0].wallet, uint8(10),  uint32(2556057600)));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(80, 0, Founder(newFoundersArr[0].wallet, uint8(10),  uint32(2556057600)));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(90, 0, Founder(newFoundersArr[0].wallet, uint8(10),  uint32(2556057600)));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(0, 0, Founder(newFoundersArr[0].wallet, uint8(10),  uint32(2556057600)));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(10, 0, Founder(newFoundersArr[0].wallet, uint8(10),  uint32(2556057600)));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(20, 0, Founder(newFoundersArr[0].wallet, uint8(10),  uint32(2556057600)));

        // Old founder2 has not been deleted, therefore new founder2 has not the same ids.
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(32, 1, Founder(newFoundersArr[1].wallet, uint8(5),  uint32(2556057600)));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(52, 1, Founder(newFoundersArr[1].wallet, uint8(5),  uint32(2556057600)));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(72, 1, Founder(newFoundersArr[1].wallet, uint8(5),  uint32(2556057600)));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(92, 1, Founder(newFoundersArr[1].wallet, uint8(5),  uint32(2556057600)));
        vm.expectEmit(true, true, true, true);
        emit MintScheduled(12, 1, Founder(newFoundersArr[1].wallet, uint8(5),  uint32(2556057600)));

        token.updateFounders(newFoundersArr);
            
    }

}
```

run:   forge test --mp test/FoundersUpdate.t.sol 
## Impact
Because part of the base token ID, which belongs to the previous founders, is not being removed, they will also continue to receive NFTs on an equal footing with the new list of founders.
## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L375-L420
## Tool used

Manual Review

## Recommendation
It is necessary to save the value of the variable _reservedUntilTokenId in order to use it later when calculating the token ID in the updateFounders() function.
```diff
function initialize(
        IManager.FounderParams[] calldata _founders,
        bytes calldata _initStrings,
        uint256 _reservedUntilTokenId,
        address _metadataRenderer,
        address _auction,
        address _initialOwner
    ) external initializer {
+   savedReservedUntilTokenId = _reservedUntilTokenId;
    ...
}

  function updateFounders(IManager.FounderParams[] calldata newFounders) external onlyOwner {
     ....
-  uint256 baseTokenId;   
+  uint256 baseTokenId = reservedUntilTokenId; 
    for (uint256 j; j < cachedFounder.ownershipPct; ++j) {
           while (clearedTokenIds[baseTokenId] != false) {
                     baseTokenId = (++baseTokenId) % 100;
           }
    delete tokenRecipient[baseTokenId];

```