Active Bone Lemur

high

# `updateFounders()` doesn't update founders's allocations properly

## Summary

In the token contract founders are set on deployment, they will receive a percentage of the minted NFTs by the auction contract. It is possible to update the founders allocation using the `updateFounders()` function, but this function doesn't take into account the `reservedUntilTokenId` which will result in some IDs allocation not properly reset if percentage was to change for a founder.

Thus founders may receive more NFTs than they're entitled to.

## Vulnerability Detail

In the [`Token.sol`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/token/Token.sol) contract the [`updateFounders()`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/token/Token.sol#L375) allows to update founders allocations or vesting duration.

It does it by removing the older allocations of each founders and then adding the new ones. The issue is that when it initially added the founders in [`addFounders()`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/token/Token.sol#L120) it used the `reservedUntilTokenId` as starting ID to set which tokens IDs will be sent to the founders, but in the [`updateFounders()`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/token/Token.sol#L375) it doesn't use the `reservedUntilTokenId` to remove the older token IDs set for each founders.

`addFounders()`:

```solidity
                // Used to store the base token id the founder will recieve
                uint256 baseTokenId = reservedUntilTokenId;

                // For each token to vest:
                for (uint256 j; j < founderPct; ++j) {
                    // Get the available token id
                    baseTokenId = _getNextTokenId(baseTokenId);
```

`updateFounders()`:

```solidity
                // Used to reverse engineer the indices the founder has reserved tokens in.
                uint256 baseTokenId;

                for (uint256 j; j < cachedFounder.ownershipPct; ++j) {
                    // Get the next index that hasn't already been cleared
                    while (clearedTokenIds[baseTokenId] != false) {
                        baseTokenId = (++baseTokenId) % 100;
                    }
```

So if we update the percentage of a founder when `reservedUntilTokenId > 0` then it might not update some IDs of the older allocation and will result in more NFTs minted to the founder.

## Impact

High. Updating the founder allocation can result in a higher allocation than expected.

## Code Snippet

https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/token/Token.sol#L157-L175

https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/token/Token.sol#L408-L428

POC that can be copy pasted in `Token.t.sol`:

```solidity
function test_UpdateFounderImproperUpdate() public {
        deployMock();

        vm.prank(address(founder));
        token.setReservedUntilTokenId(5); //set reserve ids to 5
        vm.prank(address(founder));
        token.updateFounders(foundersArr); //update once to make the mock like it was deployed with 5 reserved ids (simpler than modifiying the mock)

        address[] memory founders = new address[](1);
        uint256[] memory percents = new uint256[](1);
        uint256[] memory vestingEnds = new uint256[](1);

        founders[0] = founder;

        percents[0] = 8; //Update founder allocation to 8% -> 17, 29, 41, 53, 65, 77, 89 and 1 modulo ids should be sent to the founder

        vestingEnds[0] = 4 weeks;
        
        setFounderParams(founders, percents, vestingEnds);

        vm.prank(address(founder));
        token.updateFounders(foundersArr);

        // Mint 100 tokens and start at 5 since 5 reserve ids
        for (uint256 i = 5; i < 105; i++) {
            vm.prank(address(auction));
            token.mint();

            mintedTokens[token.ownerOf(i)] += 1;
        }

        // Read the ownership of only the first 100 minted tokens
        // Note that the # of tokens minted above can exceed 100, therefore
        // we do our own count because we cannot use balanceOf().

        assertGt(mintedTokens[founder], percents[0]); //founder received more than 8%
        assertEq(mintedTokens[founder], percents[0] + 10); //founder received 8% + 10% from initial deployment
}
```

## Tool used

Manual Review

## Recommendation

Consider not using `reservedUntilTokenId` as starting ID in `addFounders()` function as it doesn't need it since we use the modulo of the minted ID.

Using `reservedUntilTokenId` in `updateFounders()` is not a proper fix as if the `reservedUntilTokenId` was changed in between it could also result in some allocation not removed on update.
