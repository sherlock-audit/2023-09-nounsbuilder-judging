Proud Orange Blackbird

medium

# The old founders' information cannot be successfully deleted when updating with new founders.

## Summary
When updating with new `founders`, the process involves deleting the old `founders`' information and replacing it with the new `founders`. 
However, a complete deletion of the old information is not achieved due to an error.
## Vulnerability Detail
When updating with new founders, we are deleting the old information.
```solidity
uint256 baseTokenId;
for (uint256 j; j < cachedFounder.ownershipPct; ++j) {
    // Get the next index that hasn't already been cleared
    while (clearedTokenIds[baseTokenId] != false) {
        baseTokenId = (++baseTokenId) % 100;
    }
    delete tokenRecipient[baseTokenId];
    clearedTokenIds[baseTokenId] = true;
    baseTokenId = (baseTokenId + schedule) % 100;
}
```
But there is a discrepancy in the starting token ID between adding old users and deleting old users.
```solidity
uint256 baseTokenId = reservedUntilTokenId;
```
```solidity
uint256 baseTokenId;
```
For example, if `reservedUntilTokenId` is `50`, and the percentage (`pct`) is `1` for the first user, we assign `ID 50` to the first user. 
However, during the process of updating with new founders, we delete `ID 0` for the first user. 
Consequently, even though the user was deleted, the association with `ID 50` still remains, creating an issue in the token assignment for that user.

The POC test is as below:
```solidity
function test_UpdateFounders() public {
      address f1Wallet = founder;
      uint256 f1Percentage = 1;
      address[] memory founders = new address[](1);
      uint256[] memory percents = new uint256[](1);
      uint256[] memory vestingEnds = new uint256[](1);
      founders[0] = f1Wallet;
      percents[0] = f1Percentage;
      vestingEnds[0] = 4 weeks;

      setFounderParams(founders, percents, vestingEnds);
      setMockTokenParamsWithReserve(50);
      setMockAuctionParams();
      setMockGovParams();
      deploy(foundersArr, tokenParams, auctionParams, govParams);
      setMockMetadata();

      Founder memory f1 = token.getFounder(0);
      assertEq(f1.ownershipPct, f1Percentage);

      address f2Wallet = founder2;
      uint256 f2Percentage = 1;
      address[] memory newFounders = new address[](1);
      uint256[] memory newPercents = new uint256[](1);
      uint256[] memory newVestingEnds = new uint256[](1);
      newFounders[0] = f2Wallet;
      newPercents[0] = f2Percentage;
      newVestingEnds[0] = 4 weeks;

      setFounderParams(newFounders, newPercents, newVestingEnds);
      vm.prank(founder);
      token.updateFounders(foundersArr);

      uint256 scheduledCnt = 0;
      for (uint256 i; i < 100; i++) {
          Founder memory f = token.getScheduledRecipient(i);
          if (f.wallet != address(0)) scheduledCnt++;
      }
      // Should be f2Percentage
      assertEq(scheduledCnt, f1Percentage + f2Percentage);
  }
```
## Impact
More tokens can be sent to the founders than expected.
## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L412-L427
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L161
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L412
## Tool used

Manual Review
## Recommendation

Please modify as below:
```solidity
function updateFounders(IManager.FounderParams[] calldata newFounders) external onlyOwner {
   - uint256 baseTokenId;
   + uint256 baseTokenId = reservedUntilTokenId % 100;
}
```
