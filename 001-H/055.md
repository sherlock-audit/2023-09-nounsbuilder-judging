Proud Orange Blackbird

medium

# The updateFounders function will not function properly after resetting the reservedUntilTokenId.

## Summary
To update new `founders`, it is necessary to clear the old users' information. 
However, after resetting the `reservedUntilTokenId`, the `updateFounders` function fails to clear the old information correctly.
## Vulnerability Detail
When adding users at deploy time, we assign certain IDs between 0 and 99 to the founders.
```solidity
uint256 baseTokenId = reservedUntilTokenId % 100;
for (uint256 j; j < founderPct; ++j) {
    baseTokenId = _getNextTokenId(baseTokenId);
    tokenRecipient[baseTokenId] = newFounder;
    emit MintScheduled(baseTokenId, founderId, newFounder);
    baseTokenId = (baseTokenId + schedule) % 100;
}
```
And the owner updates the `reservedUntilTokenId`.
```solidity
function setReservedUntilTokenId(uint256 newReservedUntilTokenId) external onlyOwner {
    reservedUntilTokenId = newReservedUntilTokenId;
}
```
Following that, the owner updates the new `founders`.
```solidity
uint256 baseTokenId = reservedUntilTokenId % 100;
for (uint256 j; j < cachedFounder.ownershipPct; ++j) {
    while (clearedTokenIds[baseTokenId] != false) {
        baseTokenId = (++baseTokenId) % 100;
    }
    delete tokenRecipient[baseTokenId];
    clearedTokenIds[baseTokenId] = true;
    emit MintUnscheduled(baseTokenId, i, cachedFounder);
    baseTokenId = (baseTokenId + schedule) % 100;
}
```
However, there is a discrepancy in the `starting ID` between adding previous users and deleting them during the update with new founders, due to the update of `reservedUntilTokenId` between these actions. 
This leads to the incorrect removal of old information.

The POC test for this is as below.
```solidity
function test_SetReservedUntilTokenId() public {
    address f1Wallet = founder;
    uint256 f1Percentage = 1;
    address[] memory founders = new address[](1);
    uint256[] memory percents = new uint256[](1);
    uint256[] memory vestingEnds = new uint256[](1);
    founders[0] = f1Wallet;
    percents[0] = f1Percentage;
    vestingEnds[0] = 4 weeks;

    setFounderParams(founders, percents, vestingEnds);
    setMockTokenParamsWithReserve(50);
    setMockAuctionParams();
    setMockGovParams();
    deploy(foundersArr, tokenParams, auctionParams, govParams);
    setMockMetadata();

    Founder memory f1 = token.getFounder(0);
    assertEq(f1.ownershipPct, f1Percentage);

    vm.prank(founder);
    token.setReservedUntilTokenId(60);

    address f2Wallet = founder2;
    uint256 f2Percentage = 1;
    address[] memory newFounders = new address[](1);
    uint256[] memory newPercents = new uint256[](1);
    uint256[] memory newVestingEnds = new uint256[](1);
    newFounders[0] = f2Wallet;
    newPercents[0] = f2Percentage;
    newVestingEnds[0] = 4 weeks;

    setFounderParams(newFounders, newPercents, newVestingEnds);
    vm.prank(founder);
    token.updateFounders(foundersArr);

    uint256 scheduledCnt = 0;
    for (uint256 i; i < 100; i++) {
        Founder memory f = token.getScheduledRecipient(i);
        if (f.wallet != address(0)) scheduledCnt++;
    }
    // Should be f2Percentage
    assertEq(scheduledCnt, f1Percentage + f2Percentage);
}
```
## Impact
More `ID`s can be assigned to `founders` (including `old founders`), users may receive fewer tokens than expected, potentially impacting the entire protocol.
## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L161-L175
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L486-L503
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L412-L427
## Tool used

Manual Review

## Recommendation
Please add new variable `savedReservedUntilTokenId`.
```solidity
uint256 public savedReservedUntilTokenId;

function _addFounders(IManager.FounderParams[] calldata _founders, uint256 reservedUntilTokenId) internal {
    savedReservedUntilTokenId = reservedUntilTokenId;
}

function updateFounders(IManager.FounderParams[] calldata newFounders) external onlyOwner {
    - uint256 baseTokenId;
    + uint256 baseTokenId = savedReservedUntilTokenId % 100;
}
```