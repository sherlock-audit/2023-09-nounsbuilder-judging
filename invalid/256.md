Gigantic Indigo Cottonmouth

medium

# MerkleReserveMinter per-Token claimer fees might get burned/lost

## Summary

MerkleReserveMinter mint payments might get burned/lost if a token with a different Manager contract than the one set in MerkleReserveMinter uses it.

## Vulnerability Detail
In the _distributeFees function of MerkleReserveMinter the mint payment set by pricePerToken is sent to the treasury contract address without checking that the address is non zero. The function does revert if the success value returned from the call is false, however if a treasury is the zero address, the funds will be burned and the success value will be true.  
The value of treasury may be zero in the following scenario (and possibly others): A MerkleReserveMinter is created with the current Manager contract proxy and a UI is created to enable DAO founders to use it with their token. A founder of a DAO that was created at some point using a different Manager address (i.e. from a past deployment) uses the MerkleReserveMinter to enable claims. Setting the DAO token to work with the MerkleReserveMinter will work, since there is no check that they are using the same manager. claims will also work, however when a claim is executed, the wrong builderRewardsRecipient will get the builder fee, and a zero address will be retrieved for treasury (because the token doesn't exist in the MerkleReserveMinter mamager) causing the per-token-fee to get burned. This behaviour is due to the following code:
```solidity
(, , address treasury, ) = manager.getAddresses(tokenContract);
address builderRecipient = manager.builderRewardsRecipient();

// Pay out fees to the Builder DAO
protocolRewards.deposit{ value: builderFee }(builderRecipient, hex"00", "");

// Pay out remaining funds to the treasury
if (value > builderFee) {
    (bool treasurySuccess, ) = treasury.call{ value: value - builderFee }("");

    // Revert if treasury cannot accept funds
    if (!treasurySuccess) {
        revert TRANSFER_FAILED();
    }
}
```
The problem is demonstrated in the POC file in the Code Snippet section.

## Impact

All the revenue from per-token-fees will be lost (burned), and builder fee might be sent to the wrong recipient.

## Code Snippet

Problem Code 1:
[https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/minters/MerkleReserveMinter.sol#L193](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/minters/MerkleReserveMinter.sol#L193)

Problem Code 2:
[https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/minters/MerkleReserveMinter.sol#L201](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/minters/MerkleReserveMinter.sol#L201)

POC:
to run: Add the following code in a .sol file under the test folder and run `forge test -vv --match-test testMinterParamsPOC`
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.16;

import { NounsBuilderTest } from "./utils/NounsBuilderTest.sol";
import { MerkleReserveMinter } from "../src/minters/MerkleReserveMinter.sol";
import { TokenTypesV2 } from "../src/token/types/TokenTypesV2.sol";
import { MerkleReserveMinterTest } from "./MerkleReserveMinter.t.sol";
import { IManager, Manager } from "../src/manager/Manager.sol";
import { ERC1967Proxy } from "../src/lib/proxy/ERC1967Proxy.sol";
import { MockProtocolRewards } from "./utils/mocks/MockProtocolRewards.sol";
import { IToken, Token } from "../src/token/Token.sol";
import { IAuction, Auction } from "../src/auction/Auction.sol";
import { IGovernor, Governor } from "../src/governance/governor/Governor.sol";
import { ITreasury, Treasury } from "../src/governance/treasury/Treasury.sol";
import { MetadataRenderer } from "../src/token/metadata/MetadataRenderer.sol";
import { MetadataRendererTypesV1 } from "../src/token/metadata/types/MetadataRendererTypesV1.sol";
import {console2} from "forge-std/console2.sol";

contract minterParamsPOC is NounsBuilderTest {

    MerkleReserveMinter public otherManagerMinter;

    Manager public altManager;

    address internal claimer1;
    address internal claimer2;

    function setUp() public virtual override {
        super.setUp();

        //init altManager
        address altManagerImpl0 = address(new Manager(address(0), address(0), address(0), address(0), address(0), address(0)));
        altManager = Manager(address(new ERC1967Proxy(altManagerImpl0, abi.encodeWithSignature("initialize(address)", zoraDAO))));
        MockProtocolRewards altRewards = new MockProtocolRewards();
        address altTokenImpl = address(new Token(address(altManager)));
        address altMetadataRendererImpl = address(new MetadataRenderer(address(altManager)));
        address altAuctionImpl = address(new Auction(address(altManager), address(altRewards), weth, 1000, 1000));
        address altTreasuryImpl = address(new Treasury(address(altManager)));
        address altGovernorImpl = address(new Governor(address(altManager)));

        address altManagerImpl = address(new Manager(altTokenImpl, altMetadataRendererImpl, altAuctionImpl, altTreasuryImpl, altGovernorImpl, zoraDAO));

        vm.prank(zoraDAO);
        altManager.upgradeTo(altManagerImpl);

        otherManagerMinter = new MerkleReserveMinter(address(altManager), address(altRewards));
        claimer1 = address(0xC1);
        claimer2 = address(0xC2);
    }

    function testMinterParamsPOC() public {
        
        //deploy the base test standard contracts (token,auction,governor,treasury etc.)
        setMockFounderParams();
        setMockTokenParamsWithReserve(100);
        setMockAuctionParams();
        setMockGovParams();
        deploy(foundersArr, tokenParams, auctionParams, govParams);
        setMockMetadata();

    
        //create minter params to set otherManagerMinter as a minter in token
        TokenTypesV2.MinterParams[] memory minters = new TokenTypesV2.MinterParams[](1);
        minters[0] = TokenTypesV2.MinterParams({ minter: address(otherManagerMinter), allowed: true });

        //set otherManagerMinter as a minter in token
        vm.startPrank(token.owner());
        token.updateMinters(minters);

        //set minter settings in otherManagerMinter for token (although token was created by 'manager' and otherManagerMinter was initialized with 'altManager')
        bytes32 root = bytes32(0x5e0da80989496579de029b8ad2f9c234e8de75f5487035210bfb7676e386af8b);
        MerkleReserveMinter.MerkleMinterSettings memory settings = MerkleReserveMinter.MerkleMinterSettings({
            mintStart: 0,
            mintEnd: uint64(block.timestamp + 1000),
            pricePerToken: 0.5 ether,
            merkleRoot: root
        });
        otherManagerMinter.setMintSettings(address(token), settings);
        vm.stopPrank();

        bytes32[] memory proof = new bytes32[](1);
        proof[0] = bytes32(0xd77d6d8eeae66a03ce8ecdba82c6a0ce9cff76f7a4a6bc2bdc670680d3714273);

        //prepare claimer1 claim struct
        MerkleReserveMinter.MerkleClaim[] memory claims = new MerkleReserveMinter.MerkleClaim[](1);
        claims[0] = MerkleReserveMinter.MerkleClaim({ mintTo: claimer1, tokenId: 5, merkleProof: proof });

        //prepare claimer1 mint and monitor pre-state
        uint256 fees = otherManagerMinter.getTotalFeesForMint(address(token), claims.length);
        vm.deal(claimer1, fees);
        vm.prank(claimer1);
        uint256 balTreasuryBefore = address(treasury).balance;
        uint256 balCalimer1Before = address(claimer1).balance;
        console2.log("Fees are %s\n",fees);
        //OUTPUT: Fees are 500777000000000000

        //perform claimer1 mint throught the Merkle minter, passing fees as payment
        otherManagerMinter.mintFromReserve{ value: fees }(address(token), claims);

        //check post state
        uint256 balTreasuryAfter = address(treasury).balance;
        uint256 balCalimer1After = address(claimer1).balance;
        console2.log("'treasury' contract balance change:");
        console2.logInt(int256(balTreasuryAfter)-int256(balTreasuryBefore));
        console2.log("\nclaimer1 balance change:");
        console2.logInt(int256(balCalimer1After)-int256(balCalimer1Before));
        //OUTPUT:
        //'treasury' contract balance change:
        // 0

        //claimer1 balance change:
        //-500777000000000000

        //Although fees where sent to the minter, instead of the pricePerToken part of the fees being sent to the treasury contract, it was in fact burned.
        //This is because the otherManagerMinter is initialized with a manager that is unrelated to token. When otherManagerMinter tries to retrieve the treasury address
        //from the manager, it gets a zero address, and consequently sends the funds to the zero address without checking, which efectively burns the funds and returns success=true.
    }

}
```

## Tool used

Manual Review

## Recommendation

A. Consider adding  a check in the  _distributeFees function that treasury address is not zero. (assuming there is no scenario where burning the fees is the desired behaviour).
B. Consider adding a check when using MerkleReserveMinter with a token, that the manager set in MerkleReserveMinter is the one the token was created with.
