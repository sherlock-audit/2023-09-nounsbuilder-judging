Orbiting Carob Bison

medium

# `mintFromReserve` is inconsistently greedy

## Summary
When `MerkleReserveMinter.mintFromReserve` receives a value for a mint cost of zero, and the mint succeeds, the funds will be stuck in `MerkleReserveMinter`, which is inconsistent with how non-zero mint costs handle excess funds.



## Vulnerability Detail
`MerkleReserveMinter.mintFromReserve` requires the `msg.value` meets the mint cost, enforced by the [value check](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/minters/MerkleReserveMinter.sol#L149-L151)
```solidity
        if (msg.value < _getTotalFeesForMint(settings.pricePerToken, claimCount)) {
            revert INVALID_VALUE();
        }
```

When the price per token is zero, the fees are zero, otherwise it scales with claims, in [mint cost calculation](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/minters/MerkleReserveMinter.sol#L184-L187)
```solidity
    function _getTotalFeesForMint(uint256 pricePerToken, uint256 quantity) internal pure returns (uint256) {
        // If pricePerToken is 0 the mint has no Builder DAO fee
        return pricePerToken > 0 ? quantity * (pricePerToken + BUILDER_DAO_FEE) : 0;
    }
```

There is no upper bound on `msg.value`, it simply must meet, or exceed the mint cost.

When the token price is zero, no fees are distributed, only when the non-zero are the [fees distributed](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/minters/MerkleReserveMinter.sol#L170-L172)
```solidity
        if (settings.pricePerToken > 0) {
            _distributeFees(tokenContract, claimCount);
        }
```

The fee distribution handles excess value by [transferring it to the Treasury](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/minters/MerkleReserveMinter.sol#L201-L201).
```solidity
        // Pay out remaining funds to the treasury
        if (value > builderFee) {
            (bool treasurySuccess, ) = treasury.call{ value: value - builderFee }("");
```

Any `msg.vale` for a zero mint cost will remain in `MerkleReserveMinter`, which lacking support for Ether withdrawal, or upgrade support results in the funds being lost in the contract.

### PoC
Two tests that show the different behaviours when sending a value greater than minting fees required:
- `test_mint_with_excess_value()` where the NFT cost is greater than zero, all the value is distributed to the treasury and rewards.
- `test_mint_zero_cost_with_excess_value()` where the NFT cost is zero, no distribution.

Place the following code into `MerkleReserveMinter.t.sol` and run with `forge test --match-contract MerkleReserveMinterTest --match-test test_mint`
```solidity

    function test_mint_with_excess_value() public {
        uint64 cost = 4 ether;

        // Start - Refactored setup from test_MintFlow()
        deployAltMock(20);
        setUpSingleMinter(MerkleReserveMinter.MerkleMinterSettings({
            mintStart: 0,
            mintEnd: uint64(block.timestamp + 1000),
            pricePerToken: cost,
            merkleRoot: bytes32(0x5e0da80989496579de029b8ad2f9c234e8de75f5487035210bfb7676e386af8b)
        }));
        MerkleReserveMinter.MerkleClaim[] memory claims = setUpSingleClaimOnNftFive();
        // End - Refactored setup from test_MintFlow()

        // The minter, treasury and rewards all start with zero ETH balance
        assertEq( address(minter).balance,0);
        assertEq( address(treasury).balance,0);
        assertEq( address(rewards).balance,0);

        // Create and deal the generous claimant, who will be most generous when claiming their NFT
        address generousClaimant = makeAddr("GenerousClaimant");
        uint bigMoney = 1000 ether;
        vm.deal(generousClaimant, bigMoney);

        // Claim the NFT, and although the cost is zero, send with big money
        vm.prank(generousClaimant);
        minter.mintFromReserve{value: bigMoney}(address(token), claims);

        // Successful mint
        assertEq(token.ownerOf(5), claimer1);

        // Big money ends up in the Treasury, with the builder fee to the rewards
        assertEq(address(minter).balance, 0);
        assertEq(address(treasury).balance,bigMoney - minter.BUILDER_DAO_FEE());
        assertEq( address(rewards).balance, minter.BUILDER_DAO_FEE());
    }

    function test_mint_zero_cost_with_excess_value() public {
        // Start - Refactored setup from test_MintFlow()
        deployAltMock(20);
        setUpSingleMinter(MerkleReserveMinter.MerkleMinterSettings({
            mintStart: 0,
            mintEnd: uint64(block.timestamp + 1000),
            pricePerToken: 0 ether,
            merkleRoot: bytes32(0x5e0da80989496579de029b8ad2f9c234e8de75f5487035210bfb7676e386af8b)
        }));
        MerkleReserveMinter.MerkleClaim[] memory claims = setUpSingleClaimOnNftFive();
        // End - Refactored setup from test_MintFlow()

        // The minter, treasury and rewards all start with zero ETH balance
        assertEq( address(minter).balance,0);
        assertEq( address(treasury).balance,0);
        assertEq( address(rewards).balance,0);

        // Create and deal the generous claimant, who will be most generous when claiming their NFT
        address generousClaimant = makeAddr("GenerousClaimant");
        uint bigMoney = 1000 ether;
        vm.deal(generousClaimant, bigMoney);

        // Claim the NFT, and although the cost is zero, send with big money
        vm.prank(generousClaimant);
        minter.mintFromReserve{value: bigMoney}(address(token), claims);

        // Successful mint
        assertEq(token.ownerOf(5), claimer1);

        // Big money ends up in the minter, where the ETH is now trapped
        assertEq(address(minter).balance, bigMoney);
        assertEq(address(treasury).balance,0);
        assertEq( address(rewards).balance, 0);
    }

    function setUpSingleClaimOnNftFive() private view returns (MerkleReserveMinter.MerkleClaim[] memory){
        bytes32[] memory proof = new bytes32[](1);
        proof[0] = bytes32(0xd77d6d8eeae66a03ce8ecdba82c6a0ce9cff76f7a4a6bc2bdc670680d3714273);

        MerkleReserveMinter.MerkleClaim[] memory claims = new MerkleReserveMinter.MerkleClaim[](1);
        claims[0] = MerkleReserveMinter.MerkleClaim({mintTo: claimer1, tokenId: 5, merkleProof: proof});

        return claims;
    }

    function setUpSingleMinter(MerkleReserveMinter.MerkleMinterSettings memory settings) private {
        vm.prank(address(founder));
        minter.setMintSettings(address(token), settings);

        TokenTypesV2.MinterParams memory params = TokenTypesV2.MinterParams({minter: address(minter), allowed: true});
        TokenTypesV2.MinterParams[] memory minters = new TokenTypesV2.MinterParams[](1);
        minters[0] = params;
        vm.prank(address(founder));
        token.updateMinters(minters);
    }
```



## Impact
The `mintFromReserve` function is restricted to neither a `RESTRICTED` or `TRUSTED` role, as any user can call it, mistakes in input should not be unfairly punitive.

### Frequency
As the contrivance for this issue being unexpected (mistaken) user input (low frequency). 
Users do make honest mistakes, and how they are treated in that circumstance is an important aspect of User Experience.


### Severity
Depending on the mint cost, there are two different severities:
- Zero cost: full loss of sender's transferred Ether, as `MerkleReserveMinter` has no Ether withdraw support, nor is it upgradable, it is lost in the contract (total funds loss; severity high).
- Non-zero cost: sender's excess Ether is transferred to the Treasury, where they "could" retrieve it through a successful Governance proposal, which is likely to also include a customary donation (partial funds loss, time and bureaucratic effort; severity medium).



## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/minters/MerkleReserveMinter.sol#L129-L173


## Tool used
Manual Review, Forge test


## Recommendation
There are two options, each with their own merit, alternatively both could be implemented with an additional semaphore 
used to allow the DAO founds to decide. 


### 1) Greed is good
The user minting loves their DAO, and they wish donate additional funds (even when they don't realize they are doing it).

Keep the behaviour consistent for both zero cost and non-zero cost mints.

In `MerkleReserveMinter.mintFromReserve` update the [distribution check](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/minters/MerkleReserveMinter.sol#L169-L172)
```diff
    if (settings.pricePerToken > 0) {
        _distributeFees(tokenContract, claimCount);
-   }    
+   } else {
+           (, , address treasury, ) = manager.getAddresses(tokenContract);
+           (bool treasurySuccess, ) = treasury.call{ value: value - builderFee }("");
+
+           // Revert if treasury cannot accept funds
+            if (!treasurySuccess) {          
+               revert TRANSFER_FAILED();
+            }
+       }
+   }    
```


### 2) Only the price
The price is fixed, no more, no less.

Ensure the `msg.value` matches the priced exactly for both zero and non-zero cost mints.

In `MerkleReserveMinter.mintFromReserve` adjustment the [value check](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/minters/MerkleReserveMinter.sol#L149-L149)
```diff
        // Check value sent
-       if (msg.value < _getTotalFeesForMint(settings.pricePerToken, claimCount)) {
+       if (msg.value != _getTotalFeesForMint(settings.pricePerToken, claimCount)) {
            revert INVALID_VALUE();
        }
```
