Noisy Rose Blackbird

high

# minBidIncrement: If the ratio of minBidIncrement is less than referralRewardsBPS, the user can win the bid while spend fewer tokens

## Summary

Referral rewards allow you to get some of your bids back.

If the ratio of `referralRewardsBPS` is greater than the ratio of `minBidIncrement`, the amount of tokens you receive back is greater than the amount of tokens you need to spend to be the highest bidder.

As a result, you can be the highest bidder while spending less tokens than the previous highest price.

## Vulnerability Detail

 When setting the `currentBidReferral` in the `createBidWithReferral` function, it does not check if `_referral` is a bidder. So the bidder can set themselves as a referrer. Bidders can register themselves as a refferal, or collude with other users to earn referral rewards.

```solidity
function createBidWithReferral(uint256 _tokenId, address _referral) external payable nonReentrant {
@>  currentBidReferral = _referral;
    _createBid(_tokenId);
}
```

If the `referralRewardsBPS` ratio is greater than `minBidIncrement`, the user can be the highest bidder while spending less than the previous highest price due to the referral rewards.

Let's say the highest price is 100 ETH, `minBidIncrement` is 1%, and the `referralRewardsBPS` is 4%. The new bidder bids 101 ETH, set themselves as a referral, and becomes the highest bidder. If this bidder wins, they get 4.04 ETH back in referral rewards, meaning they actually spent only 96.96 ETH to become the highest bidder.

Here's the PoC code. You can add it to Auction.t.sol and run it.

```solidity
function test_PoC() external {
    vm.deal(bidder1, 200 ether);
    vm.deal(bidder2, 200 ether);

    // Setup
    deployAltMock(founder, 500);

    vm.prank(manager.owner());
    manager.registerUpgrade(auctionImpl, address(rewardImpl));

    vm.prank(auction.owner());
    auction.upgradeTo(address(rewardImpl));

    // update minBidIncrement less than refferal reward
    vm.prank(auction.owner());
    auction.setMinimumBidIncrement(1);

    assertEq(auction.minBidIncrement(), 1);

    vm.prank(founder);
    auction.unpause();

    // first, bidder2 bid without refferal
    vm.prank(bidder2);
    auction.createBid{ value: 100 ether}(2);

    // Check reward values

    vm.prank(bidder1);
    auction.createBidWithReferral{ value: 101 ether }(2, bidder1);
    vm.warp(10 minutes + 1 seconds);

    auction.settleCurrentAndCreateNewAuction();

    assertEq(token.ownerOf(2), bidder1);
    assertEq(token.getVotes(bidder1), 1);

    assertEq(address(treasury).balance, 88.88 ether);
    assertEq(address(rewards).balance, 3.03 ether + 4.04 ether + 5.05 ether);

    assertEq(MockProtocolRewards(rewards).balanceOf(zoraDAO), 3.03 ether);
    assertEq(MockProtocolRewards(rewards).balanceOf(bidder1), 4.04 ether); // bidder get reward.
    assertEq(MockProtocolRewards(rewards).balanceOf(founder), 5.05 ether);
}
```

Even if user are prevented from referring to themselves, there are still ways to get around it (using another account of userâ€™s, colluding with the referring user, etc.).

So it's not a matter of whether user can or can't refer to themselves, it's a matter of the mathematics of how an incorrect `minBidIncrement` can result in user being the highest bidder while spending less than the highest price.

## Impact

Due to the referral rewards, user can be the highest bidder while spending less than the highest price.

## Code Snippet

[https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L432-L440](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L432-L440)

[https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L70](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L70)

## Tool used

Manual Review

## Recommendation

Forces the ratio of `minBidIncrement` to be greater than the ratio of `referralRewardsBPS` so that the user spends at least as much tokens as the highest price, even if they receive referral rewards.

```diff
constructor(
    address _manager,
    address _rewardsManager,
    address _weth,
    uint16 _builderRewardsBPS,
    uint16 _referralRewardsBPS
) payable initializer {
    manager = Manager(_manager);
    rewardsManager = IProtocolRewards(_rewardsManager);
    WETH = _weth;
    builderRewardsBPS = _builderRewardsBPS;
+   require(_referralRewardsBPS * 100 < INITIAL_MIN_BID_INCREMENT_PERCENT * BPS_PER_100_PERCENT);
    referralRewardsBPS = _referralRewardsBPS;
}

function setMinimumBidIncrement(uint256 _percentage) external onlyOwner whenPaused {
    if (_percentage == 0) {
        revert MIN_BID_INCREMENT_1_PERCENT();
    }
+   require(referralRewardsBPS * 100 < _percentage * BPS_PER_100_PERCENT);
    settings.minBidIncrement = SafeCast.toUint8(_percentage);

    emit MinBidIncrementPercentageUpdated(_percentage);
}
```