Wonderful Maroon Starfish

medium

# Specific bid amounts will not allow the auction to be settled and will DoS the Auction contract

## Summary

If a user bids an oddly specific ETH value, the sum of each reward may not be equal to the total reward amount which would cause the [`Auction._settleAuction`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L244-L289) to fail and break the auction flow.

## Vulnerability Detail

When the auction is settled, rewards are split between all the recipients with [`ProtocolRewards.depositBatch`](https://github.com/ourzora/zora-protocol/blob/main/packages/protocol-rewards/src/ProtocolRewards.sol#L46) function.

This function reverts if the sum of the `amounts` is not equal the `msg.value` ([Link](https://github.com/ourzora/zora-protocol/blob/main/packages/protocol-rewards/src/ProtocolRewards.sol#L63-L65))

```solidity
    if (msg.value != expectedTotalValue) {
        revert INVALID_DEPOSIT();
    }
```

In `Auction._settleAuction` function `msg.value` is the total rewards amount and the `amounts` are the rewards for each recipient. Both of these values are calculated from [`Auction._computeTotalRewards`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L465-L508) function.

```solidity
    RewardSplits memory split = _computeTotalRewards(currentBidReferral, highestBid, founderReward.percentBps);

    if (split.totalRewards != 0) {
        // Deposit rewards
        rewardsManager.depositBatch{ value: split.totalRewards }(split.recipients, split.amounts, split.reasons, "");
    }
```

Since total rewards amount and the rewards for each recipient are calculated in 2 seperate ways in `Auction._computeTotalRewards`, it opens up a possibility for where a user bids very specific amount that would cause the `ProtocolRewards.depositBatch` function to revert with `INVALID_DEPOSIT` error indefinitely.

### Proof of Concept

Here are 2 test examples:

#### Example 1

1. Add the test snippet in `Auction.t.sol` test.

```solidity
function test_auctionWithOddlySpecificValue() public {
  // Setup
  address bob = bidder1;
  deployAltMock(founder, 500);
  vm.prank(manager.owner());
  manager.registerUpgrade(auctionImpl, address(rewardImpl));

  vm.prank(auction.owner());
  auction.upgradeTo(address(rewardImpl));

  vm.prank(founder);
  auction.unpause();
  // End of setup

  // Bob bids for tokenId 2 with very specific amount - 0.019338670680736209 ETH
  vm.prank(bob);
  auction.createBid{ value: 19338670680736209 }(2);

  vm.warp(10 minutes + 1 seconds);

  // Expect this to revert from ProtocolRewards.sol contract with error INVALID_DEPOSIT()
  vm.expectRevert(abi.encodeWithSignature("INVALID_DEPOSIT()"));
  auction.settleCurrentAndCreateNewAuction();
}

```

2. Run the test with this command

```bash
forge test --mc AuctionTest --mt test_auctionWithOddlySpecificValue
```

3. The test will succeed.

#### Example 2 (fuzz test)

1. Add the test snippet in `Auction.t.sol` test.

```solidity
function testFuzz_auctionWithOddlySpecificValue(uint256 ethAmount) public {
  vm.assume(ethAmount < 10 ether);
  vm.assume(ethAmount > 0.015 ether);

  // Setup
  address bob = bidder1;
  deployAltMock(founder, 500);
  vm.prank(manager.owner());
  manager.registerUpgrade(auctionImpl, address(rewardImpl));

  vm.prank(auction.owner());
  auction.upgradeTo(address(rewardImpl));

  vm.prank(founder);
  auction.unpause();
  // End of setup

  // Bob bids for tokenId 2
  vm.prank(bob);
  auction.createBid{ value: ethAmount }(2);

  vm.warp(10 minutes + 1 seconds);

  auction.settleCurrentAndCreateNewAuction();
}

```

2. Run the test with this command

```bash
forge test --mc AuctionTest --mt testFuzz_auctionWithOddlySpecificValue
```

3. Test will fail because there will be at least one instance that makes the `ProtocolRewards.depositBatch` function revert with `INVALID_DEPOSIT` error.

## Impact

- `_settleAuction` function will always fail
- bid amount is stuck in the contract
- bidder will never receive the DAO Token
- a new auction cannot be started

## Code Snippet

https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L268-L273
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L465-L508

## Tool used

Manual Review

## Recommendation

Calculate the total rewards amount from the sum of each reward amount instead of calculating it separately.

Example:

```solidity
function _computeTotalRewards(
  address _currentBidRefferal,
  uint256 _finalBidAmount,
  uint256 _founderRewardBps
) internal view returns (RewardSplits memory split) {
  // Get global builder recipient from manager
  address builderRecipient = manager.builderRewardsRecipient();

  // Calculate the total rewards percentage
  uint256 totalBPS = _founderRewardBps + referralRewardsBPS + builderRewardsBPS;

  // Verify percentage is not more than 100
  if (totalBPS >= BPS_PER_100_PERCENT) {
    revert INVALID_REWARD_TOTAL();
  }

  // Check if founder reward is enabled
  bool hasFounderReward = _founderRewardBps > 0 && founderReward.recipient != address(0);

  // Set array size based on if founder reward is enabled
  uint256 arraySize = hasFounderReward ? 3 : 2;

  // Initialize arrays
  split.recipients = new address[](arraySize);
  split.amounts = new uint256[](arraySize);
  split.reasons = new bytes4[](arraySize);

  // Set builder reward
  split.recipients[0] = builderRecipient;
  split.amounts[0] = (_finalBidAmount * builderRewardsBPS) / BPS_PER_100_PERCENT;

  // Set referral reward
  split.recipients[1] = _currentBidRefferal != address(0) ? _currentBidRefferal : builderRecipient;
  split.amounts[1] = (_finalBidAmount * referralRewardsBPS) / BPS_PER_100_PERCENT;

  // Set founder reward if enabled
  if (hasFounderReward) {
    split.recipients[2] = founderReward.recipient;
    split.amounts[2] = (_finalBidAmount * _founderRewardBps) / BPS_PER_100_PERCENT;
  }

  // Set total rewards
  uint256 totalRewards;
  for (uint256 i = 0; i < split.amounts.length; i++) {
    totalRewards += split.amounts[i];
  }

  split.totalRewards = totalRewards;
}

```
