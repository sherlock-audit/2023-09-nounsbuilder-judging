Feisty Honey Sheep

medium

# [M] - Locking auctions

## Summary
The calculated value for totalRewards in the [_computeTotalRewards in auction contract](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L465) may not match the sum of fees for founders, referrals, and builders.

## Vulnerability Detail
Due rounding, the calculated fees for founders, referrals, and builders may not add up to the calculated value of totalRewards.

Refer to the code snippet section for an example.

## Impact
Depending on how the [depositBatch function in the rewardsManager contract](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L272) is working, this could result in:

However, since the rewardsManager contract is not accessible and also not in scope of this assessment, the following possible impact scenarios exist:

- medium [M] severity issue: Auctions become locked, if the depositBatch function necessitates the condition that the sum of the fees for founders, referrals, and builders must equal to totalRewards. Acctually, exactly on how the [MockProtocalRewards contract](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/test/utils/mocks/MockProtocolRewards.sol#L52) is enforcing it.
- low [L] severity issue: Ether dust accumulates in the rewardsManager contract due to the absence of an option to withdraw Ether. 
- no issue arises if the rewardsManager does not enforce the equivalence of the sum of fees to totalRewards and permits the withdrawal of any accumulated Ether dust.

The case medium [M] severity case means that the Ether from the bid is locked and can only be recovered with an upgrade of the auction contract. The NFT, worth the bid, may be lost, respectively has to recovered with special code in a upgrade auction contract.

The medium [M] severity case implies that the Ether from the bid is locked and can only be recovered through an upgrade of the auction contract. The NFT, worth the bid's value, may be at risk of being lost and may need to be recovered using special code in an upgraded auction contract.

## Code Snippet
Code snippet demonstrating that the sum of _founderRewardBps, referralRewardsBPS, and builderRewardsBPS does not always yield totalRewards

For example, using the following values when calling "_computeTotalRewards" in the code snippet will result in an "INVALID_DEPOSIT" error:
_finalBidAmount = 1999999999999999999999
_founderRewardBps = 2000
referralRewardsBPS = 1000
builderRewardsBPS = 1000

```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity 0.8.16;

/**
 * @title Demo
 * @dev 
 */
contract Auction {
    uint256 private constant BPS_PER_100_PERCENT = 10_000;

    struct RewardSplits {
        //// @notice Total rewards amount
        uint256 totalRewards;
        /// @param recipients recipients to send the amount to, array aligns with amounts
        address[] recipients;
        /// @param amounts amounts to send to each recipient, array aligns with recipients
        uint256[] amounts;
        /// @param reasons optional bytes4 hash for indexing
        bytes4[] reasons;
    }

    error INVALID_REWARD_TOTAL();
    error INVALID_DEPOSIT();

    function _computeTotalRewards(
        uint256 _finalBidAmount,
        uint256 _founderRewardBps,
        uint256 referralRewardsBPS,
        uint256 builderRewardsBPS
    ) external pure returns (RewardSplits memory split) {
        // Calculate the total rewards percentage
        uint256 totalBPS = _founderRewardBps + referralRewardsBPS + builderRewardsBPS;

        // Verify percentage is not more than 100
        if (totalBPS >= BPS_PER_100_PERCENT) {
            revert INVALID_REWARD_TOTAL();
        }

        // Calulate total rewards
        split.totalRewards = (_finalBidAmount * totalBPS) / BPS_PER_100_PERCENT;

        // Check if founder reward is enabled
        bool hasFounderReward = _founderRewardBps > 0;

        // Set array size based on if founder reward is enabled
        uint256 arraySize = hasFounderReward ? 3 : 2;

        // Initialize arrays
        split.recipients = new address[](arraySize);
        split.amounts = new uint256[](arraySize);
        split.reasons = new bytes4[](arraySize);

        // Set builder reward
        split.amounts[0] = (_finalBidAmount * builderRewardsBPS) / BPS_PER_100_PERCENT;

        // Set referral reward
        split.amounts[1] = (_finalBidAmount * referralRewardsBPS) / BPS_PER_100_PERCENT;

        // Set founder reward if enabled
        if (hasFounderReward) {
            split.amounts[2] = (_finalBidAmount * _founderRewardBps) / BPS_PER_100_PERCENT;
        }

        // Check according to mock
        uint256 numRecipients = split.amounts.length;
        uint256 expectedTotalValue;

        for (uint256 i; i < numRecipients; ) {
            expectedTotalValue += split.amounts[i];

            unchecked {
                ++i;
            }
        }

        if (split.totalRewards != expectedTotalValue) {
            revert INVALID_DEPOSIT();
        }


    }

} 
```

## Tool used
Manual Review

## Recommendation
Sum up the calculated split amounts in the "_computeTotalRewards" function to derive the totalRewards, rather than calculating totalRewards independently.