Thankful Gunmetal Pig

high

# Precision Loss in Reward Calculation Causes Protocol Malfunction and User Funds Lockup

## Summary
The protocol faces a precision loss issue in the calculation of rewards, where individual rewards calculated based on basis points (bps) suffer accuracy loss. Consequently, when these rewards are summed up in the `depositBatch()` function, the total may be less than or equal to the expected total, leading to a failed verification and potential disruptions in the deposit process.

## Vulnerability Detail
The `Auction._settleAuction()` function is intended to execute appropriate settlement actions based on the state and outcome of the auction, including reward distribution, fund transfer, and token handling.
```solidity
function _settleAuction() private {
        // Get a copy of the current auction
        Auction memory _auction = auction;

        // Ensure the auction wasn't already settled
        if (auction.settled) revert AUCTION_SETTLED();

        // Ensure the auction had started
        if (_auction.startTime == 0) revert AUCTION_NOT_STARTED();

        // Ensure the auction is over
        if (block.timestamp < _auction.endTime) revert AUCTION_ACTIVE();

        // Mark the auction as settled
        auction.settled = true;

        // If a bid was placed:
        if (_auction.highestBidder != address(0)) {
            // Cache the amount of the highest bid
            uint256 highestBid = _auction.highestBid;

            // If the highest bid included ETH: Pay rewards and transfer remaining amount to the DAO treasury
            if (highestBid != 0) {
                // Calculate rewards
                RewardSplits memory split = _computeTotalRewards(currentBidReferral, highestBid, founderReward.percentBps);

                if (split.totalRewards != 0) {
                    // Deposit rewards
                    rewardsManager.depositBatch{ value: split.totalRewards }(split.recipients, split.amounts, split.reasons, "");
                }

                // Deposit remaining amount to treasury
                _handleOutgoingTransfer(settings.treasury, highestBid - split.totalRewards);
            }

```
In the `_computeTotalRewards()` function, the protocol calculates the total rewards percentage (`split.totalRewards`) based on the sum of various basis points (BPS) representing different reward components. Subsequently, individual rewards are computed using these BPS values and stored in the `split.amounts[]` array. 
```solidity
  uint256 totalBPS = _founderRewardBps + referralRewardsBPS + builderRewardsBPS;

        // Verify percentage is not more than 100
        if (totalBPS >= BPS_PER_100_PERCENT) {
            revert INVALID_REWARD_TOTAL();
        }

        // Calulate total rewards
        split.totalRewards = (_finalBidAmount * totalBPS) / BPS_PER_100_PERCENT;

        // Check if founder reward is enabled
        bool hasFounderReward = _founderRewardBps > 0 && founderReward.recipient != address(0);

        // Set array size based on if founder reward is enabled
        uint256 arraySize = hasFounderReward ? 3 : 2;

        // Initialize arrays
        split.recipients = new address[](arraySize);
        split.amounts = new uint256[](arraySize);
        split.reasons = new bytes4[](arraySize);

        // Set builder reward
        split.recipients[0] = builderRecipient;
        split.amounts[0] = (_finalBidAmount * builderRewardsBPS) / BPS_PER_100_PERCENT;

        // Set referral reward
        split.recipients[1] = _currentBidRefferal != address(0) ? _currentBidRefferal : builderRecipient;
        split.amounts[1] = (_finalBidAmount * referralRewardsBPS) / BPS_PER_100_PERCENT;

        // Set founder reward if enabled
        if (hasFounderReward) {
            split.recipients[2] = founderReward.recipient;
            split.amounts[2] = (_finalBidAmount * _founderRewardBps) / BPS_PER_100_PERCENT;
        }


```


The total rewards are then deposited to the `rewardsManager` through the `depositBatch()` function.
```solidity

    if (split.totalRewards != 0) {
                    // Deposit rewards
                    rewardsManager.depositBatch{ value: split.totalRewards }(split.recipients, split.amounts, split.reasons, "");
                }

```
The issue arises due to precision loss in the calculation of individual rewards using BPS values. Since each reward component is calculated separately and rounded down to the nearest integer, the sum of these individual rewards may be less than or equal `to split.totalRewards`. This discrepancy becomes problematic when the protocol compares the calculated total rewards with msg.value during the deposit to the `rewardsManager`.

The relevant code snippet in the `depositBatch()` [function](https://github.com/ourzora/protocol-rewards/blob/92a2b234505f988c783b3f6e757ea8a24d159344/src/ProtocolRewards.sol#L55-L66):

```solidity
if (msg.value != expectedTotalValue) {
    revert INVALID_DEPOSIT();
}
```
This code checks whether the actual ETH value sent (`msg.value`) matches the expected total value of rewards (`expectedTotalValue`). Due to precision loss in the reward calculations, these values may not align as expected, leading to a failed validation (INVALID_DEPOSIT). 


## Impact

The auction settlement is unable to proceed, preventing the initiation of a new auction round. As a result, users' funds remain permanently locked in the contract.
## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L268-L273
## Tool used

Manual Review

## Recommendation
Change the value of `split.totalRewards` to `split.amounts[0] + split.amounts[1] + split.amounts[2]`.
