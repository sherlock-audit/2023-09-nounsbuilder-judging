Real Hazelnut Tiger

high

# Certain bid amounts will make it impossible to settle the auction

## Summary
When settling an auction the rewards are calculated by using `_computeTotalRewards()` and then deposited into the rewards manager by using `depositBatch()`.
```solidity
                // Calculate rewards
                RewardSplits memory split = _computeTotalRewards(currentBidReferral, highestBid, founderReward.percentBps);

                if (split.totalRewards != 0) {
                    // Deposit rewards
                    rewardsManager.depositBatch{ value: split.totalRewards }(split.recipients, split.amounts, split.reasons, "");
                }

```
As we can see in the rewards manager, the value sent must match the sum of the amounts of the parameters. Otherwise, the transaction will revert: https://github.com/ourzora/zora-protocol/blob/main/packages/protocol-rewards/src/ProtocolRewards.sol#L53-L65
```solidity
        uint256 expectedTotalValue;


        for (uint256 i; i < numRecipients; ) {
            expectedTotalValue += amounts[i];


            unchecked {
                ++i;
            }
        }


        if (msg.value != expectedTotalValue) {
            revert INVALID_DEPOSIT();
        }
```
As we can see, if this condition is not satisfied the transaction will revert, making it impossible to settle the auction. The problem arises here, since certain bid amounts will make this condition to not hold, thus reverting all the attempts of calling `_settleAuction()`. Note that a malicious user can make this happen on purpose anytime he wants, but normal users bids will also have the same effect with some amounts.

## Vulnerability Detail
The amounts to be send as parameters to 'depositBatch()` are calculated in `_computeTotalRewards()`. This amounts will have rounding problems of just a few weis with some bid amounts because of the divisions:
```solidity
        // Set builder reward
        split.recipients[0] = builderRecipient;
        split.amounts[0] = (_finalBidAmount * builderRewardsBPS) / BPS_PER_100_PERCENT;


        // Set referral reward
        split.recipients[1] = _currentBidRefferal != address(0) ? _currentBidRefferal : builderRecipient;
        split.amounts[1] = (_finalBidAmount * referralRewardsBPS) / BPS_PER_100_PERCENT;


        // Set founder reward if enabled
        if (hasFounderReward) {
            split.recipients[2] = founderReward.recipient;
            split.amounts[2] = (_finalBidAmount * _founderRewardBps) / BPS_PER_100_PERCENT;
        }
```
This will lead to `the sum of split.amounts != split.totalRewards`. 

## PoC
This tests is the same one as `test_BuilderAndReferralReward()`, the only difference is that the value used for bidding is different and the call to `auction.settleCurrentAndCreateNewAuction()` will revert.

```solidity
function test_FounderBuilderAndReferralRewardWithCertainAmount() external {
        // Setup
        deployAltMock(founder, 500);

        vm.prank(manager.owner());
        manager.registerUpgrade(auctionImpl, address(rewardImpl));

        vm.prank(auction.owner());
        auction.upgradeTo(address(rewardImpl));

        vm.prank(founder);
        auction.unpause();

        // Check reward values

        vm.prank(bidder1);
        auction.createBidWithReferral{ value: 8333333333333333333 }(2, referral); 

        vm.warp(10 minutes + 1 seconds);

        auction.settleCurrentAndCreateNewAuction();

    }
```
Note that this will happen with all the bids that have rounding problems when dividing. This is a pretty common case when working with Solidity. In fact, this problem will arise multiple times with "normal" bids, being possible for an attacker to make it in purpose anytime he wants.
## Impact
`_settleAuction()` will revert for auctions where the winning bid leads to rounding problems when calculating the rewards. Therefore, the NFT that was in auction and the highest bid will be stuck in the contract and the Auction contract will be unusable. This will lead to the auction winning user to not receiving his NFT and losing the funds he used for bidding. Also, the Auction contract would be unusable since it would get "stuck". 

Note that an attacker could create a DoS in all the auctions by exploiting this vulnerability without the need of using a lot of funds, by bidding in auctions with small bids or no bids. 

## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L244-L289
## Tool used

Manual Review and Foundry

## Recommendation
Change the approach you have for calculating the reward amounts so that `the sum of split.amounts == split.totalRewards` always, or change the contract `ProtocolRewards.sol`.