Itchy Mint Cow

high

# Update founders could lead to voting power calculation errors, due to the incorrect value of baseTokenId

## Summary
When users mint tokens, based on the specified percentage for founders, the tokens belonging to the founder will be minted for the designated founder。

When reservedUntilTokenId is set during initialization, baseTokenId will be calculated starting from the value of reservedUntilTokenId。
[token.sol#_addFounders](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L161#L175)
```solidity
  uint256 baseTokenId = reservedUntilTokenId;@audit <
  // For each token to vest:
  for (uint256 j; j < founderPct; ++j) {
      // Get the available token id
      baseTokenId = _getNextTokenId(baseTokenId);
      
      // Store the founder as the recipient
      tokenRecipient[baseTokenId] = newFounder;
      emit MintScheduled(baseTokenId, founderId, newFounder);

      // Update the base token id
      baseTokenId = (baseTokenId + schedule) % 100;
  }
```

However when invoke updateFounders `baseTokenId` is start from ZERO
[token.sol#updateFounders](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L412#L427)
```solidity
    uint256 baseTokenId; @audit <
    for (uint256 j; j < cachedFounder.ownershipPct; ++j) {
        // Get the next index that hasn't already been cleared
        while (clearedTokenIds[baseTokenId] != false) {
            baseTokenId = (++baseTokenId) % 100;
        }

        delete tokenRecipient[baseTokenId];
        clearedTokenIds[baseTokenId] = true;

        emit MintUnscheduled(baseTokenId, i, cachedFounder);

        // Update the base token id
        baseTokenId = (baseTokenId + schedule) % 100;
    }
```

This will lead to an error in the calculation of voting power after updating founders if reservedUntilTokenId has been set during initialization

## Vulnerability Detail
Assuming `reservedUntilTokenId` is setted to 1 during initialize
Let's just modify the `NounsBuilderTest `
```diff
@@ -128,7 +128,7 @@ contract NounsBuilderTest is Test {
             "ipfs://Qmew7TdyGnj6YRUjQR68sUJN3239MYXRD8uxowxF6rGK8j",
             "https://nouns.build",
             "http://localhost:5000/render",
-            0,
+            1,//@audit aa.
             address(0)
         );
     }
```

And add test to `Token.t.sol`
```diff
@@ -52,6 +52,61 @@ contract TokenTest is NounsBuilderTest, TokenTypesV1 {
         assertEq(token.totalSupply(), 0);
     }

+    function test_FounderShareAllocationSEP() public {
+        //set init paramaters.
+        address f1Wallet = address(0x1);
+        address f2Wallet = address(0x2);
+        address f3Wallet = address(0x3);
+        uint256 f1Percentage = 1;
+        uint256 f2Percentage = 2;
+        uint256 f3Percentage = 3;
+        address[] memory founders = new address[](3);
+        uint256[] memory percents = new uint256[](3);
+        uint256[] memory vestingEnds = new uint256[](3);
+        founders[0] = f1Wallet;
+        founders[1] = f2Wallet;
+        founders[2] = f3Wallet;
+
+        percents[0] = f1Percentage;
+        percents[1] = f2Percentage;
+        percents[2] = f3Percentage;
+
+        vestingEnds[0] = 4 weeks;
+        vestingEnds[1] = 4 weeks;
+        vestingEnds[2] = 4 weeks;
+
+        deployWithCustomFounders(founders, percents, vestingEnds);
+
+        percents[0] = 20;
+        percents[1] = 50;
+        percents[2] = 1;
+        for (uint256 i; i < founders.length; ++i) {
+        foundersArrNew.push();
+            foundersArrNew[i] = IManager.FounderParams({ wallet: founders[i], ownershipPct: percents[i], vestExpiry: vestingEnds[i] });
+        }
+
+        vm.prank(token.owner());
+        token.updateFounders(foundersArrNew);
+
+        // Mint 1000 tokens
+        for (uint256 i = 0; i < 1000; i++) {
+            vm.prank(address(auction));
+            token.mint();
+        }
+
+        uint256 f11 = token.getVotes(f1Wallet);
+        uint256 f22 = token.getVotes(f2Wallet);
+        uint256 f33 = token.getVotes(f3Wallet);
+        uint256 ac = token.getVotes(address(auction));
+
+        uint256 sum = f11+f22+f33+ac;
+        console2.log("total power:", sum);
+        console2.log("f1 percent:",uint256(f11 * 100/sum));
+        console2.log("f2 percent:",uint256(f22 * 100/sum));
+        console2.log("f3 percent:",uint256(f33 * 100/sum));
+        console2.log("auction percent:",uint256(ac * 100/sum));
+    }
```

Here goes the output:

```shell
$ forge test --match-test test_FounderShareAllocationSEP -vvv
[⠒] Compiling...
[⠒] Compiling 18 files with 0.8.16
[⠰] Solc 0.8.16 finished in 21.91s
Compiler run successful!

Running 1 test for test/Token.t.sol:TokenTest
[PASS] test_FounderShareAllocationSEP() (gas: 224547981)
Logs:
  total power: 4000
  f1 percent: 20
  f2 percent: 51
  f3 percent: 4
  auction percent: 25
```
We can see that the voting power percentage for f3Wallet has exceeded 4%, whereas it should have been set to 1%。

## Impact
the calculation of voting power error  ，because voting power is crucial for the DAO, I believe this should be considered an H issue 

## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L412#L427
```solidity
    uint256 baseTokenId;

    for (uint256 j; j < cachedFounder.ownershipPct; ++j) {
        // Get the next index that hasn't already been cleared
        while (clearedTokenIds[baseTokenId] != false) {
            baseTokenId = (++baseTokenId) % 100;
        }

        delete tokenRecipient[baseTokenId];
        clearedTokenIds[baseTokenId] = true;

        emit MintUnscheduled(baseTokenId, i, cachedFounder);

        // Update the base token id
        baseTokenId = (baseTokenId + schedule) % 100;
    }
```

## Tool used

Manual Review

## Recommendation
```diff
@@ -409,8 +406,7 @@ contract Token is IToken, VersionedContract, UUPS, Ownable, ReentrancyGuard, ERC
     uint256 schedule = 100 / cachedFounder.ownershipPct;

     // Used to reverse engineer the indices the founder has reserved tokens in.
-    uint256 baseTokenId;
-
+    uint256 baseTokenId = reservedUntilTokenId;
     for (uint256 j; j < cachedFounder.ownershipPct; ++j) {
```