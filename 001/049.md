Low Boysenberry Reindeer

high

# The old `tokenRecipient` cannot be entirely cleared in the `updateFounders` function if `reservedUntilTokenId` is not set to zero.

## Summary

If `reservedUntilTokenId` is not set to zero, the founders in the old `tokenRecipient` may not be deleted because the `baseTokenId` in the function `updateFounders` starts from zero, while the `baseTokenId` in the function `addFounders` starts from `reservedUntilTokenId`.

## Vulnerability Detail

When setting the `tokenRecipient` in function [`addFounders`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/token/Token.sol#L160-L175), the `baseTokenId` is start from `reservedUntilTokenId`.
```solidity
                // Used to store the base token id the founder will recieve
                uint256 baseTokenId = reservedUntilTokenId;

                // For each token to vest:
                for (uint256 j; j < founderPct; ++j) {
                    // Get the available token id
                    baseTokenId = _getNextTokenId(baseTokenId);

                    // Store the founder as the recipient
                    tokenRecipient[baseTokenId] = newFounder;

                    emit MintScheduled(baseTokenId, founderId, newFounder);

                    // Update the base token id
                    baseTokenId = (baseTokenId + schedule) % 100;
                }
```

While in funtion [`updateFounders`](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/token/Token.sol#L411-L427), `baseTokenId` is start from *ZERO*.
```solidity
                // Used to reverse engineer the indices the founder has reserved tokens in.
                uint256 baseTokenId;

                for (uint256 j; j < cachedFounder.ownershipPct; ++j) {
                    // Get the next index that hasn't already been cleared
                    while (clearedTokenIds[baseTokenId] != false) {
                        baseTokenId = (++baseTokenId) % 100;
                    }

                    delete tokenRecipient[baseTokenId];
                    clearedTokenIds[baseTokenId] = true;

                    emit MintUnscheduled(baseTokenId, i, cachedFounder);

                    // Update the base token id
                    baseTokenId = (baseTokenId + schedule) % 100;
                }
```

The algorithm for calculating the next `baseTokenId` remains the same. Consequently, if `reservedUntilTokenId` is not set to zero, the sequence of `baseTokenId` in these two functions may differ, resulting in the non-deletion of some founders from `tokenRecipient`.


## Impact

The founders in the old `tokenRecipient` can still receive tokens if they are not deleted in the `updateFounders` function, even though these founders may have been replaced by the new founders.

## Code Snippet

https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/token/Token.sol#L160-L175
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/token/Token.sol#L411-L427

## Tool used

Manual Review

## Recommendation

Maybe you can delete the `tokenRecipient` entries from index 0 to 99 to ensure that the old `tokenRecipient` is completely cleared. This can also prevent the impact of updates to `reservedUntilTokenId`.