Feisty Honey Sheep

high

# [H] - Former founders are not removed in all cases

## Summary
Formerly registered founders may persist in the "tokenRecipient" mapping in the [Token contract](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol) even after the execution of the "upgradeFounders" function. This could potentially result in former founders receiving an excess of tokens compared to their intended allocation.

## Vulnerability Detail
When the "_addFounders" function is executing, the starting value to determine the keys for the tokenRecipient mapping is [baseTokenId is reservedUntilTokenId](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L161).

However, during the exeuction of the "updateFounders" function, the starting key is [baseTokenId is zero ("0")](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L412), resulting in a different key when attempting to delete the tokenRecipient mapping for a specific founder.

Depending what on which value the reservedUntilTokenId has been set, the tokenRecipient mapping does not get (entirely) deleted, particularly when `(reservedUntilTokenId % 100) != 0`

## Impact
After executing "updateFounders," former founders may continue to receive tokens even when they should no longer be entitled to any, or they might receive an excessive amount of tokens.

A high rating is assigned due to the potential of this issue going undetected for an extended period, thereby accumulating significant influence over the DAO.

## Code Snippet
Code snippet demonstrating that not all founders are removed when the function upgradeFounders is called.

Example:
- addFounders(412)
- getTokenRecipiens -> 19 slots are reserved
- updateFounders()
- getTokenRecipiens -> still 19 slots occupied.

Please also note that helper function to count the slots provides only 19 slots in total, since the helper function iterates sums up [0,100[, but the code stores the first slot at tokenRecipient[reservedUntilTokenId], which is wrong as well.

```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity 0.8.16;

contract Founders {

    Settings internal settings;

    mapping(uint256 => Founder) internal founder;

    mapping(uint256 => Founder) internal tokenRecipient;

    struct Settings {
        address auction;
        uint88 totalSupply;
        uint8 numFounders;
        uint88 mintCount;
        uint8 totalOwnership;
    }

    struct Founder {
        address wallet;
        uint8 ownershipPct;
        uint32 vestExpiry;
    }

    error INVALID_FOUNDER_OWNERSHIP();

    function addFounders(uint256 reservedUntilTokenId) external {
        
        Founder[2] memory initFounders = [Founder(address(0x01), 10, 0), Founder(address(0x02), 10, 0)];
        _addFounders(initFounders, reservedUntilTokenId);
    }
  
    function _addFounders(Founder[2] memory _founders, uint256 reservedUntilTokenId) internal {
        // Used to store the total percent ownership among the founders
        uint256 totalOwnership;

        uint8 numFoundersAdded = 0;

        unchecked {
            // For each founder:
            for (uint256 i; i < _founders.length; ++i) {
                // Cache the percent ownership
                uint256 founderPct = _founders[i].ownershipPct;

                // Continue if no ownership is specified
                if (founderPct == 0) {
                    continue;
                }

                // Update the total ownership and ensure it's valid
                totalOwnership += founderPct;

                // Check that founders own less than 100% of tokens
                if (totalOwnership > 99) {
                    revert INVALID_FOUNDER_OWNERSHIP();
                }

                // Compute the founder's id
                uint256 founderId = numFoundersAdded++;

                // Get the pointer to store the founder
                Founder storage newFounder = founder[founderId];

                // Store the founder's vesting details
                newFounder.wallet = _founders[i].wallet;
                newFounder.vestExpiry = uint32(_founders[i].vestExpiry);
                // Total ownership cannot be above 100 so this fits safely in uint8
                newFounder.ownershipPct = uint8(founderPct);

                // Compute the vesting schedule
                // Marco: Every Xth belongs to founder
                uint256 schedule = 100 / founderPct;

                // Used to store the base token id the founder will recieve
                uint256 baseTokenId = reservedUntilTokenId;

                // For each token to vest:
                for (uint256 j; j < founderPct; ++j) {
                    // Get the available token id
                    baseTokenId = _getNextTokenId(baseTokenId);

                    // Store the founder as the recipient
                    tokenRecipient[baseTokenId] = newFounder;

                    // Update the base token id
                    baseTokenId = (baseTokenId + schedule) % 100;
                }
            }

            // Store the founders' details
            settings.totalOwnership = uint8(totalOwnership);
            settings.numFounders = numFoundersAdded;

        }
    }

    function getTokenRecipiens() external view returns (uint256) {
        // Cache the number of founders
        uint256 counter;

        for (uint256 i; i< 100; i++) {
            if (tokenRecipient[i].wallet != address(0)) {
                counter++;
            }

        }


        return counter;
    }

    function updateFounders() external {
        // Cache the number of founders
        uint256 numFounders = settings.numFounders;

        // Get a temporary array to hold all founders
        Founder[] memory cachedFounders = new Founder[](numFounders);

        // Cannot realistically overflow
        unchecked {
            // Add each founder to the array
            for (uint256 i; i < numFounders; ++i) {
                cachedFounders[i] = founder[i];
            }
        }

        // Keep a mapping of all the reserved token IDs we're set to clear.
        bool[] memory clearedTokenIds = new bool[](100);

        unchecked {
            // for each existing founder:
            for (uint256 i; i < cachedFounders.length; ++i) {
                // copy the founder into memory
                Founder memory cachedFounder = cachedFounders[i];

                // Delete the founder from the stored mapping
                delete founder[i];

                // Some DAOs were initialized with 0 percentage ownership.
                // This skips them to avoid a division by zero error.
                if (cachedFounder.ownershipPct == 0) {
                    continue;
                }

                // using the ownership percentage, get reserved token percentages
                uint256 schedule = 100 / cachedFounder.ownershipPct;

                // Used to reverse engineer the indices the founder has reserved tokens in.
                uint256 baseTokenId;
                for (uint256 j; j < cachedFounder.ownershipPct; ++j) {
                    // Get the next index that hasn't already been cleared
                    while (clearedTokenIds[baseTokenId] != false) {
                        baseTokenId = (++baseTokenId) % 100;
                    }

                    delete tokenRecipient[baseTokenId];
                    clearedTokenIds[baseTokenId] = true;

                    // Update the base token id
                    baseTokenId = (baseTokenId + schedule) % 100;
                }
            }
        }

        // Clear values from storage before adding new founders
        settings.numFounders = 0;
        settings.totalOwnership = 0;

    }

    function _getNextTokenId(uint256 _tokenId) internal view returns (uint256) {
        unchecked {
            while (tokenRecipient[_tokenId].wallet != address(0)) {
                _tokenId = (++_tokenId) % 100;
            }

            return _tokenId;
        }
    }
  
  } 

```

## Tool used
Manual Review

## Recommendation
Thoroughly delete registered tokenRecipients by iterating through all 100 possible slots.