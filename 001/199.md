Alert Lime Alpaca

high

# The first founder will lose ONE percentage ownership if more than 99 tokens are reserved for minters

## Summary
The first founder will lose 1 percentage of token ownership if more than 99 tokens are reserved for minters.

## Vulnerability Detail
[_addFounders(...)](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L120-L182) function is called upon initialization to add founders and compute their vesting allocations, the token indices is reserved in a mapping:
```solidity
// Used to store the base token id the founder will recieve
uint256 baseTokenId = reservedUntilTokenId;

// For each token to vest:
for (uint256 j; j < founderPct; ++j) {
    // Get the available token id
    baseTokenId = _getNextTokenId(baseTokenId);

    // Store the founder as the recipient
    tokenRecipient[baseTokenId] = newFounder;

    emit MintScheduled(baseTokenId, founderId, newFounder);

    // Update the base token id
    baseTokenId = (baseTokenId + schedule) % 100;
}
```
The `baseTokenId` is initialized as `reservedUntilTokenId`, and is calculated in [_getNextTokenId(...)](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L184-L194) function:
```solidity
/// @dev Finds the next available base token id for a founder
/// @param _tokenId The ERC-721 token id
function _getNextTokenId(uint256 _tokenId) internal view returns (uint256) {
    unchecked {
        while (tokenRecipient[_tokenId].wallet != address(0)) {
            _tokenId = (++_tokenId) % 100;
        }

        return _tokenId;
    }
}
```
When a new token is minted, protocol checks if a given token is for a founder in [_isForFounder(...)](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L261-L285) function:
```solidity
// Get the base token id
uint256 baseTokenId = _tokenId % 100;

// If there is no scheduled recipient:
if (tokenRecipient[baseTokenId].wallet == address(0)) {
    return false;

    // Else if the founder is still vesting:
    } else if (block.timestamp < tokenRecipient[baseTokenId].vestExpiry) {
        // Mint the token to the founder
        _mint(tokenRecipient[baseTokenId].wallet, _tokenId);

        return true;

    // Else the founder has finished vesting:
} else {
```
As we can see from above, the token is sent to the appropriate founder if `token mint ID % 100` was reserved. 

The problem is that if more than 99 tokens are reserved minters, for example, `reservedUntilTokenId` is 100, then `baseTokenId` is initialized as 100 in [_addFounders(...)](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L120-L182) function, then in the first iteration of the calculation, `baseTokenId` remains 100, results in `tokenRecipient[100] = newFounder`, meaning the first founder's token index of ONE percentage ownership is set to 100.

Because `token mint ID % 100` can never be 100, no token will be sent to founder, which means the first founder lose this ONE percentage ownership.

Please see blew test codes and run in `Auction.t.sol` to verify:
```solidity
   function test_Audit() public {
        deployMock();

        IManager.FounderParams[] memory newFoundersArr = new IManager.FounderParams[](1);
        // set founder, it is expected that founder will get 3 tokens for every 100 tokens minted
        newFoundersArr[0] = IManager.FounderParams({
            wallet: founder,
            ownershipPct: 3,
            vestExpiry: 2556057600
        });

        vm.startPrank(founder);
        token.setReservedUntilTokenId(100);
        token.updateFounders(newFoundersArr);
        // 1 token is minted
        auction.unpause();
        vm.stopPrank();

        // create auctions to mint 99 more tokens
        uint256 duration = auction.duration();
        for (uint256 i = 0; i < 99; i++) {
            vm.warp(block.timestamp + duration * (i + 1));
            auction.settleCurrentAndCreateNewAuction();
        }

        // loop through the first 100 tokens to get the number of tokens minted to founder
        uint256 num;
        uint256 reservedUntilTokenId = token.reservedUntilTokenId();
        for (uint256 tokenId = reservedUntilTokenId; tokenId < reservedUntilTokenId + 100; tokenId++) {
            try token.ownerOf(tokenId) returns (address owner) {
                if (owner == founder) {
                    num++;
                }
            } catch {
                continue;
            }
        }

        // founder only gets 2 tokens, not 3
        assertEq(num, 2);
    }
```

## Impact
Founder will receive less tokens.

## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L192

## Tool used
Manual Review

## Recommendation
```diff
When calculating the token indices for founders, should always return `_tokenId % 100`:
    function _getNextTokenId(uint256 _tokenId) internal view returns (uint256) {
        unchecked {
            while (tokenRecipient[_tokenId].wallet != address(0)) {
                _tokenId = (++_tokenId) % 100;
            }


-           return _tokenId;
+           return _tokenId % 100;
        }
    }
```