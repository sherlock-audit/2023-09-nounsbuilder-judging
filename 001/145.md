Furry Peach Turkey

high

# [High] The limitation of total ownership is calculated incorrectly during Token._addFounders(), which could result in incorrect founder's token IDs.

## Summary
Token contract have been updated with new feature [Token Reserve](https://hackmd.io/peXISQ2CSQOwRGmvpUpK9A?view#Token-Reserve). This change includes new storage variable **reservedUntilTokenId**, rework of  [_addFounders()](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L120-L182) and a lot of other logic updates to handle reservedUntilTokenId.

## Vulnerability Detail
During the [_addFounders()](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L120-L182) contract checking **totalOwnership** for founders. But it doesn't count newly added  **reservedUntilTokenId**. 
```solidity
                // Cache the percent ownership
                uint256 founderPct = _founders[i].ownershipPct;

                // Continue if no ownership is specified
                if (founderPct == 0) {
                    continue;
                }

                // Update the total ownership and ensure it's valid
                totalOwnership += founderPct;

                // Check that founders own less than 100% of tokens
                if (totalOwnership > 99) {
                    revert INVALID_FOUNDER_OWNERSHIP(); // @audit doesn't verify reservedUntilTokenId
                }
```

In case if **totalOwnership** + **reservedUntilTokenId** > 100 logic will end up with generating founder's token IDs less then **reservedUntilTokenId**.

Steps to reproduce:
1) Initialize token with **reservedUntilTokenId = 80** and founderList with **totalOwnership = 25**
2) [_addFounders()](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L120-L182) will be started during initialization.
3) After **totalOwnership** check( **totalOwnership = 25** < 99) [logic will calculate founder's tokenId to store it into **tokenRecipient[]**](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L160-L175).
Base tokenId will be same as **reservedUntilTokenId = 80**.
```solidity
                // Used to store the base token id the founder will recieve
                uint256 baseTokenId = reservedUntilTokenId;

                // For each token to vest:
                for (uint256 j; j < founderPct; ++j) {
                    // Get the available token id
                    baseTokenId = _getNextTokenId(baseTokenId);

                    // Store the founder as the recipient
                    tokenRecipient[baseTokenId] = newFounder;

                    emit MintScheduled(baseTokenId, founderId, newFounder);

                    // Update the base token id
                    baseTokenId = (baseTokenId + schedule) % 100;
                }
```
4) Implementation of [_getNextTokenId()](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L186-L194) doesn't check **reservedUntilTokenId** and has auto-increment of the ID. Because it works with modulo operation IDs > 100 will be cycled back to 1+ which is less than **reservedUntilTokenId = 80**.
```solidity
    function _getNextTokenId(uint256 _tokenId) internal view returns (uint256) {
        unchecked {
            while (tokenRecipient[_tokenId].wallet != address(0)) {
                _tokenId = (++_tokenId) % 100;
            }

            return _tokenId;
        }
    }
```
5) So, with **reservedUntilTokenId = 80** and **totalOwnership = 25** some tokens with IDs < **reservedUntilTokenId = 80** will be calculated for founders.
6) All founder's tokens under **reservedUntilTokenId** will be calculated for other users via [mintFromReserveTo()](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L211-L217).
7) And there is no option to mint them for founders [_mintWithVesting()](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L230-L243) because in this case **reservedUntilTokenId** check is added.

```solidity
            do {
                // Get the next token to mint
                tokenId = reservedUntilTokenId + settings.mintCount++;

                // Lookup whether the token is for a founder, and mint accordingly if so
            } while (_isForFounder(tokenId));
```
## Impact
In case if **totalOwnership** + **reservedUntilTokenId** > 100 founders will lose some of their vesting token ids.

Severity justification:
Marked it as High severity issue because [Token Reserve](https://hackmd.io/peXISQ2CSQOwRGmvpUpK9A?view#Token-Reserve) is a new core functionality of this update and the impact will cause founder to lose his vesting tokens.

## Code Snippet
[addFounders()](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L120-L182)
```solidity
    function _addFounders(IManager.FounderParams[] calldata _founders, uint256 reservedUntilTokenId) internal {
        // Used to store the total percent ownership among the founders
        uint256 totalOwnership;

        uint8 numFoundersAdded = 0;

        unchecked {
            // For each founder:
            for (uint256 i; i < _founders.length; ++i) {
                // Cache the percent ownership
                uint256 founderPct = _founders[i].ownershipPct;

                // Continue if no ownership is specified
                if (founderPct == 0) {
                    continue;
                }

                // Update the total ownership and ensure it's valid
                totalOwnership += founderPct;

                // Check that founders own less than 100% of tokens
                if (totalOwnership > 99) {
                    revert INVALID_FOUNDER_OWNERSHIP();
                }

                // Compute the founder's id
                uint256 founderId = numFoundersAdded++;

                // Get the pointer to store the founder
                Founder storage newFounder = founder[founderId];

                // Store the founder's vesting details
                newFounder.wallet = _founders[i].wallet;
                newFounder.vestExpiry = uint32(_founders[i].vestExpiry);
                // Total ownership cannot be above 100 so this fits safely in uint8
                newFounder.ownershipPct = uint8(founderPct);

                // Compute the vesting schedule
                uint256 schedule = 100 / founderPct;

                // Used to store the base token id the founder will recieve
                uint256 baseTokenId = reservedUntilTokenId;

                // For each token to vest:
                for (uint256 j; j < founderPct; ++j) {
                    // Get the available token id
                    baseTokenId = _getNextTokenId(baseTokenId);

                    // Store the founder as the recipient
                    tokenRecipient[baseTokenId] = newFounder;

                    emit MintScheduled(baseTokenId, founderId, newFounder);

                    // Update the base token id
                    baseTokenId = (baseTokenId + schedule) % 100;
                }
            }

            // Store the founders' details
            settings.totalOwnership = uint8(totalOwnership);
            settings.numFounders = numFoundersAdded;
        }
    }
```

## Tool used

Manual Review

## Recommendation
Include **reservedUntilTokenId** into calculation of **totalOwnership** and [_getNextTokenId()](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L186-L194) to bring consistency between reserved and founder's tokens.
