Proud Orange Blackbird

medium

# Founders may receive fewer tokens than expected.

## Summary
If a founder has a percentage `pct`, then `pct` IDs between 0 and 99 should be assigned to him in the `tokenRecipient` mapping. 
This ensures that if a token is minted with `tokenId % 100` equal to one of his assigned IDs, it is minted directly to him.
However, there is an error in the calculation of these `ID`s.
## Vulnerability Detail
There are reserved tokens for the `DAO`, and this value is stored in the `reservedUntilTokenId` variable.
This value is utilized when adding `founder`s.
```solidity
 _addFounders(_founders, _reservedUntilTokenId);
```
If a founder has a percentage (`pct`), we assign `pct` IDs that have not been assigned yet to that founder.
```solidity
uint256 baseTokenId = reservedUntilTokenId;
for (uint256 j; j < founderPct; ++j) {
      // Get the available token id
     baseTokenId = _getNextTokenId(baseTokenId);
     
     tokenRecipient[baseTokenId] = newFounder;
     baseTokenId = (baseTokenId + schedule) % 100;
}
```
However, there is an issue with the first `founder` when `reservedUntilTokenId` is larger than `100`. 
The `_getNextTokenId` function returns the `input parameter` if there is no token recipient for that `parameter`. 
```solidity
function _getNextTokenId(uint256 _tokenId) internal view returns (uint256) {
     unchecked {
         while (tokenRecipient[_tokenId].wallet != address(0)) {
             _tokenId = (++_tokenId) % 100;
         }
         return _tokenId;
     }
 }
```
For the first user, the first `parameter` is the same as `reservedUntilTokenId`, and evidently, there is no token `recipient` assigned for that specific `token ID`.
So, the `_getNextTokenId` function returns the `reservedUntilTokenId`, which is larger than `100` in this case.
When assigning the `token recipient` for the `first founder` using `tokenRecipient[baseTokenId] = newFounder`, an issue arises because `baseTokenId` is larger than `100`, and the assigned `pct IDs` should be lower than `100`. 
As a result, the `first founder` may receive fewer `tokens` than planned.

The POC test is as below.
```solidity
function test_AddFounders() public {
        deployAltMock(150);
        Founder memory f1 = token.getFounder(0);
        Founder memory f2 = token.getFounder(1);
        assertEq(f1.ownershipPct, 10);
        assertEq(f2.ownershipPct, 5);

        uint256 scheduledCnt = 0;
        for (uint256 i; i < 100; i++) {
            Founder memory f = token.getScheduledRecipient(i);
            if (f.wallet != address(0)) scheduledCnt++;
        }
        // The first founder received 9 tokens instead of 10 tokens.
        assertEq(scheduledCnt, 9 + 5);
 }
```

## Impact
When the reserved token number is greater than `100`, `founders` may receive fewer tokens than expected.
## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L92
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L161-L175
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L186-L194
## Tool used

Manual Review

## Recommendation
Please modify as below.
```solidity
- uint256 baseTokenId = reservedUntilTokenId;
+ uint256 baseTokenId = reservedUntilTokenId % 100;
```
