Brave Alabaster Copperhead

high

# updateFounders() may not be correctly clear old tokenRecipient

## Summary

in `updateFounders()`
The old `founder`'s `tokenRecipient` is not correctly cleared, causing the old `founder` still be able to obtain NFT.

## Vulnerability Detail
in `token.sol`
when `_addFounders()` ,  using `baseTokenId = reservedUntilTokenId`.
```solidity
    function _addFounders(IManager.FounderParams[] calldata _founders, uint256 reservedUntilTokenId) internal {
...
@>              uint256 baseTokenId = reservedUntilTokenId;

                // For each token to vest:
                for (uint256 j; j < founderPct; ++j) {
                    // Get the available token id
                    baseTokenId = _getNextTokenId(baseTokenId);

                    // Store the founder as the recipient
                    tokenRecipient[baseTokenId] = newFounder;

                    emit MintScheduled(baseTokenId, founderId, newFounder);

                    // Update the base token id
                    baseTokenId = (baseTokenId + schedule) % 100;
                }
            }
```

However, when `updateFounders()`, clearing the old `tokenRecipient[]` is using `baseTokenId=0`.

```solidity
   function updateFounders(IManager.FounderParams[] calldata newFounders) external onlyOwner {
...

                // Used to reverse engineer the indices the founder has reserved tokens in.
@>              uint256 baseTokenId;

                for (uint256 j; j < cachedFounder.ownershipPct; ++j) {
                    // Get the next index that hasn't already been cleared
                    while (clearedTokenIds[baseTokenId] != false) {
                        baseTokenId = (++baseTokenId) % 100;
                    }

@>                  delete tokenRecipient[baseTokenId];
                    clearedTokenIds[baseTokenId] = true;

                    emit MintUnscheduled(baseTokenId, i, cachedFounder);

                    // Update the base token id
                    baseTokenId = (baseTokenId + schedule) % 100;
                }
            }
        }
```

This leads to the fact that the old ones cannot be cleaned up properly.

Example
reservedUntilTokenId = 5

1. initialize has founder alice and founderPct = 10
because baseTokenId use from 5 so:
tokenRecipient[5,15,25,35,45,55,65,75,85,95].wallet = alice


2. owner execute updateFunder(bob)  and ownershipPct = 1
- because when clear  , baseTokenId use from 0 so:
  will delete  tokenRecipient[0,10,20,30,40,50,60,70,80,90].wallet
- then add bob tokenRecipient
  tokenRecipient[5].wallet = bob

In this way, tokenRecipient[15,25,35,45,55,65,75,85,95].wallet is not cleared, it is still `alice`.

Subsequently, `alice` can still automatically obtain NFT.

## POC
The following test demonstrates that `tokenRecipient[95]` still old founder

1. need change tokenRecipient to public , so can assertEq
```diff 
contract TokenStorageV1 is TokenTypesV1 {
    /// @notice The token settings
    Settings internal settings;

    /// @notice The vesting details of a founder
    /// @dev Founder id => Founder
    mapping(uint256 => Founder) internal founder;

    /// @notice The recipient of a token
    /// @dev ERC-721 token id => Founder
-   mapping(uint256 => Founder) internal tokenRecipient;
+   mapping(uint256 => Founder) public tokenRecipient;
}
```

2. add to `token.t.sol`
```solidity
    function test_notClean() public {
        //1.founder[0] = [5,15,25,35,45,55,65,75,85,95]
        //  founder[1] = [6,26,46,66,86]
        deployAltMock(5);

        IManager.FounderParams[] memory newFoundersArr = new IManager.FounderParams[](2);
        newFoundersArr[0] = IManager.FounderParams({
            wallet: address(0x06B59d0b6AdCc6A5Dc63553782750dc0b41266a3),
            ownershipPct: 1,
            vestExpiry: 2556057600
        });

        vm.prank(address(founder));
        //2.delete [0,10,20,30,40,50,60,70,80,90]
        //  delete [1,21,41,61,81]        
        //  add    [5]
        token.updateFounders(newFoundersArr);

        (address wallet ,,)= token.tokenRecipient(95);
        assertEq(wallet,founder); //still old founder
    }
```

```console
$ forge test -vvv --match-test test_notClean

Running 1 test for test/Token.t.sol:TokenTest
[PASS] test_notClean() (gas: 3311340)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 21.56ms
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact

`updateFounders()` cannot correctly clear the old funder's tokenRecipient, causing the old `funder` that has been removed to still automatically obtain NFT.

## Code Snippet

https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L412



## Tool used

Manual Review

## Recommendation

```diff
   function updateFounders(IManager.FounderParams[] calldata newFounders) external onlyOwner {
...

                // Used to reverse engineer the indices the founder has reserved tokens in.
-                uint256 baseTokenId;
+                uint256 baseTokenId = reservedUntilTokenId % 100;

                for (uint256 j; j < cachedFounder.ownershipPct; ++j) {
                    // Get the next index that hasn't already been cleared
                    while (clearedTokenIds[baseTokenId] != false) {
                        baseTokenId = (++baseTokenId) % 100;
                    }

                    delete tokenRecipient[baseTokenId];
                    clearedTokenIds[baseTokenId] = true;

                    emit MintUnscheduled(baseTokenId, i, cachedFounder);

                    // Update the base token id
                    baseTokenId = (baseTokenId + schedule) % 100;
                }
            }
        }
```
