Melodic Menthol Starfish

medium

# Insufficient input validation in the ```_addFounders``` function: ```newFounder``` can be the null address and ```totalOwnership``` can overflow

## Summary

In the [```_addFounders```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L120-L182) function of the [```Token```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol) contract, no check is being made on the ```wallet``` component of each ```newFounder```, which can lead to the ```address(0)``` being nominated as a founder.

Moreover, the ```founderPct``` variable can be too high, leading to a potential overflow of ```totalOwnership``` (as the whole code block is marked as ```unchecked```).

## Vulnerability Detail

Let's assume the ```owner``` (or ```manager```) uses an input defined as ```newFounders == [newFounder1, newFounder2]``` where ```newFounder1``` is defined such that 

```newFounder1.wallet == address1``` (```address1``` being a valid founder address) and ```newFounder1.ownershipPct == 50``` 

and ```newFounder2``` is defined such that 

```newFounder2.wallet == address(0)``` and ```newFounder2.ownershipPct == type(uint256).max``` 

in the [```_addFounders```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L120-L182) function (which is called in the ```initializer``` and the [```updateFounders```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L375C14-L437) function).

In such a case, we will have ```totalOwnership == 50``` after iterating through the first founder.
However, when reaching the second founder, ```totalOwnership``` will be set to ```49``` (```type(uint256).max + 50``` overflows as the whole code blocked is marked as ```unchecked```), meaning it will pass the check below

```solidity
if (totalOwnership > 99) {
                    revert INVALID_FOUNDER_OWNERSHIP();
 }
```

The second founder will then be added with a ```newFounder.wallet``` set to ```address(0)``` and a ```newFounder.ownershipPct``` set to ```255``` (which corresponds to the casting of ```type(uint256).max``` to ```uint8```), which is clearly invalid and could lead to greatly dangerous behaviors when using the contract (such as transferring more than the intended total rewards to the null address).

## Impact

We rate this vulnerability as MEDIUM as it leads to serious consequences (wrong ```totalOwnership```, reward percentage higher than ```100``` for ```address(0)```) but is highly unlikely as the affected functions are restricted to the ```manager``` / ```owner```.

## Code Snippet

https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L120-L182

## Tool used

Manual Review

## Recommendation

To fix this vulnerability, we need to add a ```require``` statement to the [```_addFounders```](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L120-L182) function checking that ```founderPct``` is lesser than 100.
We also need to add a check for the zero address in the same function.

The function would then look like the below

```solidity
function _addFounders(IManager.FounderParams[] calldata _founders, uint256 reservedUntilTokenId) internal {
        // Used to store the total percent ownership among the founders
        uint256 totalOwnership;

        uint8 numFoundersAdded = 0;

        unchecked {
            // For each founder:
            for (uint256 i; i < _founders.length; ++i) {
                // Cache the percent ownership
                uint256 founderPct = _founders[i].ownershipPct;

                require(founderPct <= 100, "founderPct too high");

                // Continue if no ownership is specified
                if (founderPct == 0) {
                    continue;
                }

                // Update the total ownership and ensure it's valid
                totalOwnership += founderPct;

                // Check that founders own less than 100% of tokens
                if (totalOwnership > 99) {
                    revert INVALID_FOUNDER_OWNERSHIP();
                }

                // Compute the founder's id
                uint256 founderId = numFoundersAdded++;

                // Get the pointer to store the founder
                Founder storage newFounder = founder[founderId];

                // Store the founder's vesting details
                newFounder.wallet = _founders[i].wallet;
                
                require(newFounder.wallet != address(0), "beneficiary cannot be the zero address");
                
                newFounder.vestExpiry = uint32(_founders[i].vestExpiry);
                // Total ownership cannot be above 100 so this fits safely in uint8
                newFounder.ownershipPct = uint8(founderPct);

                // Compute the vesting schedule
                uint256 schedule = 100 / founderPct;

                // Used to store the base token id the founder will recieve
                uint256 baseTokenId = reservedUntilTokenId;

                // For each token to vest:
                for (uint256 j; j < founderPct; ++j) {
                    // Get the available token id
                    baseTokenId = _getNextTokenId(baseTokenId);

                    // Store the founder as the recipient
                    tokenRecipient[baseTokenId] = newFounder;

                    emit MintScheduled(baseTokenId, founderId, newFounder);

                    // Update the base token id
                    baseTokenId = (baseTokenId + schedule) % 100;
                }
            }

            // Store the founders' details
            settings.totalOwnership = uint8(totalOwnership);
            settings.numFounders = numFoundersAdded;
        }
 }
```

