Round Chartreuse Yak

high

# The first founder lose a portion of his token ownership when `reservedUntilTokenId >= 100`.

## Summary
The first founder lose a portion of his token ownership when `reservedUntilTokenId >= 100`. The percentage of loss is `1/his_total_ownership`.

## Vulnerability Detail
Founders' vesting allocations are set by reserving an mapping of [0, 100) token indices, such that a new token mint ID % 100 is reserved, it is sent to the appropriate founder. Notice that the reserved token indices exclude 100. As shown in the following code ([L161-L169](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L161-L169)), the first reserved token index for the first founder is `reservedUntilTokenId`. If `reservedUntilTokenId >= 100`, then the index will be >= 100 and will be beyond the range of the reserved token indices.
```solidity
157:                // Compute the vesting schedule
158:                uint256 schedule = 100 / founderPct;
159:
160:                // Used to store the base token id the founder will recieve
161:->              uint256 baseTokenId = reservedUntilTokenId;
162:
163:                // For each token to vest:
164:                for (uint256 j; j < founderPct; ++j) {
165:                    // Get the available token id
166:->                  baseTokenId = _getNextTokenId(baseTokenId);
167:
168:                    // Store the founder as the recipient
169:->                  tokenRecipient[baseTokenId] = newFounder;
170:
171:                    emit MintScheduled(baseTokenId, founderId, newFounder);
172:
173:                    // Update the base token id
174:                    baseTokenId = (baseTokenId + schedule) % 100;
175:                }
```
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L157-L175

```solidity
186:    function _getNextTokenId(uint256 _tokenId) internal view returns (uint256) {
187:        unchecked {
188:->          while (tokenRecipient[_tokenId].wallet != address(0)) {
189:                _tokenId = (++_tokenId) % 100;
190:            }
191:
192:            return _tokenId;
193:        }
194:    }
```
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L186-L194

As a result, the first founder will never get the tokens for his first token index. Because the `baseTokenId` ([L265](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L265)) always is less than 100 (`baseTokenId = _tokenId % 100`), and never matches the founder's first index.

```solidity
263:    function _isForFounder(uint256 _tokenId) private returns (bool) {
264:        // Get the base token id
265:->      uint256 baseTokenId = _tokenId % 100;
266:
267:        // If there is no scheduled recipient:
268:        if (tokenRecipient[baseTokenId].wallet == address(0)) {
269:            return false;
270:
271:            // Else if the founder is still vesting:
272:        } else if (block.timestamp < tokenRecipient[baseTokenId].vestExpiry) {
273:            // Mint the token to the founder
274:->          _mint(tokenRecipient[baseTokenId].wallet, _tokenId);
275:
276:            return true;
```
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L263-L276

Let's add a test case to show the problem. 
```solidity
// 1. Set "reservedUntilTokenId" with 100 in "setMockTokenParams" (test/utils/NounsBuilderTest.sol#L123-L134)
123:    function setMockTokenParams() internal virtual {
124:        setTokenParams(
125:            "Mock Token",
126:            "MOCK",
127:            "This is a mock token",
128:            "ipfs://Qmew7TdyGnj6YRUjQR68sUJN3239MYXRD8uxowxF6rGK8j",
129:            "https://nouns.build",
130:            "http://localhost:5000/render",
131:->          100,  // set `reservedUntilTokenId` to 100
132:            address(0)
133:        );
134:    }

// 2. Add a test case to "Token.t.sol"
function test_AuctionMintToken100() public {
    deployMock();

    vm.prank(founder);
    auction.unpause();

    assertEq(token.reservedUntilTokenId(), uint256(100));
    assertEq(token.ownerOf(100), address(auction));
}
```
Run with `forge test -vvvvv --match-test test_AuctionMintToken100`, and part of the output is as follows. We can see that baseTokenId 100 is scheduled to founder in the first `MintScheduled` event log, but that token's owner is actually the auction contract.
```solidity
    │   │   │   ├─ [592074] Token::initialize ......
    │   │   │   │   ├─ emit MintScheduled(baseTokenId: 100, founderId: 0, founder: (0xd3562Fd10840f6bA56112927f7996B7c16edFCc1, 10, 2419200 [2.419e6]))
    │   │   │   │   ├─ emit MintScheduled(baseTokenId: 10, founderId: 0, founder: (0xd3562Fd10840f6bA56112927f7996B7c16edFCc1, 10, 2419200 [2.419e6]))
    │   │   │   │   ├─ emit MintScheduled(baseTokenId: 20, founderId: 0, founder: (0xd3562Fd10840f6bA56112927f7996B7c16edFCc1, 10, 2419200 [2.419e6]))
    │   │   │   │   ├─ emit MintScheduled(baseTokenId: 30, founderId: 0, founder: (0xd3562Fd10840f6bA56112927f7996B7c16edFCc1, 10, 2419200 [2.419e6]))
    
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 10.99ms
```

## Impact
The first founder lose a portion of his token ownership when `reservedUntilTokenId >= 100`. The percentage of loss is `1/his_total_ownership`.

## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L157-L175

https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L186-L194

https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L263-L276

## Tool used

Manual Review

## Recommendation
Fix the `_getNextTokenId` function, making it always return a `_tokenId`` less than 100.
```solidity
@@ -184,6 +184,7 @@ contract Token is IToken, VersionedContract, UUPS, Ownable, ReentrancyGuard, ERC
     /// @dev Finds the next available base token id for a founder
     /// @param _tokenId The ERC-721 token id
     function _getNextTokenId(uint256 _tokenId) internal view returns (uint256) {
+        _tokenId = _tokenId % 100;
         unchecked {
             while (tokenRecipient[_tokenId].wallet != address(0)) {
                 _tokenId = (++_tokenId) % 100;
```