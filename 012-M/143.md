Basic Mauve Sealion

medium

# Incorrect initialization of `Token.sol` contract

## Summary

`__EIP712_init` is never initialized in `initialize` function in `Token` contract.

## Vulnerability Detail

The `Token` contract inherits `ERC721Votes` contract which inherits `EIP712` contract, however `__EIP712_init` is never invoked during initialization in `Token` contract. It is also not invoked in `ERC721Votes`. State of `EIP712` remains uninitialized which affects `delegateBySig` function in `ERC721Votes`.

```solidity
contract Token is ... ERC721Votes, ... {

function initialize(
        IManager.FounderParams[] calldata _founders,
        bytes calldata _initStrings,
        uint256 _reservedUntilTokenId,
        address _metadataRenderer,
        address _auction,
        address _initialOwner
    ) external initializer {
        // Ensure the caller is the contract manager
        if (msg.sender != address(manager)) {
            revert ONLY_MANAGER();
        }

        // Initialize the reentrancy guard
        __ReentrancyGuard_init();

        // Setup ownable
        __Ownable_init(_initialOwner);

        // Store the founders and compute their allocations
        _addFounders(_founders, _reservedUntilTokenId);

        // Decode the token name and symbol
        (string memory _name, string memory _symbol, , , , ) = abi.decode(_initStrings, (string, string, string, string, string, string));

        // Initialize the ERC-721 token
        __ERC721_init(_name, _symbol);

        // Store the metadata renderer and auction house
        settings.metadataRenderer = IBaseMetadata(_metadataRenderer);
        settings.auction = _auction;
        reservedUntilTokenId = _reservedUntilTokenId;
    }
}
```

```solidity
abstract contract ERC721Votes is IERC721Votes, EIP712, ERC721 {
    ...
}
```

As we can see `initialize` in `Token` contract invokes `__ERC721_init` but does not invoke `__EIP712_init`.

Due to this issue state variables like `HASHED_NAME`, `HASHED_VERSION`, `INITIAL_CHAIN_ID` and `INITIAL_DOMAIN_SEPARATOR` are never initialized. These variables are used in the `delegateBySig` function in `ERC721Votes` contract. The non-initialization of `EIP712` contract breaks the compatibility of `Token` contract with the EIP712 standard.

`delegateBySig` function uses `DOMAIN_SEPARATOR()` while calculating the value of `digest`. It means that protocol wants to utilze delegation by signature which requires EIP712 compatibility.

```solidity
function delegateBySig(
        address _from,
        address _to,
        uint256 _deadline,
        uint8 _v,
        bytes32 _r,
        bytes32 _s
    ) external {
        // Ensure the signature has not expired
        if (block.timestamp > _deadline) revert EXPIRED_SIGNATURE();

        // Used to store the digest
        bytes32 digest;

        // Cannot realistically overflow
        unchecked {
            // Compute the hash of the domain seperator with the typed delegation data
            digest = keccak256(
                abi.encodePacked("\x19\x01", DOMAIN_SEPARATOR(), keccak256(abi.encode(DELEGATION_TYPEHASH, _from, _to, nonces[_from]++, _deadline)))
            );
        }

        // Recover the message signer
        address recoveredAddress = ecrecover(digest, _v, _r, _s);

        // Ensure the recovered signer is the voter
        if (recoveredAddress == address(0) || recoveredAddress != _from) revert INVALID_SIGNATURE();

        // Update the delegate
        _delegate(_from, _to);
    }
```

```solidity
    digest = keccak256(
        abi.encodePacked("\x19\x01", DOMAIN_SEPARATOR(), keccak256(abi.encode(DELEGATION_TYPEHASH, _from, _to, nonces[_from]++, _deadline)))
    );
```

## Code Snippet

https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L72-L104

https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/lib/token/ERC721Votes.sol#L15

## Tool used

Manual Review

## Recommendation

Add `__EIP712_init` with desired `_name` and `_version` parameters to the `Token` `initialize` function to initialize the `EIP712` contract.
