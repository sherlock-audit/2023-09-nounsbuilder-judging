Mammoth Aqua Worm

high

# DoS of the current auction settlement due to inaccurate calculation of rewards

## Summary
The current implementation of the `Auction` contract has a vulnerability that could lead to a denial of service (DoS) attack. The vulnerability is related to the inaccurate calculation of rewards, which could cause the sum of the splitted amounts to be different from the total rewards value. This inconsistency could lead to the permanent blocking of the Auction contract with the highest bid stucking.

## Vulnerability Detail
The `Auction._computeTotalRewards` function calculates `split.totalRewards` separately from splitted rewards. It leads to the sum of splitted amounts not always equal to the `split.totalRewards` value.
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L473-L507
For example:
builderRewardsBPS = 300;
referralRewardsBPS = 400;
_founderRewardBps = 3000;
_finalBidAmount = 111111;

split.totalRewards = 11111 * 3700 / 10000 = 41111;
split.amounts[0] = 111111 * 300 / 10000 = 3333;
split.amounts[1] = 111111 * 400 / 10000 = 4444;
split.amounts[2] = 111111 * 3000 / 10000 = 33333;
sum = 41110;

The `split` struct is used by the `_settleAuction` function to send rewards at the zora-protocol's ProtocolRewards contract:
```solidity
                if (split.totalRewards != 0) {
                    // Deposit rewards
                    rewardsManager.depositBatch{ value: split.totalRewards }(split.recipients, split.amounts, split.reasons, "");
                }
```
The eqality of the sum of splitted amounts with the `split.totalRewards` value is really important because the [`depositBatch` function](https://github.com/ourzora/zora-protocol/blob/ea27f01693769fb3d85fc518be196acd15fa0664/packages/protocol-rewards/src/ProtocolRewards.sol#L55-L65) reverts in case of inconsistency:
```solidity
        for (uint256 i; i < numRecipients; ) {
            expectedTotalValue += amounts[i];

            unchecked {
                ++i;
            }
        }

        if (msg.value != expectedTotalValue) {
            revert INVALID_DEPOSIT();
        }
```
Thus the `_settleAuction` will always revert and a new auction won't be created.

## Impact
Permanent DoS of the `Auction` contract.

## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L473-L507
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L270-L273

## Tool used
Manual Review

## Recommendation
Consider calculating `split.totalRewards` as a sum of splitted rewards:
```diff
-       // Calulate total rewards
-       split.totalRewards = (_finalBidAmount * totalBPS) / BPS_PER_100_PERCENT;
+       // Initialize variables for total rewards calculation
+       uint256 totalRewards;
+       uint256 amounts;

        // Check if founder reward is enabled
        bool hasFounderReward = _founderRewardBps > 0 && founderReward.recipient != address(0);

        // Set array size based on if founder reward is enabled
        uint256 arraySize = hasFounderReward ? 3 : 2;

        // Initialize arrays
        split.recipients = new address[](arraySize);
        split.amounts = new uint256[](arraySize);
        split.reasons = new bytes4[](arraySize);

        // Set builder reward
        split.recipients[0] = builderRecipient;
-       split.amounts[0] = (_finalBidAmount * builderRewardsBPS) / BPS_PER_100_PERCENT;
+       amounts = (_finalBidAmount * builderRewardsBPS) / BPS_PER_100_PERCENT;
+       totalRewards += amounts;
+       split.amounts[0] = amounts;

        // Set referral reward
        split.recipients[1] = _currentBidRefferal != address(0) ? _currentBidRefferal : builderRecipient;
-       split.amounts[1] = (_finalBidAmount * referralRewardsBPS) / BPS_PER_100_PERCENT;
+       amounts = (_finalBidAmount * referralRewardsBPS) / BPS_PER_100_PERCENT;
+       totalRewards += amounts;
+       split.amounts[1] = amounts;

        // Set founder reward if enabled
        if (hasFounderReward) {
            split.recipients[2] = founderReward.recipient;
-           split.amounts[2] = (_finalBidAmount * _founderRewardBps) / BPS_PER_100_PERCENT;
+           amounts = (_finalBidAmount * _founderRewardBps) / BPS_PER_100_PERCENT;
+           totalRewards += amounts;
+           split.amounts[2] = amounts;
        }

+       // Set total rewards
+       split.totalRewards = totalRewards;
```