Lively Cinnabar Sidewinder

medium

# Auction settlement could be DOSed due to inconsistency between calculated and deposited reward amounts

## Summary
It could become impossible to settle an auction (blocking ability to start new auctions) if the total ETH attempted to be deposited into the rewards contract does not match with the total calculated from the separate reward amounts.

## Vulnerability Detail
In `_settleAuction` in `Auction.sol`, the reward amounts for each receiver (builder DAO, referrer if specified, possibly founder) are calculated in `_computeTotalRewards`.
`split.totalRewards` is calculated using the formula `finalBidAmount * (founderRewardsBps + referralRewardsBPS + builderRewardsBPS) / 10000` ie. taking the portion of the bid amount represented by the total reward BPS.
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L474-L482
```solidity
        uint256 totalBPS = _founderRewardBps + referralRewardsBPS + builderRewardsBPS;
        ...
        // Calulate total rewards
        split.totalRewards = (_finalBidAmount * totalBPS) / BPS_PER_100_PERCENT;
```
The separate reward amounts are then calculated using the formula `finalBidAmount * rewardBPS / 10000` where rewardBPS refers to the percentage of the bid allocated to a specific receiver. There is always at least 2 reward amounts calculated this way.
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L496-L507
```solidity
        split.recipients[0] = builderRecipient;
        split.amounts[0] = (_finalBidAmount * builderRewardsBPS) / BPS_PER_100_PERCENT;

        // Set referral reward
        split.recipients[1] = _currentBidRefferal != address(0) ? _currentBidRefferal : builderRecipient;
        split.amounts[1] = (_finalBidAmount * referralRewardsBPS) / BPS_PER_100_PERCENT;

        // Set founder reward if enabled
        if (hasFounderReward) {
            split.recipients[2] = founderReward.recipient;
            split.amounts[2] = (_finalBidAmount * _founderRewardBps) / BPS_PER_100_PERCENT;
        }
```
These separately calculated amounts are used as input for when calling `rewardsManager.depositBatch`, while the actual ETH sent is `split.totalRewards`.

The issue is the sum of the separate reward amounts may not be equal to `split.totalRewards` due to truncation on division (e.g. when finalBidAmount % 10000 != 0, though this does not cover all cases). Consequently, `rewardsManager.depositBatch` will always revert, blocking auction settlement.
https://github.com/ourzora/zora-protocol/blob/ea27f01693769fb3d85fc518be196acd15fa0664/packages/protocol-rewards/src/ProtocolRewards.sol#L55-L65
```solidity
        for (uint256 i; i < numRecipients; ) {
            expectedTotalValue += amounts[i];

            unchecked {
                ++i;
            }
        }

        if (msg.value != expectedTotalValue) {
            revert INVALID_DEPOSIT();
        }
```

This could occur through normal operations (e.g. if winner is bidding ETH based on dollar value) or due to a griefing attack (aiming for DOS).

Paste the following test into `Auction.t.sol`. It proves there exists values for which this happens.
```solidity
function test_DepositRevert(uint256 highestBid) public {
        vm.assume(highestBid >= 1 ether && highestBid <= 2 ether);

        deployAltMock(founder, 500);

        vm.prank(manager.owner());
        manager.registerUpgrade(auctionImpl, address(rewardImpl));

        vm.prank(auction.owner());
        auction.upgradeTo(address(rewardImpl));

        vm.prank(founder);
        auction.unpause();

        vm.prank(bidder1);
        auction.createBidWithReferral{ value: highestBid }(2, referral);

        vm.warp(10 minutes + 1 seconds);

        auction.settleCurrentAndCreateNewAuction();
    }
```

## Impact
The auction contract for the affected DAO will be DOSed since there is no way to start a new auction if the previous one has not been settled, preventing minting of any new non reserved tokens. Additionally, there is no way to retrieve the ETH for the winning bid, the winner does not receive their token, and the reward recipients do not receive their rewards.

## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L261-L273
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L465-L508
https://github.com/ourzora/zora-protocol/blob/ea27f01693769fb3d85fc518be196acd15fa0664/packages/protocol-rewards/src/ProtocolRewards.sol#L55-L65

## Tool used

Manual Review

## Recommendation
Calculate `split.totalReward` using the sum of the individually calculated reward amounts.