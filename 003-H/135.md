Skinny Frost Puppy

high

# auction settlements will revert because function `_computeTotalRewards()` calculates inconsistent totalReward and reward list

## Summary
function `settleAuction()` uses function `_computeTotalRewards()` to computes the total rewards for highest bid and then distributes rewards by sending to the rewardManager. function `_computeTotalRewards()` returns `totalRewards` and list containing each reward amount. because of rounding error in the they way `totalRewards` is calculated the `totalRewards` will be sum of each rewards, so when  function `rewardsManager.depositBatch()` is called, the transferred ETH will not be equal to sum of the rewards and that call will revert and it will cause the whole transaction to revert.

## Vulnerability Detail
This is part of `settleAuction()` code, as you can see it calls `_computeTotalRewards()` and then use the return value to deposit the rewards in rewardsManager contract:
```javascript
                if (highestBid != 0) {
                // Calculate rewards
                RewardSplits memory split = _computeTotalRewards(currentBidReferral, highestBid, founderReward.percentBps);

                if (split.totalRewards != 0) {
                    // Deposit rewards
                    rewardsManager.depositBatch{ value: split.totalRewards }(split.recipients, split.amounts, split.reasons, "");
                }
```

This is `_computeTotalRewards()` code:
```javascript
    function _computeTotalRewards(        address _currentBidRefferal,        uint256 _finalBidAmount,        uint256 _founderRewardBps    ) nternal view returns (RewardSplits memory split) {
        address builderRecipient = manager.builderRewardsRecipient();

        uint256 totalBPS = _founderRewardBps + referralRewardsBPS + builderRewardsBPS;

        if (totalBPS >= BPS_PER_100_PERCENT) {
            revert INVALID_REWARD_TOTAL();
        }

        split.totalRewards = (_finalBidAmount * totalBPS) / BPS_PER_100_PERCENT;

        bool hasFounderReward = _founderRewardBps > 0 && founderReward.recipient != address(0);

        uint256 arraySize = hasFounderReward ? 3 : 2;

        split.recipients = new address[](arraySize);
        split.amounts = new uint256[](arraySize);
        split.reasons = new bytes4[](arraySize);

        // Set builder reward
        split.recipients[0] = builderRecipient;
        split.amounts[0] = (_finalBidAmount * builderRewardsBPS) / BPS_PER_100_PERCENT;

        // Set referral reward
        split.recipients[1] = _currentBidRefferal != address(0) ? _currentBidRefferal : builderRecipient;
        split.amounts[1] = (_finalBidAmount * referralRewardsBPS) / BPS_PER_100_PERCENT;

        // Set founder reward if enabled
        if (hasFounderReward) {
            split.recipients[2] = founderReward.recipient;
            split.amounts[2] = (_finalBidAmount * _founderRewardBps) / BPS_PER_100_PERCENT;
        }
    }
```
as you can see first `totalRewards` has been calculated based on `(finalBidAmount * totalBPS) / BPS_PER_100_PERCENT` then each reward is calculated by `(finalBidAmount * rewardBPS) / BPS_PER_100_PERCENT`, so the division error that happens `totalRewards` calculation is different than 3 separate reward calculation division error. for example let's assume:
1. builderRewardsBPS = 1, referralRewardsBPS = 1, founderRewardBps=1, finalBidAmount=150
2. then totalRewards would be `150 * (1+1+1) / 100 = 450/100 = 4`
3. then builderRewards, referralRewardswould and founderReward would be `150 * 1 / 100 = 1`
4. as you can see total reward is 4 while each of them is 1, so sum of the total rewards doesn't equal the `totalReward`

so overall this will cause sending inconsistent argument and ETH to RewardsManager contract and because of the check in the RewadsManager contract that checks `msg.value == sum(amounts in input list)` the call to the RewardsManager would revert and the whole `settleAuction()` would revert.

the RewardsManager is not in the scope but according to the Mock contract code checks this property(sum of amounts in the function input should equal to `msg.sender`, and this a common check in functions that receive ETH and list of amounts)
```javascript
    function depositBatch(
        address[] calldata recipients,
        uint256[] calldata amounts,
        bytes4[] calldata reasons,
        string calldata
    ) external payable {
        uint256 numRecipients = recipients.length;

        if (numRecipients != amounts.length || numRecipients != reasons.length) {
            revert ARRAY_LENGTH_MISMATCH();
        }

        uint256 expectedTotalValue;

        for (uint256 i; i < numRecipients; ) {
            expectedTotalValue += amounts[i];

            unchecked {
                ++i;
            }
        }

        if (msg.value != expectedTotalValue) {
            revert INVALID_DEPOSIT();
        }
```

## Impact
Auction won't be settled in most of the time and highest bidder funds would be locked and Auction contract core functionality will be broken. 

## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/test/utils/mocks/MockProtocolRewards.sol#L42-L54
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L268-L273
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L474-L507

## Tool used
Manual Review

## Recommendation
code should first calculate the amount of  builderRewards, referralRewardswould and founderReward and then set totalRewards as their sum to make sure totalRewards correctly represent sum of rewards.