Warm Ceramic Jay

medium

# Attacker is able to get NFTs multiple times under some circumestances

Hello,

## Summary
The ERC712 logic in minting depends on that the tokenId owner is address 0 (ERC721.sol#L194), if not the mint will reverts, and it's burn deletes the owner[tokenId] which makes owner[tokenId] is address(0), means this token can be minted again when burnt,

The [Nouns builder ERC721 token](https://github.com/sherlock-audit/2023-09-nounsbuilder-ProgramFilesx86/tree/0c3c712e9b7143223e36d56dcfbfb81506851e4b/nouns-protocol/src/token/Token.sol) allows minters to burn any NFT they own (Line 293), and Minters can be set by owners as contracts or individuals and are not constants (updateMinters function in line 465 - https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L465C3-L476C6),

By time owners obviously will integrate bridges or other contracts as minters to manage more the overflow of the Defi, taking a bridge as example they uses the burn functionality to move some nft to other chain ...etc 
Attackers by these logics, they can mint and burn for profit and then re-mint again due to the missing check explained below in MerkleReserveMinter.mintFromReserve 

## Vulnerability Detail
The `MerkleReserveMinter.mintFromReserve` function allows users to get their NFTs minted using the settings merklee root and the claims merkle proof, the mintFromReserve function anyways checks if the claim is called during the allowed time however it does not check if the user has already claimed or not, as it relies on ERC721.mint function's logic (if owner[tokenId] == address(0) the mint will fails, and since minter role is updatable and can burn any token which this will sets owner[tokenId] as address(0), the following scenario may occurs :

* Owners add bridge as minter role
* Alice have token with id 70 eligible to be minted via `MerkleReserveMinter.mintFromReserve`
* Alice calls MerkleReserveMinter.mintFromReserve and mints his token with id 70
* Alice uses the bridge with minter role on The [Nouns builder ERC721 token](https://github.com/sherlock-audit/2023-09-nounsbuilder-ProgramFilesx86/tree/0c3c712e9b7143223e36d56dcfbfb81506851e4b/nouns-protocol/src/token/Token.sol) 
* Bridge will get the token with 70 and burn it, now owner[70] == address(0), userA will gets an NFT on the other chain,
* Alice re-calls the MerkleReserveMinter.mintFromReserve and mints his token with id 70 again on [Nouns builder ERC721 token](https://github.com/sherlock-audit/2023-09-nounsbuilder-ProgramFilesx86/tree/0c3c712e9b7143223e36d56dcfbfb81506851e4b/nouns-protocol/src/token/Token.sol) 

And many other scenarios may occurs if Alice is able to transfer their NFT to any minter that they can control it's burn later,

## Impact
Users will be able to mint a token ID, they get it's profit and re-mint it over and over

## Code Snippet

See in MerkleReserveMinter that there is no check that the claim.mintTo with the specified claim.tokenId has been earned previously or not (which means it can be used over again as long it's on the settings period)
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/minters/MerkleReserveMinter.sol#L129

```solidity
 function mintFromReserve(address tokenContract, MerkleClaim[] calldata claims) public payable {
        MerkleMinterSettings memory settings = allowedMerkles[tokenContract];
        uint256 claimCount = claims.length;

        // Ensure claims are not empty
        if (claimCount == 0) {
            revert INVALID_CLAIM_COUNT();
        }

        // Check sale end
        if (block.timestamp > settings.mintEnd) {
            revert MINT_ENDED();
        }

        // Check sale start
        if (block.timestamp < settings.mintStart) {
            revert MINT_NOT_STARTED();
        }

        // Check value sent
        if (msg.value < _getTotalFeesForMint(settings.pricePerToken, claimCount)) {
            revert INVALID_VALUE();
        }

        // Mint tokens
        unchecked {
            for (uint256 i = 0; i < claimCount; ++i) {
                // Load claim in memory
                MerkleClaim memory claim = claims[i];

                // Requires one proof per tokenId to handle cases where users want to partially claim
                if (!MerkleProof.verify(claim.merkleProof, settings.merkleRoot, keccak256(abi.encode(claim.mintTo, claim.tokenId)))) {
                    revert INVALID_MERKLE_PROOF(claim.mintTo, claim.merkleProof, settings.merkleRoot);
                }

                // Only allowing reserved tokens to be minted for this strategy
                IToken(tokenContract).mintFromReserveTo(claim.mintTo, claim.tokenId);
            }
        }

        // Distribute fees if minting fees for this collection are set (Builder DAO fee does not apply to free mints)
        if (settings.pricePerToken > 0) {
            _distributeFees(tokenContract, claimCount);
        }
    }

```

**NFT will make owner is address(0) (deleted) when the nft is burning**
OZ erc721
```solidity
    function _burn(uint256 _tokenId) internal virtual {
        address owner = owners[_tokenId];

        if (owner == address(0)) revert NOT_MINTED();

        _beforeTokenTransfer(owner, address(0), _tokenId);

        unchecked {
            --balances[owner];
        }

        delete owners[_tokenId];

        delete tokenApprovals[_tokenId];
```

**NFT will be minted as long as the owner is address(0)**
OZ erc721
```solidity
        if (owners[_tokenId] != address(0)) revert ALREADY_MINTED();
```


**Minters are updatable by owners**
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/token/Token.sol#L465C3-L476C6
```solidity
  function updateMinters(MinterParams[] calldata _minters) external onlyOwner {
        // Update each minter
        for (uint256 i; i < _minters.length; ++i) {
            // Skip if the minter is already set to the correct value
            if (minter[_minters[i].minter] == _minters[i].allowed) continue;

            emit MinterUpdated(_minters[i].minter, _minters[i].allowed);

            // Update the minter
            minter[_minters[i].minter] = _minters[i].allowed;
        }
    }
```

## Tool used
Manual Review

## Recommendation
Add a mapping for each tokenId when has been used in MerkleReserveMinter.mintFromReserve,

```solidity
mapping(uint256 => bool) public IDclaimed;
```

in mintFromReserve function add this check : 

```solidity
require(!IDclaimed[claim.tokenId],"claimed already");
IDclaimed[claim.tokenId] = true;
```

