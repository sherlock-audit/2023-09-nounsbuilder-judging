Perfect Mint Mantis

medium

# Use `safeTransferFrom()` instead of `transferFrom()` for outgoing erc721 transfers

## Summary
**Acknowledgement**
*There may be a possible reason for using `transferFrom` in preference to  `safeTransferFrom`, as it would then be possible for a malicious bidder to stop the Auction from being settled, however this can be catered for as shown in the recommendation below.*

It is mostly recommended to use `safeTransferFrom` in place of `transferFrom` when transferring ERC721 tokens to ensure that the receiver is capable of supporting ERC721 tokens should the recipient be a contract.

Further information regarding the use of transferFrom and safeTransferFrom can be found on OpenZeppelin's site [here](https://docs.openzeppelin.com/contracts/4.x/api/token/erc721#IERC721-transferFrom-address-address-uint256-).

This is similar to other Sherlock audit report findings as listed below:
Issue [M-3](https://audits.sherlock.xyz/contests/18/report)

Issue [M-1](https://audits.sherlock.xyz/contests/3/report)

## Vulnerability Detail
Bidding on a token is not limited to EOA accounts, this allows contracts to action bids on tokens in the auction.

In the ` _settleAuction()` function which is called once an auction has expired, the logic to finalise the auction and distribute the NFT token to the winning bidder is implemented. Should the `_auction.highestBidder` address not be equal to address(0) the NFT token is transferred from the `Auction` contract to the address that has been saved in the `_auction.highestBidder` variable.

The call that is made is `transferFrom` which should preferrably be replaced with `safeTransferFom`.
## Impact
There is the potential that the winning bid was actioned via a contract. If the contract which submitted the bid does not implement the logic to handle ERC721 tokens there is the potential that NFT tokens could be lost.

## PoC
This shows that a contract can bid successfully on a token and win the bid. The token will then be transferred to the contract without it being able to support ERC721 tokens.
### I have also coded a POC to show that the recommendation works as expected should you wish me to share it.

Please create a file in the `test` directory called `ContractUser.sol`

Please copy/paste the code below into the file just created
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.16;

import { Auction } from "../src/auction/Auction.sol";
import { IAuction } from "../src/auction/IAuction.sol";
import "forge-std/console.sol";
contract ContractUser {
    Auction public auction;    
    uint256 tokenId;
    address owner;
    constructor(address _auction, uint256 _tokenId) payable {
        auction = Auction(_auction);
        owner = msg.sender;
        tokenId = _tokenId;
        
    }

    function actionBid(uint256 _tokenId) payable external{
        tokenId = _tokenId;
        auction.createBid{value:msg.value}(tokenId);
    }
    
    fallback() external payable{
        
    }
}
```
Please add the import for this new file in the test file called `Auction.t.sol`
```solidity
import "./contractUser.sol";
```

Please copy/paste the code below into the test file called `Auction.t.sol`
```solidity
function test_CreateContractBid() public {
        deployMock();
        

        vm.prank(founder);
        auction.unpause();

        uint256 beforeBidderBalance = bidder1.balance;
        uint256 beforeAuctionBalance = address(auction).balance;

        vm.startPrank(bidder1);
        ContractUser contractUser = new ContractUser(address(auction),2);
        contractUser.actionBid{value: 3 ether}(2);
        vm.stopPrank();

        console.log("[i] Address of contractUser contract is : ",address(contractUser));
        
        vm.warp(10 minutes + 1 seconds);
        vm.prank(founder);
        auction.settleCurrentAndCreateNewAuction();
        console.log("[i] Owner of winning bid NFT is         : ",auction.token().ownerOf(2));
    }
```
To run the test please run the command `forge test -vv --match-test test_CreateContractBid`

This will give the output below showing the contract successfully won the bid and was allocated the NFT token.

```text
Running 1 test for test/Auction.t.sol:AuctionTest
[PASS] test_CreateContractBid() (gas: 3977348)
Logs:
  [i] Address of contractUser contract is :  0x8398Bf88b37a623b38b9d4CacE5655F39D4e0FEB
  [i] Owner of winning bid NFT is         :  0x8398Bf88b37a623b38b9d4CacE5655F39D4e0FEB

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 9.64ms
```
## Code Snippet
https://github.com/sherlock-audit/2023-09-nounsbuilder/blob/main/nouns-protocol/src/auction/Auction.sol#L280
```solidity
    function _settleAuction() private {
        // Get a copy of the current auction
        Auction memory _auction = auction;

        // Ensure the auction wasn't already settled
        if (auction.settled) revert AUCTION_SETTLED();

        // Ensure the auction had started
        if (_auction.startTime == 0) revert AUCTION_NOT_STARTED();

        // Ensure the auction is over
        if (block.timestamp < _auction.endTime) revert AUCTION_ACTIVE();

        // Mark the auction as settled
        auction.settled = true;

        // If a bid was placed:
        if (_auction.highestBidder != address(0)) {
            // Cache the amount of the highest bid
            uint256 highestBid = _auction.highestBid;

            // If the highest bid included ETH: Pay rewards and transfer remaining amount to the DAO treasury
            if (highestBid != 0) {
                // Calculate rewards
                RewardSplits memory split = _computeTotalRewards(currentBidReferral, highestBid, founderReward.percentBps);

                if (split.totalRewards != 0) {
                    // Deposit rewards
                    rewardsManager.depositBatch{ value: split.totalRewards }(split.recipients, split.amounts, split.reasons, "");
                }

                // Deposit remaining amount to treasury
                _handleOutgoingTransfer(settings.treasury, highestBid - split.totalRewards);
            }

            // Transfer the token to the highest bidder
            token.transferFrom(address(this), _auction.highestBidder, _auction.tokenId);

            // Else no bid was placed:
        } else {
            // Burn the token
            token.burn(_auction.tokenId);
        }

        emit AuctionSettled(_auction.tokenId, _auction.highestBidder, _auction.highestBid);
    }
```
## Tool used
Manual Review

## Recommendation
It is recommended to use the safeTransferFrom call which implements a check that the recipient contract has the ability to deal with ERC721 tokens. A further mitigation can be implemented by using the try/catch concept to stop malicious bidders from stopping the auction from being settled by not implementing `onERC721Received` on purpose.

```solidity
             // Transfer the token to the highest bidder
-            token.transferFrom(address(this), _auction.highestBidder, _auction.tokenId);
-
+            try token.safeTransferFrom(address(this), _auction.highestBidder, _auction.tokenId){
+                //create new event to log transfer
+                emit TokenTransferred(_auction.highestBidder, _auction.tokenId);
+            } catch {
+                // you can consider logging the failure and perhaps transferring the token
+                // to a DAO address/contract so it can be sent to the winner of the bid
+                emit TokenTransferFailed(_auction.highestBidder, _auction.tokenId);
+            }
             // Else no bid was placed:
```
You would need to add the code below to the `IAuction.sol` contract.
```solidity
    /// @notice Emitted when the token is successfully transferred.
    /// @param winner The address of the winner of the bid
    /// @param tokenId The tokenId that was successfully bid on
    event TokenTransferred(address winner, uint256 tokenId);

    /// @notice Emitted when the token transfer fails.
    /// @param winner The address of the winner of the bid
    /// @param tokenId The tokenId that was successfully bid on
    event TokenTransferFailed(address winner, uint256 tokenId);
```
